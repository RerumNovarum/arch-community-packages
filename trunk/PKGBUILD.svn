# $Id: PKGBUILD 57680 2011-11-01 05:03:57Z ebelanger $
# Maintainer: Eric Bélanger <eric@archlinux.org>
# Contributor: Alexander Rødseth <rodseth@gmail.com>
pkgname=electricsheep
pkgver=2.7b34
pkgrel=1
pkgdesc="Screensaver that realize the collective dream of computers all over the internet"
arch=('x86_64' 'i686')
url="http://community.electricsheep.org/"
license=('GPL')
depends=('curl' 'expat' 'flam3' 'ffmpeg' 'mplayer' 'libglade' 'tinyxml' 'boost' 'gtk2' 'lua')
#subversion libgtk2.0 libgl1-mesa libavcodec libavformat libswscale liblua5 libcurl4-openssl libxml2 libjpeg8 libgtop2 libboost libboost-filesystem libboost-thread libtinyxml libglut3
makedepends=('xscreensaver')
optdepends=('xscreensaver: to use electricsheep with xscreensaver')
_fixed_revision=107

build() {
  cd "$srcdir"

  msg2 "Checking out svn revision $_fixed_revision (in lack of a Linux release for $pkgver)..."
  if [[ -d "$pkgname/.svn" ]]; then
    (cd "$pkgname" && svn up -r $_fixed_revision)
  else
    svn co \
      http://electricsheep.googlecode.com/svn/trunk/ \
      --config-dir ./ -r $_fixed_revision "$pkgname"
  fi

  msg2 "Setting up build directory..."
  rm -rf "$srcdir/$pkgname-build"
  cp -r "$srcdir/$pkgname" "$srcdir/$pkgname-build"
  cd "$srcdir/$pkgname-build/client_generic"

  #patch -p1 -i ../electricsheep-ffmpeg.patch
  msg2 "Configuring..."
  ./autogen.sh
  ./configure --prefix=/usr

  msg2 "Compiling..."
  make
}

package() {
  cd "$srcdir/$pkgname-build/client_generic"

  install -d "$pkgdir/usr/share/applications/screensavers"
  install -d "$pkgdir/usr/lib/gnome-screensaver/gnome-screensaver"
  make DESTDIR="$pkgdir" \
    GNOME_SCREENSAVER_DATADIR="$pkgdir/usr/share/applications/screensavers" \
    GNOME_SCREENSAVER_PRIVEXEDIR="$pkgdir/usr/lib/gnome-screensaver/gnome-screensaver" \
    install
  install -Dm644 electricsheep.desktop.kde \
    "$pkgdir/usr/share/kde4/services/ScreenSavers/electricsheep.desktop"
  install -Dm644 electricsheep.xml \
    "$pkgdir/usr/share/xscreensaver/config/electricsheep.xml"
  install -Dm755 electricsheep-saver \
    "$pkgdir/usr/bin/electricsheep-saver"
}

# vim:set ts=2 sw=2 et:

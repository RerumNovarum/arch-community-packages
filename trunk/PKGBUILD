# $Id$
# Maintainer: Evangelos Foutras <foutrelis@gmail.com>
# Contributor: Daniel Isenmann <daniel.isenmann [at] gmx.de>

pkgname=('gcompris' 'gcompris-data')
pkgver=8.4.13
pkgrel=1
pkgdesc="Educational software suite comprising of numerous activities for children aged 2 to 10"
arch=('i686' 'x86_64')
url="http://gcompris.net/"
license=('GPL3')
makedepends=('pkgconfig' 'intltool' 'gettext' 'perlxml' 'texi2html' 'gnuchess'
             'pygtk' 'gstreamer0.10-base-plugins' 'gnome-python' 'libxxf86vm'
             'python-pysqlite' 'pyxml' 'sdl_mixer' 'desktop-file-utils')
options=('!libtool')
source=(http://downloads.sourceforge.net/sourceforge/gcompris/$pkgname-$pkgver.tar.gz)
md5sums=('d4768c2c471da4f1eb12a82d37f54dc1')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr --disable-gnet
  make || return 1
}

package_gcompris() {
  depends=('gcompris-data' 'pygtk' 'gstreamer0.10-base-plugins' 'gnome-python'
           'libxxf86vm' 'python-pysqlite' 'pyxml' 'sdl_mixer')
  optdepends=('gnucap: for computer simulation within the electricity activity'
              'tuxpaint: for the painting activity'
              'gnuchess: for the chess activity')

  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install-exec
}

package_gcompris-data() {
  pkgdesc="Data files for GCompris"
  #arch=('any')
  depends=('desktop-file-utils')
  install=gcompris-data.install

  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install-data
}

# vim:set ts=2 sw=2 et:

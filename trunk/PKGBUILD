# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: tobias <tobias@archlinux.org>
# Contributor: Tobias Kieslich <tobias@justdreams.de>

pkgname=monica
pkgver=3.7
pkgrel=3
pkgdesc='Monitor calibration tool'
arch=('x86_64' 'i686')
url="http://www.pcbypaul.com/software/monica.html"
license=('BSD')
depends=('fltk' 'xorg-xgamma')
makedepends=('librsvg')
source=("ftp://ftp.archlinux.org/other/monica/$pkgname-$pkgver.tar.bz2" \
        'monica.svg')
sha256sums=('da0d7762f5a2eed482e8e2f87762d3ac1f013d5c99bd99b34083b69f03492036'
            '30497d65dfe1038d6c6d017ea00f93ae03d736ab4210bc00df730e37325887c9')

prepare() {
  cd "$pkgname-$pkgver"

  gendesk -n --pkgname "$pkgname" --pkgdesc "$pkgdesc" \
    --categories 'Application;Settings;System'
}

build() {
  cd "$pkgname-$pkgver"

  make
}

package() {
  cd "$pkgname-$pkgver"

  install -Dm755 "$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -Dm644 "$pkgname.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
  rsvg-convert -w 64 -h 57 -f png -o "$pkgdir/usr/share/pixmaps/$pkgname.png" \
    "$srcdir/$pkgname.svg"
  install -Dm644 licence "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:

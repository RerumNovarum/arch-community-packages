# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Daniel Isenmann <daniel.isenmann [at] gmx.de>

pkgname=('gcompris' 'gcompris-data')
pkgver=9.6.1
pkgrel=1
pkgdesc="Educational software suite comprising of numerous activities for children aged 2 to 10"
arch=('i686' 'x86_64')
url="http://gcompris.net/"
license=('GPL3')
makedepends=('texinfo' 'texi2html' 'intltool' 'gettext' 'gnuchess'
             'pygtk' 'pyxml' 'librsvg' 'gnet' 'python-pysqlite'
             'gstreamer0.10-base-plugins')
options=('!libtool')
source=(http://downloads.sourceforge.net/project/$pkgname/$pkgver/$pkgname-$pkgver.tar.gz)
md5sums=('2fb1dbb331166dd19d0a758220e4caf0')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr
  make
}

package_gcompris() {
  depends=('gcompris-data' 'pygtk' 'pyxml' 'librsvg' 'gnet' 'python-pysqlite'
           'gstreamer0.10-base-plugins')
  optdepends=('gnucap: for computer simulation within the electricity activity'
              'tuxpaint: for the painting activity'
              'gnuchess: for the chess activity')

  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install-exec
}

package_gcompris-data() {
  pkgdesc="Data files for GCompris"
  #arch=('any')
  install=gcompris-data.install

  cd "$srcdir/gcompris-$pkgver"
  make DESTDIR="$pkgdir" install-data

  # Place GCompris menu entry under Education
  sed -i 's|^Categories=.*|Categories=Education;|' \
    "$pkgdir/usr/share/applications/gcompris.desktop"
}

# vim:set ts=2 sw=2 et:

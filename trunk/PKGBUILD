# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: grimi <grimi at poczta dot fm>

pkgname=urbanterror
pkgver=4.1
pkgrel=9
pkgdesc="A team-based tactical shooter based on the Quake 3 Engine"
arch=('i686' 'x86_64')
url="http://www.urbanterror.net"
license=('GPL2')
depends=('sdl' 'openal' 'curl' 'urbanterror-data')
makedepends=('mesa')
source=("http://ftp.snt.utwente.nl/pub/games/urbanterror/iourbanterror/source/complete/ioUrbanTerrorSource_2007_12_20.zip" \
	"urbanterror_home.patch" "default-curl_lib.patch" \
	"cmd.c.patch" "g_cmds.c.patch" "qcommon.h.patch" "sv_client.c.patch" "sv_main.c.patch" \
	"urbanterror.sh" "urbanterror-server.sh" "urbanterror.desktop" "urbanterror.png")
md5sums=('4dbe5a03a10f23138f42b5e0dc39598b'
         'a1d992453aa64108c0b7b8304484c21f'
         'e917e9fe1210598885665e13a40c11d8'
         '0e5cc83478b8d37f59740620e5eccd21'
         '1120e5f2a3a5944f219a432e5eb9bcaa'
         'a12059253d555fdb169d2717899b47e6'
         '57241e730301a78d1b13de5b9cc9f68a'
         '8b24a2aa5446e9c6ccbc04e844b189ab'
         'e8b2665e5ed81a8ca0fa18f32ea0b2d9'
         'b9b478aa27cb07280ff00378b157e90d'
         '08a99f4d7ad63024bc886e118ddcbc0f'
         'f9a57d898df73f43c6a85c8d8cc455ba')
# disable buggy SMP compilation
MAKEFLAGS=-j1

build() {
	# Patch urbanterror home.
	cd $srcdir
	patch -Np0 -i urbanterror_home.patch

	# Patch and compile client.
	cd $srcdir/ioUrbanTerrorClientSource
	patch -Np0 -i $srcdir/default-curl_lib.patch
	make
	
	# Patch and compile server.
	cd $srcdir/ioUrbanTerrorServerSource
	patch -Np1 -i $srcdir/cmd.c.patch
	patch -Np1 -i $srcdir/g_cmds.c.patch
	patch -Np1 -i $srcdir/qcommon.h.patch
	patch -Np1 -i $srcdir/sv_client.c.patch
	patch -Np1 -i $srcdir/sv_main.c.patch
  sed -i 's/master.urbanterror.net/master.urbanterror.info/g' \
    $srcdir/ioUrbanTerrorServerSource/code/qcommon/qcommon.h \
    $srcdir/ioUrbanTerrorClientSource/code/qcommon/qcommon.h
  sed -i 's/master2.urbanterror.net/master2.urbanterror.info/g' \
    $srcdir/ioUrbanTerrorServerSource/code/qcommon/qcommon.h \
    $srcdir/ioUrbanTerrorClientSource/code/qcommon/qcommon.h
	make
}

package() {
  install -d $pkgdir/opt/urbanterror

  cd $pkgdir/opt/urbanterror

  # Copy binaries.
  install -m755 $srcdir/ioUrbanTerrorClientSource/build/release-linux-*/ioUrbanTerror.* ioUrbanTerror
  install -m755 $srcdir/ioUrbanTerrorServerSource/build/release-linux-*/ioUrTded.* ioUrTded

  # Copy desktop launcher.
  install -Dm644 $srcdir/urbanterror.desktop $pkgdir/usr/share/applications/urbanterror.desktop
  install -Dm644 $srcdir/urbanterror.png $pkgdir/usr/share/pixmaps/urbanterror.png

  # Copy launch scripts.
  install -Dm755 $srcdir/urbanterror.sh $pkgdir/usr/bin/urbanterror
  install -Dm755 $srcdir/urbanterror-server.sh $pkgdir/usr/bin/urbanterror-server
}

# vim: sw=2:ts=2 et:

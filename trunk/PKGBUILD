# $Id$
# Maintainer: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: Zygmunt Krynicki <me at zygoon dot pl>

pkgname=snap-confine
pkgver=1.0.38
pkgrel=2
pkgdesc="Confinement system for snap applications"
arch=('i686' 'x86_64')
url="git://github.com/snapcore/snap-confine"
license=('GPL3')
depends=('bash' 'libseccomp' 'libsystemd')
makedepends=('python-docutils' 'systemd')
checkdepends=('indent' 'shellcheck')
source=("https://github.com/snapcore/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.gz"
        "dont-mount-media.patch")
md5sums=('1ab7bcb5074f47b3dc818c0d06c1eb37'
         '89b887eccd0bf96b7b88f2f86329e085')

prepare() {
  cd "$pkgname-$pkgver"

  # Revert "add /media to source_mounts to allow access to mounted devices"
  # since Arch Linux doesn't have /media directory anymore
  patch -Np1 -i "$srcdir"/dont-mount-media.patch
}

build() {
  cd "$pkgname-$pkgver"

  # Confinement is disabled because arch doesn't yet have all the supporting
  # kernel patches.
  # Nvidia support is enabled to support proprietary nvidia drivers.
  ./configure \
    --prefix=/usr \
    --libexecdir=/usr/lib/snapd \
    --disable-apparmor \
    --enable-nvidia-arch
  make
}

check() {
  cd "$pkgname-$pkgver"
  make -k check
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}

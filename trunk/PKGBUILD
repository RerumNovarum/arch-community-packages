# $Id$
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Roman Kyrylych <roman@archlinux.org>
# Contributor: cdhotfire <cdhotfire@gmail.com>

pkgname=rawstudio
pkgver=1.2
pkgrel=6
pkgdesc="An open source raw-image converter written in GTK+"
arch=('i686' 'x86_64')
license=('GPL')
url="http://rawstudio.org/"
depends=('gconf' 'libxml2' 'lcms' 'libjpeg' 'libtiff' 'desktop-file-utils' 'exiv2')
install=${pkgname}.install
source=(http://rawstudio.org/files/release/${pkgname}-${pkgver}.tar.gz
	'rawstudio-const.patch')
md5sums=('25399cd80294fa44220a8940fc77689f' '6692b460e838ea56462aff7626b2dcef')
sha1sums=('9ce96c40e0b8feaf4627c14310646782578d4da1' '95d83914d5ae156f0fe747945516ae6cc93adac5')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	patch -Np1 -i "${srcdir}/rawstudio-const.patch"
	./configure --prefix=/usr
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make prefix="${pkgdir}/usr" install
}

# $Id$
# Contributor: James Rayner <james@archlinux.org>
# Contributor: Todd Maynard <arch@toddmaynard.com>

pkgname=python-cheetah
pkgver=2.4.4
pkgrel=2
pkgdesc="A Python-powered template engine and code generator"
arch=('i686' 'x86_64')
url="http://www.cheetahtemplate.org/"
license=('custom')
depends=('python2' 'python-markdown')
makedepends=('python2-distribute')
optdepends=('python2-pygments: for the CodeHighlighter filter')
provides=('cheetah')
conflicts=('cheetah')
source=(http://pypi.python.org/packages/source/C/Cheetah/Cheetah-${pkgver}.tar.gz)
md5sums=('853917116e731afbc8c8a43c37e6ddba')

build() {
  cd "${srcdir}/Cheetah-${pkgver}"
  python2 setup.py build
}

package() {
  cd "${srcdir}/Cheetah-${pkgver}"
  python2 setup.py install --root="${pkgdir}"
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  find $pkgdir/usr/lib/python2.7/site-packages/Cheetah/ -name '*.py' | \
    xargs sed -i "s|#!/usr/bin/env python|#!/usr/bin/env python2|"
}

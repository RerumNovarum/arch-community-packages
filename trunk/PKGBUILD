# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Tobias Kieslich <tobias@archlinux.org>
# Contributor: Martin Sugioarto <nakal@web.de>

pkgname=gimp-ufraw
pkgver=0.18
pkgrel=6
pkgdesc='Standalone or gimp plugin converter for raw files'
url='http://ufraw.sourceforge.net/'
arch=('x86_64' 'i686')
license=('GPL')
depends=('gtkimageview' 'exiv2' 'lcms' 'bzip2' 'desktop-file-utils' 'cfitsio' 'lensfun')
makedepends=('gimp' 'cinepaint')
optdepends=('gimp: to use the gimp import plugin for raw images'
            'cinepaint: to use the cinepaint import plugin for raw images')
install=gimp-ufraw.install
source=("http://downloads.sourceforge.net/ufraw/ufraw-$pkgver.tar.gz")
sha256sums=('24fe739c61e9e14de1900008477c8662553e93427569c1d595556d440e6b9205')

build() {
  cd "$srcdir/ufraw-$pkgver"

  ./configure --prefix=/usr \
              --enable-extras \
              --enable-mime \
              --enable-openmp \
	      --with-gimp \
	      --with-cinepaint \
	      --enable-contrast
  sed -i "s/-ffast-math -fomit-frame-pointer -W -Wall -O3/${CFLAGS}/" Makefile
  make
}

package() {
  cd "$srcdir/ufraw-$pkgver"

  make DESTDIR="$pkgdir" install
  rm -f "$pkgdir/usr/bin/dcraw"
}

# vim:set ts=2 sw=2 et:

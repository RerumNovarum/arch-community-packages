# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=libmongoc
pkgver=1.3.0
pkgrel=1
pkgdesc='A client library written in C for MongoDB'
arch=('i686' 'x86_64')
url='http://www.mongodb.org/display/DOCS/C+Language+Center'
license=('Apache')
depends=('libbson' 'libsasl')
checkdepends=('mongodb')
source=("$pkgname-$pkgver.tar.gz::https://github.com/mongodb/mongo-c-driver/archive/$pkgver.tar.gz")
sha512sums=('5cc473480e7b49cad6a4934d0ebc50a1c17f16079f6b4d3d562e522deaa93dcad98d60951aa624305686b4b4ca4eeae357a1d988e66bd346cdd336852d3fcde1')

build() {
    cd "$srcdir/mongo-c-driver-$pkgver"
    PTHREAD_LIBS=-pthread ./autogen.sh --prefix=/usr
    make
}

check() {
    _mongod_run() {
        # Start a mongod instance for test
        rm -rf "$srcdir/mongo_tmp"
        mkdir "$srcdir/mongo_tmp"
        rm -f "$srcdir/mongo_tmp.pid"
        [[ "$CARCH" == "i686" ]] && _dbengine="--storageEngine=mmapv1"
        mongod --bind_ip localhost --port 27017 --dbpath "$srcdir/mongo_tmp" --journal $_dbengine \
             --nohttpinterface --noauth --smallfiles --nssize 1 --fork --pidfilepath "$srcdir/mongo_tmp.pid" --logpath "$srcdir/mongo_tmp.log"

        "$@"
        kill $(cat "$srcdir/mongo_tmp.pid")
    }

    cd "$srcdir/mongo-c-driver-$pkgver"
    _mongod_run make test
}

package() {
    cd "$srcdir/mongo-c-driver-$pkgver"
    make DESTDIR="$pkgdir/" install
}

# $Id$
# Maintainer : Martin Wimpress <code@flexion.org>

_ver=1.10
pkgbase=mate-power-manager
pkgname=(${pkgbase}-gtk3)
pkgver=${_ver}.0
pkgrel=1
pkgdesc="Power management tool for the MATE desktop (GTK3 version [EXPERIMENTAL])"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('dbus-glib' 'dconf' 'gtk3' 'libcanberra' 'libgnome-keyring'
         'libnotify' 'libunique3' 'mate-desktop-gtk3>=1.10' 'upower')
makedepends=('docbook2x' 'docbook-xml' 'mate-common' 'mate-panel-gtk3' 'xmlto' 'yelp-tools')
optdepends=('mate-panel-gtk3: Set brightness and inhibit power management from the panel'
            'yelp: for reading MATE help documents')
source=("http://pub.mate-desktop.org/releases/${_ver}/${pkgbase}-${pkgver}.tar.xz")
groups=('mate-extra-gtk3')
conflicts=("${pkgbase}")
sha1sums=('7540735374e5892c26a9c06f9c973742ef873e74')
install=${pkgbase}.install

prepare() {
    cd "${srcdir}/${pkgbase}-${pkgver}"
    #Work around a problem where the .sgml fails to parse.
    sed -e 's:@HAVE_DOCBOOK2MAN_TRUE@.*::' -i man/Makefile.in
}

build() {
    cd "${srcdir}/${pkgbase}-${pkgver}"
    ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib/${pkgbase} \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --sbindir=/usr/bin \
        --enable-applets \
        --enable-unique \
        --with-gtk=3.0 \
        --disable-strict
    make
}

package_mate-power-manager-gtk3() {
    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}

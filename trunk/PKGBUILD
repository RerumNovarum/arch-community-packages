# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Det <nimetonmaili at gmail a-dot com>
# Contributor: Godane aka Christopher Rogers <slaxemulator@gmail.com>

pkgname=unetbootin
pkgver=575
pkgrel=1
arch=('x86_64' 'i686')
license=('GPL')
pkgdesc="Create bootable Live USB drives"
url="http://unetbootin.sourceforge.net/"
depends=('syslinux' 'p7zip' 'qt' 'mtools' 'xorg-xmessage' 'desktop-file-utils' 'gtk-update-icon-cache')
optdepends=('gksu: GUI authentication'
            'kdebase-runtime: GUI authentication')
install=$pkgname.install
source=("http://downloads.sourceforge.net/project/$pkgname/UNetbootin/$pkgver/$pkgname-source-$pkgver.tar.gz"
        "$pkgname.sh")
sha256sums=('a098a8683ee724a6dcc82027418d5bef15d1d88d81cafc7b98aa46f5d4b176ba'
            'e735780e209e2654c49528ee58b84c1c56cdc87f61efe23486e371ea70e240b8')

build() {
  cd "$srcdir"

  lupdate "$pkgname.pro"
  lrelease "$pkgname.pro"
  qmake
  make
}

package() {
  cd "$srcdir"

  install -Dm755 "$pkgname.sh" "$pkgdir/usr/bin/$pkgname"
  install -Dm755 "$pkgname" "$pkgdir/usr/bin/$pkgname.elf"
  mkdir -p "$pkgdir/usr/share/$pkgname"
  install -m644 "$pkgname"_*.qm "$pkgdir/usr/share/$pkgname/"
  install -Dm644 "$pkgname.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
  for i in 16 22 24 32 48 256; do
    install -D "${pkgname}_${i}.png" \
      "$pkgdir/usr/share/icons/hicolor/${i}x${i}/apps/$pkgname.png"
  done
}

# vim:set ts=2 sw=2 et:

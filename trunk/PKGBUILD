# $Id$
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Connor Behan <connor.behan@gmail.com>
# Contributor: henning mueller <henning@orgizm.net>

pkgname=audit
pkgver=2.2.3
pkgrel=2
pkgdesc='User space utilities for storing and searching the audit records generated by the audit subsystem in the Linux kernel.'
url=http://people.redhat.com/sgrubb/$pkgname
arch=(i686 x86_64)
depends=(krb5 libcap-ng)
makedepends=(libldap swig linux-headers python2)
license=(GPL)
options=(!libtool emptydirs)
backup=(
	etc/libaudit.conf
	etc/audit/audit.rules
	etc/audit/auditd.conf
	etc/audisp/audispd.conf
	etc/audisp/audisp-remote.conf
	etc/audisp/zos-remote.conf
	etc/audisp/plugins.d/af_unix.conf
	etc/audisp/plugins.d/audispd-zos-remote.conf
	etc/audisp/plugins.d/au-remote.conf
	etc/audisp/plugins.d/syslog.conf
)
source=(
	$url/$pkgname-$pkgver.tar.gz
	auditd.service
	python2.patch
)

build() {
	cd "$srcdir/$pkgname-$pkgver"
	patch -p0 -i "$srcdir/python2.patch"
	./configure \
		--prefix=/usr \
		--sbindir=/usr/bin \
		--sysconfdir=/etc \
		--libexecdir=/usr/lib/audit \
		--with-python=yes \
		--enable-gssapi-krb5=yes \
		--with-libcap-ng=yes #--with-apparmor=yes
	make
}

package() {
	cd "$srcdir/$pkgname-$pkgver"
	make DESTDIR="$pkgdir" install

	install -Dm755 "$srcdir/auditd.service" "$pkgdir/usr/lib/systemd/system/auditd.service"

	cd "$pkgdir"
	install -d var/log/audit
	rm -rf etc/rc.d/init.d etc/sysconfig

	sed -ri 's| /sbin| /usr/bin|' etc/audit/*.conf etc/audisp/plugins.d/*.conf
}

sha256sums=('2fc8f97020121593f516dc011ef61f39043c4cea9b2cb9ab3849bf9e41dedf02'
            '83ebda69bbc2d4058d7baddbf91ab8a2aafd70e64ceb2137d9c9335f8d8d4b83'
            '6adadb405bebe99bb6a50e80e7c1a356996626e135d566283448fe4019fff3a4')

# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Lukas Jirkovsky <l.jirkovsky@gmail.com>
pkgname=ufoai
pkgver=2.5.0
_snapshot=58580c9b55a69ba65fad565c7a7bd29617736f6c
pkgrel=2
pkgdesc="In UFO: Alien Invasion you fight aliens trying to capture Earth"
arch=('i686' 'x86_64')
url="http://ufoai.ninex.info/"
license=('GPL')
depends=('libjpeg' 'libgl' 'libpng' 'curl' 'sdl_image' 'sdl_mixer' 'sdl_ttf' 'xvidcore' 'ufoai-data' 'glu')
makedepends=('mesa' 'git')
# use snapshot
source=(http://sourceforge.net/code-snapshots/git/u/uf/ufoai/code.git/ufoai-code-$_snapshot.zip)
md5sums=('c8293cbf00883320d7def71c11c90992')

prepare() {
  cd ufoai-code-$_snapshot

  # disable building of various data files
  sed -i 's|include build/maps.mk|#&|' Makefile
  sed -i 's|include build/models.mk|#&|' Makefile
  sed -i 's|include build/data.mk|#&|' Makefile
}

build() {
  cd ufoai-code-$_snapshot

  ./configure --prefix=/usr --datadir=/usr/share/ufoai --enable-release --localedir=/usr/share/ufoai/base/i18n/
  make
}

package() {
  cd ufoai-code-$_snapshot

  install -Dm755 ufo "$pkgdir"/usr/bin/ufo
  install -Dm755 ufoded "$pkgdir"/usr/bin/ufoded
  install -Dm755 ufo2map "$pkgdir"/usr/bin/ufo2map
  install -Dm755 ufomodel "$pkgdir"/usr/bin/ufomodel
  install -Dm644 base/game.so "$pkgdir"/usr/share/ufoai/base/game.so

  install -Dm644 debian/ufoai.desktop "$pkgdir"/usr/share/applications/ufoai.desktop
  install -Dm644 debian/ufoai-safe.desktop "$pkgdir"/usr/share/applications/ufoai-safe.desktop
  install -Dm644 debian/ufoded.desktop "$pkgdir"/usr/share/applications/ufoded.desktop

  sed -i 's|/usr/games/||' "$pkgdir"/usr/share/applications/*

  install -Dm644 debian/ufoai.xpm "$pkgdir"/usr/share/pixmaps/ufoai.xpm
  install -Dm644 debian/ufoded.xpm "$pkgdir"/usr/share/pixmaps/ufoded.xpm
}

# vim: sw=2:ts=2 et:

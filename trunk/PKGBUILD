# $Id$
# Maintainer: Mateusz Herych <heniekk@gmail.com>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Sergio Jovani Guzman <moret.sjg@gmail.com>
# Contributor: royrocks <royrocks13@gmail.com>
pkgname=tuxpaint
pkgver=0.9.21
pkgrel=5
pkgdesc="Free drawing program designed for young children"
arch=('x86_64' 'i686')
url="http://www.tuxpaint.org"
license=('GPL')
backup=('etc/tuxpaint/tuxpaint.conf')
depends=('python2' 'fribidi' 'sdl_ttf' 'sdl_mixer' 'sdl_image' 'sdl_pango' 'libpaper' 'librsvg')
source=("http://downloads.sourceforge.net/sourceforge/tuxpaint/$pkgname-$pkgver.tar.gz")
sha256sums=('082a179a3cb44ee17af47421bc92125a890846e4')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # python2 fix
  for file in docs/zh_tw/mkTuxpaintIM.py fonts/locale/zh_tw_docs/maketuxfont.py; do
    sed -i 's_#!/usr/bin/env python_#!/usr/bin/env python2_' $file
  done

  echo "Categories=Game;KidsGame;Graphics;RasterGraphics;" >> src/tuxpaint.desktop
  make PREFIX=/usr
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make PREFIX=/usr DESTDIR="$pkgdir" CONFDIR="$pkgdir/etc" MAN_PREFIX="$pkgdir/usr/share/man" X11_ICON_PREFIX="$pkgdir/usr/share/pixmaps" KDE_PREFIX="" KDE_ICON_PREFIX="" install
  install -Dm644 "$srcdir/$pkgname-$pkgver/src/tuxpaint.desktop" "$pkgdir/usr/share/applications/tuxpaint.desktop"
  chmod -R 644 "$pkgdir/usr/share/doc/tuxpaint-dev/"*
  chmod 755 "$pkgdir/usr/share/doc/tuxpaint-dev/html"
  mkdir -p "$pkgdir/etc/tuxpaint"
  mv "$pkgdir/etc/tuxpaint.conf" "$pkgdir/etc/tuxpaint/"
}

# vim:set ts=2 sw=2 et:

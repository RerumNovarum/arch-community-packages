# $Id$
# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
pkgname=luxrays
pkgver=0.8
_pkgver=ed424ec77571
pkgrel=2
epoch=0
pkgdesc="Accelerate the ray intersection process by using GPUs"
arch=('i686' 'x86_64')
url="http://www.luxrender.net/"
license=('GPL')
depends=('freeimage' 'freeglut' 'glew')
makedepends=('cmake' 'boost')
source=(https://bitbucket.org/luxrender/luxrays/get/$_pkgver.tar.bz2)
md5sums=('386a3549376ac8240065232ae3294da4')

build() {
  cd "$srcdir/luxrender-$pkgname-$_pkgver"

  export CXXFLAGS="$CXXFLAGS -lpthread"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_RPATH=ON \
    -DLUXRAYS_DISABLE_OPENCL=ON \
    .
  make
}

package() {
  cd "$srcdir/luxrender-$pkgname-$_pkgver"

  install -d -m755 "$pkgdir"/usr/{bin,include,lib}
  install -m755 bin/* "$pkgdir"/usr/bin
  install -m644 lib/* "$pkgdir"/usr/lib
  cp -a include/luxrays "$pkgdir"/usr/include
}

# vim:set ts=2 sw=2 et:

# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-artwork-themes
pkgver=15.12.2
_commit=6c1c4f5bcbe070337352f634f8473a76e3efe59c
_desktopbaserev=77f030f19ce5b04da5634d1a7275feb4487b7fb3
pkgrel=1
pkgdesc='Deepin artwork themes'
arch=('any')
license=('LGPL3')
url="https://gitcafe.com/Deepin/deepin-artwork-themes"
depends=('deepin-icon-theme')
makedepends=('python2' 'git')
groups=('deepin')
source=("git+https://github.com/linuxdeepin/deepin-artwork-themes.git#commit=$_commit"
        "git+https://github.com/linuxdeepin/deepin-desktop-base.git#commit=$_desktopbaserev")
sha256sums=('SKIP'
            'SKIP')

prepare() {
  cd $pkgname

  # fix default background url
  sed -i 's#^BackgroundFile=.*$#BackgroundFile=file:///usr/share/backgrounds/deepin_default_background.jpg#' themes/deepin/theme.ini
}

build() {
  cd $pkgname
  make build
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install

  install -d "${pkgdir}"/usr/share/backgrounds/
  cp "${srcdir}"/deepin-desktop-base/files/desktop.jpg "${pkgdir}"/usr/share/backgrounds/deepin_default_background.jpg
}

# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>

pkgname=processing
pkgver=1.5.1
pkgrel=1
pkgdesc='Programming language and environment for programming images, animation, and interactions'
arch=('any')
url='http://www.processing.org/'
license=('GPL' 'LGPL')
depends=('libgl' 'libxxf86vm')
optdepends=('lib32-libxtst: for x86_64 systems')
makedepends=('gendesk')
options=(!strip)
source=("http://$pkgname.googlecode.com/files/$pkgname-$pkgver-linux.tgz"
        "$pkgname.png::http://wiki.$pkgname.org/skins/${pkgname}skin/${pkgname}_wiki_logo.png")
sha256sums=('970a8a36d9fd77764a9d369865140825956eba006c37e83d331c576c7e6f595e'
            '019ae700333409acbc628779747ab8f153f6647de25e4de3a9bd4a42d0d8056a')

build() {
    cd "$srcdir"
    gendesk
}

package() {
    cd "$srcdir/$pkgname-$pkgver"

    install -d "$pkgdir/usr/share/$pkgname/" "$pkgdir/usr/bin/"
    cp -r ./* "$pkgdir/usr/share/$pkgname/"
    chmod a+x "$pkgdir/usr/share/$pkgname/$pkgname"
    ln -s /usr/share/$pkgname/$pkgname "$pkgdir/usr/bin/$pkgname"
    install -Dm644 "$srcdir/$pkgname.png" \
      "$pkgdir/usr/share/pixmaps/$pkgname.png"
    install -Dm644 "$srcdir/$pkgname.desktop" \
      "$pkgdir/usr/share/applications/$pkgname.desktop"
}

# vim:set ts=2 sw=2 et:

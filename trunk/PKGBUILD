# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Adrián Chaves Fernández, aka Gallaecio <adriyetichaves@gmail.com>
# Contributor: Shahar Weiss <sweiss4@gmx.net> (build() code from his TORCS' PKGBUILD)

pkgname=speed-dreams
pkgver=1.4.0
pkgver2=1.4.0-r2307 # For managing files and directories
pkgrel=3
pkgdesc="Speed Dreams is a fork of TORCS which aims to implement exciting new features, improving visual and physics realism."
arch=('i686' 'x86_64')
url="http://speed-dreams.sourceforge.net/"
license=('GPL2')
depends=('freealut' 'freeglut' 'libpng' 'libxi' 'libxmu' 'libxrandr' 'plib>=1.8.3' 'libjpeg' 'zlib' 'enet')
source=("http://sourceforge.net/projects/${pkgname}/files/1.4.0/${pkgname}-${pkgver2}-src.tar.bz2/download"
	"${pkgname}.desktop"
	"${pkgname}.sh")
md5sums=('92c324c2c47a598dea9278d9a66c5b78'
         'f89654510f1b558054eb17e778f1af7f'
         'f0f5040a3746084d9967e055afa35f32')

build() {
  cd "$srcdir/${pkgname}-${pkgver}-src"
  
  unset LDFLAGS # Fix reported by Ionuț Bîru

  ./configure --prefix="/opt/${pkgname}" --x-includes=/usr/include --x-libraries=/usr/lib 
  make
}

package() {
  cd "$srcdir/${pkgname}-${pkgver}-src"
  
  make DESTDIR="${pkgdir}/" install datainstall

  install -D -m644 "icon.png" "${pkgdir}/usr/share/pixmaps/speed-dreams.png" && \
  install -D -m644 "../${pkgname}.desktop" "${pkgdir}/usr/share/applications/speed-dreams.desktop" && \
  install -D -m755 "../${pkgname}.sh" "${pkgdir}/etc/profile.d/speed-dreams.sh"

  find "${pkgdir}/" -type d -exec chmod 755 {} \;
}

# vim:set ts=2 sw=2 et:

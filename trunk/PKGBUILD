# $Id$
# Maintainer: Brad Fanella <bradfanella@archlinux.us>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: tocer.deng <tocer.deng@gmail.com>

pkgname=apvlv
pkgver=0.1.1
pkgrel=1
pkgdesc="A PDF Viewer which behaves like Vim"
arch=('i686' 'x86_64')
url="http://code.google.com/p/apvlv/"
license=('GPL')
depends=('poppler-glib' 'libdjvu')
makedepends=('cmake')
source=(http://apvlv.googlecode.com/files/$pkgname-${pkgver}-Source.tar.gz
        apvlv-poppler015.patch)
md5sums=('221b25fd34ebf398047424459d8e5131'
         '809aea91e13032663d6533892757891a')

build() {
  cd $srcdir/$pkgname-$pkgver-Source

  patch -Np0 -i $srcdir/apvlv-poppler015.patch

  mkdir -p build
  cd build

  cmake -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DAPVLV_WITH_UMD=no  ..
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver-Source/build

  make DESTDIR=$pkgdir install
}

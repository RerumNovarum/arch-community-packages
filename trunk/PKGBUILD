# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=haskell-gtk
pkgver=0.12.1
pkgrel=1
pkgdesc="Binding to the gtk library for Gtk2Hs."
url="http://hackage.haskell.org/package/gtk"
license=('LGPL2.1')
arch=('i686' 'x86_64')
depends=('ghc' 'gtk2' "haskell-pango=$pkgver" 'haskell-mtl=2.0.1.0')
makedepends=("haskell-gtk2hs-buildtools=$pkgver")
provides=('gtk2hs-gtk')
replaces=('gtk2hs-gtk')
options=('strip')
install=gtk2hs-gtk.install
source=(http://hackage.haskell.org/packages/archive/gtk/$pkgver/gtk-$pkgver.tar.gz)
md5sums=('57b9432ea8d4d7ef3b96f0c19f805ed8')

build() {
    cd ${srcdir}/gtk-${pkgver}
    runhaskell Setup configure -O -p --enable-split-objs --enable-shared \
      --prefix=/usr --docdir=/usr/share/doc/haskell-gtk \
      --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup haddock
    runhaskell Setup register   --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    _ghcver=`pacman -Q ghc | cut -f2 -d\  | cut -f1 -d-`
    depends=("ghc=${_ghcver}" 'gtk2' 'gtk2hs-pango=0.12.0' 'haskell-mtl=2.0.1.0')

    cd ${srcdir}/gtk-${pkgver}
    install -D -m744 register.sh   ${pkgdir}/usr/share/haskell/gtk2hs-gtk/register.sh
    install    -m744 unregister.sh ${pkgdir}/usr/share/haskell/gtk2hs-gtk/unregister.sh
    install -d -m755 ${pkgdir}/usr/share/doc/ghc/html/libraries
    ln -s /usr/share/doc/haskell-gtk/html ${pkgdir}/usr/share/doc/ghc/html/libraries/gtk2hs-gtk
    runhaskell Setup copy --destdir=${pkgdir}
}

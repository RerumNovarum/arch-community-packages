# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Det <nimetonmaili at gmail a-dot com>
# Contributor: Godane aka Christopher Rogers <slaxemulator@gmail.com>

pkgname=unetbootin
pkgver=581
pkgrel=1
arch=('x86_64' 'i686')
license=('GPL')
pkgdesc="Create bootable Live USB drives"
url="http://unetbootin.sourceforge.net/"
depends=('syslinux' 'p7zip' 'qt' 'mtools' 'xorg-xmessage' 'desktop-file-utils' 'gtk-update-icon-cache')
optdepends=('gksu: GUI authentication'
            'kdebase-runtime: GUI authentication')
install=$pkgname.install
source=("http://downloads.sourceforge.net/project/$pkgname/UNetbootin/$pkgver/$pkgname-source-$pkgver.tar.gz"
        "$pkgname.sh")
sha256sums=('b8bf5972653c023c159358f0e4c743dfaf1c6c27f28fd15cd97739d08c751509'
            'c35329011559288b964cc4c927c969fe35cb8cb3f88020583affe359fc72de10')

build() {
  cd "$srcdir"

  lupdate "$pkgname.pro"
  lrelease "$pkgname.pro"
  qmake
  make
}

package() {
  cd "$srcdir"

  install -Dm755 "$pkgname.sh" "$pkgdir/usr/bin/$pkgname"
  install -Dm755 "$pkgname" "$pkgdir/usr/bin/$pkgname.elf"
  mkdir -p "$pkgdir/usr/share/$pkgname"
  install -m644 "$pkgname"_*.qm "$pkgdir/usr/share/$pkgname/"
  install -Dm644 "$pkgname.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
  for i in 16 22 24 32 48 256; do
    install -D "${pkgname}_${i}.png" \
      "$pkgdir/usr/share/icons/hicolor/${i}x${i}/apps/$pkgname.png"
  done
}

# vim:set ts=2 sw=2 et:

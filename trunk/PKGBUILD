# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Vinay S Shastry <vinayshastry@gmail.com>

pkgname=swftools
pkgver=0.9.1
pkgrel=1
pkgdesc="A collection of SWF manipulation and creation utilities"
arch=('i686' 'x86_64')
url="http://www.swftools.org/"
license=('GPL')
depends=('avifile' 'giflib' 'freeglut' 'lame' 't1lib' 'libjpeg' 'fontconfig')
makedepends=('bison' 'flex' 'zlib')
changelog=$pkgname.changelog
source=(http://www.swftools.org/$pkgname-$pkgver.tar.gz)
md5sums=('72dc4a7bf5cdf98c28f9cf9b1d8f5d7a')

build() {
  cd ${srcdir}/$pkgname-$pkgver

  ./configure --prefix=/usr
  make
}

package() {
  cd ${srcdir}/$pkgname-$pkgver

  make prefix=${pkgdir}/usr install

  cd ${pkgdir}/usr/share/$pkgname/swfs
  rm -f default_loader.swf default_viewer.swf
  ln -s tessel_loader.swf default_loader.swf
  ln -s simple_viewer.swf default_viewer.swf
}

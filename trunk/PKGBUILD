# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Tom K <tomk@runbox.com>

pkgname=perl-libapreq2
pkgver=2.12
pkgrel=9
pkgdesc="A safe, standards-compliant, high-performance library used for parsing HTTP cookies, query-strings and POST data."
arch=('i686' 'x86_64')
url="http://search.cpan.org/dist/libapreq2"
depends=('mod_perl' 'apr-util')
makedepends=('perl-extutils-xsbuilder' 'perl-version')
license=("GPL")
options=('!libtool' '!makeflags')
source=(http://www.cpan.org/authors/id/J/JO/JOESUF/libapreq2-$pkgver.tar.gz)
md5sums=('76e2acde0d82246dea6f2565f3746eec')

build() {
  cd  $srcdir/libapreq2-$pkgver
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL --with-apache2-apxs=/usr/sbin/apxs
  find . -type f -name Makefile -exec sed -i 's#-ldb-5.1#-ldb-5.2#' {} \;
  sed -i 's#-ldb-5.1#-ldb-5.2#' apreq2-config
  make
}
package(){
  cd  $srcdir/libapreq2-$pkgver
  make install DESTDIR=$pkgdir
  sed -i "s#$srcdir#/usr/src#" $pkgdir/usr/bin/apreq2-config
  find $pkgdir -name '.packlist' -delete
  find $pkgdir -name '*.pod' -delete
}

# $Id$
# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
pkgname=intellij-idea-libs
pkgver=15.0.4
_pkgver=143.2287.1
pkgrel=1
epoch=1
pkgdesc="Architecture dependent libraries needed by the Intellij Idea IDE"
arch=('i686' 'x86_64')
url="https://www.jetbrains.com/idea/"
license=('Apache')
depends=('glibc')
options=(!strip)
source=(http://download.jetbrains.com/idea/ideaIC-$pkgver.tar.gz)
sha256sums=('7653edd0687de6948b3c5b128c0683c96f89dea96bedd995a279f4d359816a16')

package() {
  cd "idea-IC-$_pkgver"

  [ $CARCH == "x86_64" ] && SUFFIX=64
  install -D -m755 bin/fsnotifier${SUFFIX} "$pkgdir"/usr/share/intellijidea-ce/bin/fsnotifier${SUFFIX}
  install -D -m644 bin/libbreakgen${SUFFIX}.so "$pkgdir"/usr/lib/libbreakgen${SUFFIX}.so
  
  # libpty
  [ $CARCH == "x86_64" ] && _ARCH=x86_64 || _ARCH=x86
  install -D -m644 lib/libpty/linux/$_ARCH/libpty.so "$pkgdir"/usr/share/intellijidea-ce/lib/libpty/linux/$_ARCH/libpty.so
}

# vim:set ts=2 sw=2 et:

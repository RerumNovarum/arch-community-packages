# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Tiago Pierezan Camargo <tcamargo@gmail.com>

pkgname=('python-south' 'python2-south')
pkgver=0.8.1
pkgrel=1
pkgdesc="Intelligent database migrations library for the Django web framework"
arch=('any')
url="http://south.aeracode.org/"
license=('APACHE')
makedepends=('python-distribute' 'python2-distribute')
source=(http://pypi.python.org/packages/source/S/South/South-$pkgver.tar.gz)
sha256sums=('b695477fc7171d8844a0477a8caa688d6988d895ec14faa817f44def8a5d7845')

package_python-south() {
  depends=('python-django')

  cd "$srcdir/South-$pkgver"
  python3 setup.py install --root="$pkgdir" -O1
}

package_python2-south() {
  depends=('python2-django')
  conflicts=('python-south<=0.7.6-1')
  replaces=('python-south<=0.7.6-1')

  cd "$srcdir/South-$pkgver"
  python2 setup.py install --root="$pkgdir" -O1
}

# vim:set ts=2 sw=2 et:

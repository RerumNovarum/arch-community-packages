# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-artwork-themes
pkgver=15.12.2
pkgrel=2
pkgdesc='Deepin artwork themes'
arch=('any')
license=('LGPL3')
url="https://gitcafe.com/Deepin/deepin-artwork-themes"
depends=('deepin-icon-theme' 'deepin-desktop-base')
makedepends=('python2' 'git')
groups=('deepin')
source=("git+https://github.com/linuxdeepin/deepin-artwork-themes.git#tag=$pkgver")
sha256sums=('SKIP')

prepare() {
  cd $pkgname

  # fix default background url
  sed -i 's#^BackgroundFile=.*$#BackgroundFile=file:///usr/share/backgrounds/deepin_default_background.jpg#' themes/deepin/theme.ini
}

build() {
  cd $pkgname
  make build
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}

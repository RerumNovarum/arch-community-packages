# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Darwin Bautista <djclue917@gmail.com>

pkgname=linphone
pkgver=3.5.2
pkgrel=4
pkgdesc='Voice-over-IP phone'
arch=('i686' 'x86_64')
url="http://www.linphone.org/index.php/eng"
license=('GPL')
depends=('alsa-lib' 'ffmpeg' 'gtk2' 'libexosip2' 'speex' 'libv4l' 'libglade' 'v4l-utils'
	 'libpulse' 'libxv' 'mediastreamer' 'ortp' 'libzrtpcpp' 'bcg729')
makedepends=('pkgconfig' 'perlxml' 'intltool' 'sgmltools-lite')
optdepends=('pulseaudio')
options=('!libtool' '!emptydirs')
source=("http://download-mirror.savannah.gnu.org/releases/linphone/3.5.x/sources/linphone-$pkgver.tar.gz")
md5sums=('4be6e940372dba1f6793aef849c1ff0d')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  export PKG_CONFIG=/usr/bin/pkg-config
  unset SGML_CATALOG_FILES
  ./configure --prefix=/usr --disable-static --enable-ipv6 --enable-video \
	--enable-alsa --enable-pulseaudio --disable-artsc --disable-strict \
	--libexecdir=/usr/lib/$pkgname \
	--enable-external-mediastreamer \
	--enable-external-ortp \
	--enable-zrtp
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Michael Spencer <sonrisesoftware@gmail.com>

pkgname=papyros-terminal
pkgver=0.1.0
pkgrel=2
pkgdesc="The terminal app for Papyros"
arch=("i686" "x86_64")
url="https://github.com/papyros/terminal-app"
license=("GPL")
depends=("qt5-base" "qt5-declarative" "qml-material" "qmltermwidget"
         "libpapyros")
makedepends=("git" "extra-cmake-modules")
source=("git+https://github.com/papyros/terminal-app.git#tag=v$pkgver"
        libpapyros-0.2.patch)
sha256sums=('SKIP'
            '6c9720692749854672a5aec2e47afead17b836fddeaa65a67749e941170c2cdc')

prepare() {
  mkdir -p build
  sed -i 's/Roboto Mono for Powerline/Monospace/' terminal-app/src/qml/Settings.qml

  (cd terminal-app; patch -p1 -i ../libpapyros-0.2.patch)
}

build() {
  cd build
  cmake "$srcdir/terminal-app" -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}

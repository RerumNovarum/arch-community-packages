# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Ronald van Haren <ronald.archlinux.org>

pkgname=pyneighborhood
pkgver=0.5.1
pkgrel=2
pkgdesc="A GTK+ 2 rewrite of the well-known LinNeighborhood tool"
license=('GPL')
url="https://launchpad.net/pyneighborhood"
source=('http://launchpad.net/pyneighborhood/0.5/0.5.1/+download/pyneighborhood-0.5.1.tar.bz2')
depends=('pygtk' 'smbclient')
install=pyneighborhood.install
arch=('i686' 'x86_64')
sha1sums=('02c068330e8ad712e083f2c579686a01926a9283')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # python2 fix
  for file in $(find . -name '*.py' -print); do
    sed -i 's_^#!.*/usr/bin/python_#!/usr/bin/python2_' $file
    sed -i 's_^#!.*/usr/bin/env.*python_#!/usr/bin/env python2_' $file
  done

  python2 setup.py build
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  python2 setup.py install --root=${pkgdir}
}

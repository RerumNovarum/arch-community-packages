# $Id$
# Maintainer : Ionut Biru <ionut@archlinux.ro>
# Contributor: Dieter Plaetinck <dieter@plaetinck.be>
pkgbase=uzbl
pkgname=('uzbl-core' 'uzbl-browser')
pkgver=2009.12.22
pkgrel=1
arch=('i686' 'x86_64')
url="http://www.uzbl.org"
license=('GPL3')
makedepends=('git' 'pkgconfig' 'libwebkit>=1.1.15.4')
source=(uzbl-$pkgver.tar.gz::http://github.com/Dieterbe/uzbl/tarball/$pkgver
	fix_build.patch)
md5sums=('8cf1bd7c2fe6c14345b1671160f0214f'
         'c01ce2b428dca345adb7032179f2dec0')

build() {
    cd "$srcdir"/Dieterbe-uzbl-*
    patch -Np0 -i "$srcdir/fix_build.patch"
    make || return 1
}
package_uzbl-core(){
   pkgdesc="Main component meant for integration with other tools and scripts"
   depends=('libwebkit>=1.1.15.4')
   optdepends=('socat: to interface with the socket' 'dmenu: to run some of the example scripts'
               'zenity: to run some of the example scripts'
               'python: to run some of the example scripts' 'perl: for the example formfiller'
               'xclip: for clipboard related keybindings')

   cd "$srcdir"/Dieterbe-uzbl-*
   make DESTDIR="$pkgdir" PREFIX=/usr  install-uzbl-core || return 1
}

package_uzbl-browser(){
   pkgdesc="A complete browser experience based on uzbl-core"
   depends=("uzbl-core=$pkgver" 'socat>=1.7.1.1' 'zenity>=2.28.0' 'xclip>=0.12' 'dmenu>=4.0')
   cd "$srcdir"/Dieterbe-uzbl-*
   make DESTDIR="$pkgdir" PREFIX=/usr  install-uzbl-browser || return 1
}


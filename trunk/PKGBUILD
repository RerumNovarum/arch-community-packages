# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Jameson Pugh <imntreal@gmail.com>
# Contributor: Tim Jester-Pfadt <t.jp<at>gmx.de>

pkgbase=appstream
pkgname=(appstream appstream-qt)
pkgver=0.10.0
pkgrel=2
pkgdesc="Provides a standard for creating app stores across distributions"
arch=(i686 x86_64)
url="http://distributions.freedesktop.org/wiki/AppStream"
license=(GPL)
depends=(xapian-core polkit libyaml libxml2 protobuf libstemmer)
makedepends=(cmake xmlto intltool gobject-introspection python2 docbook-xsl qt5-base)
source=("http://www.freedesktop.org/software/appstream/releases/AppStream-$pkgver.tar.xz" update-appstream-index.hook
        appstream-screenshot-url.patch::"https://github.com/ximion/appstream/commit/cef772ac.patch")
sha256sums=('71e561064522b10039c3f36864cbcbdebb4e94db0500d9c4e92a13760072bbb8'
            'eb4fcec37d2cbe42ddf13778c21554aba0dcbc789781571a568ea35526496f79'
            '1e08cce26259f9e48f1792c8437036082fa7542fdc7f918d5619e47111d8ce17')

prepare() {
  mkdir -p build

  cd AppStream-$pkgver
# Fix cache (de)serialization for screenshots and suggestions
  patch -p1 -i ../appstream-screenshot-url.patch
}

build() {
  cd build

  cmake ../AppStream-$pkgver \
	-DQT=ON \
	-DCMAKE_INSTALL_LIBDIR=lib \
	-DCMAKE_BUILD_TYPE=Release
  make
}

package_appstream() {
  cd build

  make DESTDIR="$pkgdir" install
  
# provided by -qt subpackage
  rm -r "$pkgdir"/usr/{include/AppstreamQt,lib/cmake,lib/libAppstreamQt.*}

  install -Dm644 "$srcdir"/update-appstream-index.hook "$pkgdir"/usr/share/libalpm/hooks/update-appstream-index.hook
}

package_appstream-qt() {
  pkgdesc='Qt5 interface for AppStream'
  depends=(appstream qt5-base)

  cd build/qt
  make DESTDIR="$pkgdir" install
}

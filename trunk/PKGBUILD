# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=archlinux-appstream-data
pkgver=20160428
pkgrel=1
pkgdesc="Arch Linux application database for AppStream-based software centers"
arch=(any)
url="http://www.archlinux.org"
license=(GPL)
depends=()
makedepends=()
source=(http://sources.archlinux.org/other/community/archlinux-appstream-data/$pkgver/{core,extra,community}.xml.xz
 http://sources.archlinux.org/other/community/archlinux-appstream-data/$pkgver/{core,extra,community}-icons-64x64.tar.gz
 http://sources.archlinux.org/other/community/archlinux-appstream-data/$pkgver/{core,extra,community}-icons-128x128.tar.gz)
noextract=({core,extra,community}-icons-{64x64,128x128}.tar.gz)
md5sums=('ca8e186de849c2c53ed71414922692b7'
         '9a0c8fe7b74c22de9573d97eb7c96aa3'
         'af95a564736784f9fc057387a427508a'
         '3ab87b983dab1b42b298fca86e427b9e'
         '1234e8ab11a6c903ac9f868f88737189'
         'c954864542a2ff6cd0c8329ba3633224'
         '3ab87b983dab1b42b298fca86e427b9e'
         '991b267e81383a0daef261116cae0ade'
         '15b3d9158919d854ad5a4f1444ea4552')

package() {
  mkdir -p "$pkgdir"/usr/share/app-info/{icons/archlinux-arch-{core,extra,community}/{64x64,128x128},xmls}
  for _repo in core extra community; do
   tar -xzf $_repo-icons-64x64.tar.gz -C "$pkgdir"/usr/share/app-info/icons/archlinux-arch-$_repo/64x64
   tar -xzf $_repo-icons-128x128.tar.gz -C "$pkgdir"/usr/share/app-info/icons/archlinux-arch-$_repo/128x128
  done
  cp *.xml "$pkgdir"/usr/share/app-info/xmls/
}

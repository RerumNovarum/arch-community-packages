# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=directfb
pkgver=1.6.1
pkgrel=1
pkgdesc='Thin library that provides hardware graphics acceleration, input device handling and abstraction, integrated windowing system on top of the Linux Framebuffer Device'
arch=('i686' 'x86_64')
url="http://www.directfb.org"
license=('LGPL')
depends=('gcc-libs' 'libjpeg' 'sdl' 'libpng' 'freetype2')
options=('!libtool')
source=(http://www.directfb.org/downloads/Core/DirectFB-${pkgver%.*}/DirectFB-${pkgver}.tar.gz \
        DirectFB-1.6.1-zlib-include.patch)
sha1sums=('121c7fd1e81ccb41962986126e1d69f188c2f2c2'
          'f2309edd87c772a7894d4b648d56197d91790cad')

build() {
  cd "${srcdir}/DirectFB-${pkgver}"
  patch -p1 -i ../DirectFB-1.6.1-zlib-include.patch
  ./configure --prefix=/usr --sysconfdir=/etc --enable-static \
    --enable-zlib --enable-x11 --enable-sdl --disable-vnc --disable-osx \
    --enable-video4linux2 --enable-voodoo
  make
} 

package() {
  cd "${srcdir}/DirectFB-${pkgver}"
  make DESTDIR="${pkgdir}" install
}

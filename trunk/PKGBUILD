# $Id$
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Roman Kyrylych <roman@archlinux.org>
# Contributor: Johannes Sjolund <j.sjolund@gmail.com>

pkgname=gnome-commander
pkgver=1.2.8.13
pkgrel=1
pkgdesc="A graphical two-pane filemanager for Gnome"
arch=('i686' 'x86_64')
url="http://www.nongnu.org/gcmd/"
license=('GPL')
depends=('libgnomeui' 'gnome-vfs' 'gconf' 'python2' 'libsm')
makedepends=('perlxml' 'gnome-doc-utils>=0.20.1' 'intltool')
options=(!libtool)
install=gnome-commander.install
source=(ftp://ftp.gnome.org/pub/GNOME/sources/gnome-commander/1.2/$pkgname-$pkgver.tar.xz)
sha256sums=('d5fd6da276a21046c73ae9ebdc676ee3c82b4a2149e6dc6d38006f8b364a5893')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # python2 fix
  for file in doc/*/gnome-commander.xml; do
      sed -i 's_#!/usr/bin/env python_#!/usr/bin/env python2_' $file
  done

  ./configure --prefix=/usr --libdir=/usr/lib --sysconfdir=/etc \
    --localstatedir=/var --disable-scrollkeeper --enable-python

  make
}

package() {
   cd "${srcdir}/${pkgname}-${pkgver}"
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install

  mkdir -p "${pkgdir}"/usr/share/gconf/schemas
  gconf-merge-schema "${pkgdir}"/usr/share/gconf/schemas/${pkgname}.schemas \
    --domain ${pkgname} "${pkgdir}"/etc/gconf/schemas/*.schemas
  rm -rf "${pkgdir}"/etc/gconf/schemas/
}

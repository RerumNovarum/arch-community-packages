# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Geoffroy Carrier <geoffroy.carrier@koon.fr>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=flumotion
pkgver=0.8.0
pkgrel=4
arch=('i686' 'x86_64')
pkgdesc="A streaming media server"
url="http://www.flumotion.net"
options=('!libtool')
license=('GPL')
backup=(etc/flumotion/workers/default.xml
	etc/flumotion/managers/default/planet.xml)
depends=('gtk2' 'pygtk' 'gstreamer0.10-python' 'kiwi' 'gstreamer0.10-good-plugins'
	'gstreamer0.10-base-plugins' 'pyopenssl' 'twisted' 'gnome-vfs')
makedepends=('perlxml')
source=(http://www.flumotion.net/src/$pkgname/$pkgname-$pkgver.tar.bz2
        python27.patch)
md5sums=('1df1f8fc47ca4cc6d3ead912f2ac76af'
         '623a41a985d2b114c58db701c6d5d13e')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # python2 fix
  sed -i 's_candidate in python_candidate in python2_' configure
  for file in $(find . -name '*.py' -print); do
    sed -i 's_^#!.*/usr/bin/python_#!/usr/bin/python2_' $file
    sed -i 's_^#!.*/usr/bin/env.*python_#!/usr/bin/env python2_' $file
  done

  # python2.7 patch
  patch -Np0 -i "$srcdir/python27.patch"

  ./configure --prefix=/usr --sysconfdir=/etc

  install -d "$pkgdir/etc/$pkgname"
  make
  make DESTDIR="$pkgdir" install
  cp -R conf/workers "$pkgdir/etc/$pkgname"
  cp -R conf/managers "$pkgdir/etc/$pkgname"
  cp conf/default.pem "$pkgdir/etc/$pkgname"
}

# $Id$
# Maintainer : Martin Wimpress <code@flexion.org>

_ver=1.10
pkgbase=mate-panel
pkgname=(${pkgbase}-gtk3)
pkgver=${_ver}.0
pkgrel=1
pkgdesc="The MATE Panel (GTK3 version [EXPERIMENTAL])"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('caja-gtk3>=1.10' 'dbus-glib' 'dconf' 'gtk3' 'libwnck3' 'libcanberra' 'libice'
         'libmateweather-gtk3>=1.10' 'librsvg' 'libsm' 'libsoup' 'libxau' 'marco-gtk3>=1.10'
         'mate-menus>=1.10' 'mate-desktop-gtk3>=1.10' 'mate-session-manager-gtk3>=1.10')
makedepends=('gobject-introspection' 'mate-common' 'yelp-tools')
optdepends=('yelp: for reading MATE help documents')
source=("http://pub.mate-desktop.org/releases/${_ver}/${pkgbase}-${pkgver}.tar.xz")
groups=('mate-gtk3')
conflicts=("${pkgbase}")
sha1sums=('632d7127fba8c5fa76f5dd951db89325c6ca3f89')
install=${pkgbase}.install


build() {
    cd "${srcdir}/${pkgbase}-${pkgver}"
    ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib/${pkgbase} \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --with-gtk=3.0 \
        --enable-introspection \
        --disable-static
    make
}

package_mate-panel-gtk3() {
    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}

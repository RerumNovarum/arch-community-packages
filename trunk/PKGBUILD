# Contributor: Allan McRae <mcrae_allan@hotmail.com>
# Contributor: fancris3 <fancris3 at aol.com>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=gecko-mediaplayer
pkgver=0.9.8
pkgrel=2
pkgdesc="Browser plugin that uses gnome-mplayer to play media in a web browser"
arch=('i686' 'x86_64')
url="http://code.google.com/p/gecko-mediaplayer/"
license=('GPL')
depends=("gnome-mplayer=${pkgver}" 'dbus-glib')
makedepends=('xulrunner' 'pkgconfig')
replaces=('mplayer-plugin')
source=(http://gecko-mediaplayer.googlecode.com/files/${pkgname}-${pkgver}.tar.gz)
md5sums=('4df009d21b1a95c35c9adfdb6fbc96fe')

build() {
	cd ${srcdir}/${pkgname}-${pkgver}

	./configure --prefix=/usr --sysconfdir=/etc --disable-schemas-install --without-gconf \
	GECKO_LIBS="-L/usr/lib/xulrunner-devel-1.9.1/lib -lxpcomglue_s -lxul -lxpcom -lplds4 -lplc4 -lnspr4 -lpthread -ldl" \
	GECKO_CFLAGS="-fshort-wchar -I/usr/include/xulrunner-1.9.1/stable -I/usr/include/nspr" \
	GECKO_XPIDL="/usr/lib/xulrunner-1.9.1/xpidl" \
	GECKO_IDLDIR="/usr/share/idl/xulrunner-1.9.1/stable"

	make || return 1
}

package() {
	cd ${srcdir}/${pkgname}-${pkgver}

	make DESTDIR=${pkgdir} install
}

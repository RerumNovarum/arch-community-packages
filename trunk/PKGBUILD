# $Id$
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=python-psycopg1
pkgver=1.1.21
pkgrel=5
pkgdesc="A PostgreSQL database adapter for Python"
arch=('i686' 'x86_64')
url="http://initd.org/projects/psycopg1"
license=('GPL')
depends=('python-egenix-mx-base' 'postgresql')
source=(http://initd.org/pub/software/psycopg/psycopg-$pkgver.tar.gz)
md5sums=('a31f79f68d6d32898d6f24e11369a106')
sha1sums=('02d8a8b5dd682306117896c7c63a9a1b518997b6')

build() {
  cd "$srcdir/psycopg-$pkgver"
  ./configure --prefix=/usr --with-python=/usr/bin/python2 \
              --with-mxdatetime-includes=/usr/lib/python2.7/site-packages/mx/DateTime/mxDateTime
  sed -e 's:\(echo "  install -m 555 $$mod \)\($(PY_MOD_DIR)\)\("; \\\):\1${DESTDIR}\2/$$mod\3:' \
		-e 's:\($(INSTALL)  -m 555 $$mod \)\($(PY_MOD_DIR)\)\(; \\\):\1${DESTDIR}\2/$$mod\3:' \
		-i Makefile
  make
}

package() {
  cd "$srcdir/psycopg-$pkgver"
  make DESTDIR="$pkgdir" install
}

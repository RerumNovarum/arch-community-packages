# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: William Rea <sillywilly@gmail.com>
# Contributor: Hans Janssen <hans@janserv.xs4all.nl>

pkgname=flightgear
pkgver=2.6.0
pkgrel=2
pkgdesc="An open-source, multi-platform flight simulator"
arch=(i686 x86_64)
depends=('simgear' 'libxmu' 'libxi' 'zlib' 'openscenegraph' 'subversion')
makedepends=('boost' 'cmake')
license=("GPL")
url="http://www.flightgear.org"
options=('!makeflags')
#source=("$pkgname-$pkgver.tar.gz::https://gitorious.org/fg/flightgear/archive-tarball/release/$pkgver")
#source=("ftp://ftp.de.flightgear.org/pub/fgfs/Source/flightgear-$pkgver.tar.bz2")
source=("http://mirrors.ibiblio.org/pub/mirrors/flightgear/ftp/Source/flightgear-$pkgver.tar.bz2")
md5sums=('943f784f55c7e11ddce6082d2d1dbba4')

build() {
  cd $srcdir/flightgear-$pkgver
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=lib .
  make
  sed -i 's|Exec=.*|Exec=fgfs|' package/flightgear.desktop
}

package() {
  depends=('simgear' 'libxmu' 'libxi' 'zlib' 'openscenegraph' 'subversion' 'flightgear-data')
  cd $srcdir/flightgear-$pkgver
  make DESTDIR=$pkgdir install
  install -Dm0644 package/flightgear.desktop $pkgdir//usr/share/applications/flightgear.desktop
  install -Dm0644 package/RedHat/flightgear.48.png $pkgdir/usr/share/icons/flightgear.png
}

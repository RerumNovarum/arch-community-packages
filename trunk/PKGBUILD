# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>

pkgname=vdrift-data
pkgver=896
_pkgver=2011.09.01
pkgrel=1
epoch=1
pkgdesc="An open source driving simulation made with drift racing in mind (data files)"
arch=('any')
url="http://vdrift.net/"
license=('GPL')
depends=()
makedepends=('subversion')
source=()
md5sums=()

_svntrunk=https://vdrift.svn.sourceforge.net/svnroot/vdrift/tags/vdrift-data-2011-09-01/
_svnmod=vdrift

build() {
  cd "$srcdir"
  msg "Connecting to SVN server...."

  if [[ -d "$_svnmod/.svn" ]]; then
    (cd "$_svnmod" && svn up -r "$pkgver")
  else
    svn co "$_svntrunk" --config-dir ./ -r "$pkgver" "$_svnmod"
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_svnmod-build"
  svn export "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build"
}

package() {
  cd "$srcdir/$_svnmod-build"

  mkdir -p "$pkgdir"/usr/share/vdrift/
  find . -name SConscript -delete
  cp -r * "$pkgdir"/usr/share/vdrift/

  # install icons
  install -Dm644 textures/icons/vdrift-16x16.png \
    "$pkgdir"/usr/share/icons/hicolor/16x16/apps/vdrift.png
  install -Dm644 textures/icons/vdrift-32x32.png \
    "$pkgdir"/usr/share/icons/hicolor/32x32/apps/vdrift.png
  install -Dm644 textures/icons/vdrift-64x64.png \
    "$pkgdir"/usr/share/icons/hicolor/64x64/apps/vdrift.png
}
# vim: sw=2:ts=2 et:

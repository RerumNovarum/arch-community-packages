# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Diego <cdprincipe@gmail.com>

pkgname=numix-themes
pkgver=2.6.1
pkgrel=1
pkgdesc='A flat and light theme with a modern look (GNOME, Openbox, Unity, Xfce)'
arch=('any')
url='http://numixproject.org/'
license=('GPL3')
depends=('gtk-engine-murrine')
makedepends=('git' 'ruby-bundler')
_commit='a6be63453516d3be718ddffc9f791ecc34da70bf'
source=("numix-themes-${pkgver}.tar.gz::https://github.com/numixproject/numix-gtk-theme/archive/${pkgver}.tar.gz"
        "Gemfile")
sha256sums=('7d81fb0fe7a50f933ae8016154a6da8ea4f6c12d988f735db277424af344a5bf'
            'b1061ba7ce09c9eb3e41f59362aa8c21d27073b0658f13fb5eabdaea654afe2c')

build() {
  cd numix-gtk-theme-${pkgver}

  bundle install --path .
  make SASS="bundle exec sass"
}

package() {
  cd numix-gtk-theme-${pkgver}

  make SASS="bundle exec sass" DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:

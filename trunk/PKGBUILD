# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Lubomir 'Kuci' Kucera <kuci24-at-gmail-dot-com>

pkgname=gitlab-ci-multi-runner
pkgver=1.5.2
pkgrel=1
pkgdesc="The official GitLab CI runner written in Go"
arch=('i686' 'x86_64')
url='https://gitlab.com/gitlab-org/gitlab-ci-multi-runner'
license=('GPL3')
depends=('ca-certificates' 'curl' 'git' 'glibc' 'tar')
makedepends=('git' 'go' 'godep' 'go-bindata' 'mercurial')
install='gitlab-runner.install'
backup=('etc/gitlab-runner/config.toml')
noextract=('prebuilt-x86_64.tar.xz'
           'prebuilt-arm.tar.xz')
source=("${pkgname}-${pkgver}.tar.gz"::"https://gitlab.com/gitlab-org/gitlab-ci-multi-runner/repository/archive.tar.gz?ref=v${pkgver}"
        "https://gitlab-ci-multi-runner-downloads.s3.amazonaws.com/v${pkgver}/docker/prebuilt-x86_64.tar.xz"
        "https://gitlab-ci-multi-runner-downloads.s3.amazonaws.com/v${pkgver}/docker/prebuilt-arm.tar.xz"
        "gitlab-runner.install"
        "gitlab-runner.service"
        "gitlab-runner.sysusers"
        "gitlab-runner.tmpfiles"
        "config.toml"
        "godeps.patch")
sha256sums=('fbade53f2603187207fc7a58b8dc8fcdc5c089c8405cc136edbc715079caad19'
            '265a49a8ecb5a4ca3fe51c66baca2a8e82608e37fbe487e261bfe72d83dc5902'
            '951b4ee84fcab0c63e80d70fe78774a32eb96bff65266417d4f6e4aeb8fe7e64'
            '289f73a16df5c8823a15b67a4aaa635d32b9c49ce4e11baa5d33f12828a1e0bb'
            '843cabc4e1cfbf0a81365abae0c3d6ac388c00074017f9c286a7d5545828d1fa'
            '0d7621aec132ce44d7dadbe6c5f43b330af5c1dddfe06c958336a297e70c5bba'
            '8b17ed458da6926ea0d28aa4cb307b42b047bda9dfa4a96ee48e1c02051efb13'
            'b1f7d9a6a0148884528584909729cce817253759650da61d641e2225845f9330'
            'e75aafe94fe0029e42267e459a1c2d1edae1c3f8d4914af353fc005fef74a828')

prepare() {
    _srcdir=$(ls "${srcdir}" | grep -E "${pkgname}-v${pkgver}-[0-9a-f]{40}")

    mkdir -p "${srcdir}/src/gitlab.com/gitlab-org/"

    ln -sf "${srcdir}/${_srcdir}" "${srcdir}/src/gitlab.com/gitlab-org/${pkgname}"

    cd "${srcdir}/src/gitlab.com/gitlab-org/${pkgname}"

    # Patch invalid windows packages in Godeps/Godeps.json
    patch -p0 -i "${srcdir}/godeps.patch"

    GOPATH="${srcdir}" godep restore

    ln -sf "${srcdir}/prebuilt-x86_64.tar.xz" prebuilt-x86_64.tar.xz
    ln -sf "${srcdir}/prebuilt-x86_64.tar.xz" prebuilt-arm.tar.xz
}

build() {
    _rev=$(ls "${srcdir}" | grep -Po "${pkgname}-v${pkgver}-\K([0-9a-f]{7})")

    cd "${srcdir}/src/gitlab.com/gitlab-org/${pkgname}"

    GOPATH="${srcdir}" go-bindata \
        -pkg docker \
        -nocompress \
        -nomemcopy \
        -prefix out/docker/ \
        -o executors/docker/bindata.go \
        prebuilt-x86_64.tar.xz \
        prebuilt-arm.tar.xz

    GOPATH="${srcdir}" go build \
        -ldflags "-X main.NAME=${pkgname} -X main.VERSION=${pkgver} -X main.REVISION=${_rev}"
}

package() {
    cd "${srcdir}/src/gitlab.com/gitlab-org/${pkgname}"

    install -Dm644 "${srcdir}/config.toml" "${pkgdir}/etc/gitlab-runner/config.toml"
    install -Dm644 "${srcdir}/gitlab-runner.service" "${pkgdir}/usr/lib/systemd/system/gitlab-runner.service"
    install -Dm644 "${srcdir}/gitlab-runner.sysusers" "${pkgdir}/usr/lib/sysusers.d/gitlab-runner.conf"
    install -Dm644 "${srcdir}/gitlab-runner.tmpfiles" "${pkgdir}/usr/lib/tmpfiles.d/gitlab-runner.conf"
    install -Dm755 "${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
}

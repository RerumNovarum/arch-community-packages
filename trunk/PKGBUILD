# $Id$
# Maintainer : Laurent Carlier <lordheavym@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Dieter Plaetinck <dieter@plaetinck.be>

pkgbase=uzbl
pkgname=('uzbl-core' 'uzbl-browser' 'uzbl-tabbed')
pkgver=2011.02.15
pkgrel=1
arch=('i686' 'x86_64')
url="http://www.uzbl.org"
license=('GPL3')
makedepends=('git' 'pkgconfig' 'libwebkit>=1.2.0')
source=(uzbl-$pkgver.tar.gz::http://github.com/Dieterbe/uzbl/tarball/$pkgver)
md5sums=('81520b74a5e19a85627a0258e3364704')

build() {
    cd "$srcdir"/Dieterbe-uzbl-*

    # python2 fix
    for file in examples/data/scripts/uzbl*; do
        sed -i 's_#!/usr/bin/env python_#!/usr/bin/env python2_' $file
    done
    sed -i -e "s|#![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
           -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
           $(find ./ -name '*.py')

    make
}

package_uzbl-core() {
   pkgdesc="Webpage interface component meant for integration with other tools and scripts"
   depends=('libwebkit')
   optdepends=('socat: to interface with the socket' 'dmenu: to run some of the example scripts'
               'zenity: to run some of the example scripts'
               'python2: to run some of the example scripts'
               'xclip: for clipboard related keybindings'
	       'pygtk: for uzbl-tabbed')
   install=uzbl.install

   cd "$srcdir"/Dieterbe-uzbl-*

   make DESTDIR="$pkgdir" PREFIX=/usr  install-uzbl-core
}

package_uzbl-browser() {
   pkgdesc="A complete browser experience based on uzbl-core"
   depends=("uzbl-core=$pkgver" 'socat' 'zenity' 'xclip' 'dmenu' 'python2')

   cd "$srcdir"/Dieterbe-uzbl-*

   make DESTDIR="$pkgdir" PREFIX=/usr  install-uzbl-browser
}

package_uzbl-tabbed() {
   pkgdesc="Tabbing manager providing multiple uzbl-browser instances in 1 window"
   depends=("uzbl-browser=$pkgver" 'pygtk')

   cd "$srcdir"/Dieterbe-uzbl-*

   make DESTDIR="$pkgdir" PREFIX=/usr install-uzbl-tabbed
}


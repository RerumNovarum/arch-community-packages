# $Id$
# Maintainer: Angel Velasquez <angvp@archlinux.org>
# Contributor: Roberto Alsina <ralsina@kde.org>

pkgname=python2-rst2pdf
pkgver=0.91
pkgrel=2
pkgdesc='Create PDFs from simple text markup, no LaTeX required'
arch=('any')
url='http://rst2pdf.googlecode.com'
license=('MIT')
depends=('python2-distribute' 'python2-docutils' 'python-reportlab')
optdepends=('uniconvertor: vector images support'
            'python-svglib: some SVG support'
            'inkscape: best SVG support'
            'python-wordaxe: hyphenation'
            'python2-pygments: Syntax Highlighting'
            'python-imaging: non-JPEG bitmap image formats')
source=("http://rst2pdf.googlecode.com/files/rst2pdf-$pkgver.tar.gz"
        '01-fix-docutils-move-roman.patch'
        '02-display-style-math-broken.patch')
md5sums=('9ca219b0460be89585f96588e12543b9'
         '97fdf681aa6ee2a3326b56530c71c3f6'
         '0a4ef9cbe1c00b0f9c0f1b591394f8b7')

build() {
  cd rst2pdf-$pkgver
  patch -N -p1 -i "$srcdir/01-fix-docutils-move-roman.patch"
  patch -N -p1 -i "$srcdir/02-display-style-math-broken.patch"
  python2 setup.py build
  find build -type f -exec \
    sed -i '1s,^#! \?/usr/bin/\(env \|\)python$,#!/usr/bin/python2,' {} \;
  cd doc && rst2man2 rst2pdf.txt rst2pdf.1
}

package() {
  cd rst2pdf-$pkgver
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"
  install -Dm644 doc/rst2pdf.1 "$pkgdir/usr/share/man/man1/rst2pdf.1"
}

# vim:set ts=2 sw=2 ft=sh et:

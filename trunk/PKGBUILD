# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: v01d <phreakuencies@gmail.com>

pkgname=p3scan
pkgver=2.3.2
pkgrel=5
pkgdesc="Fully transparent POP3/SMTP proxy with Anti-Virus and SPAM protection"
arch=('i686' 'x86_64')
url="http://p3scan.sourceforge.net/"
depends=('pcre' 'openssl')
license=('GPL')
source=("http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.gz" "rc.p3scan")
md5sums=('9f8decc7d4701228788e3c8717096ca0' 'f6e3450c3a932477c49082559b7abef8')
install=p3scan.install
md5sums=('9f8decc7d4701228788e3c8717096ca0'
         '482a783e699b6532cbc478977f429eb5')

build() {
  cd $srcdir/$pkgname-$pkgver

  # Fix CFLAGS and other options hardcoded in Makefiles
  unset CFLAGS
  sed -ri "s|^CFLAGS=-Wall( -g)? -O2|CFLAGS=-Wall $CFLAGS|" Makefile ripmime/Makefile ripmime/tnef/Makefile ripmime/ripOLE/Makefile
  sed -ri 's|^LOCATION=.+$|LOCATION=/usr|' ripmime/Makefile
  sed -ri 's|\$\(SYSINS\) -v -m 550 --strip p3scan \$\(PREFIX\)/sbin/|$(SYSINS) -D -v -m 550 --strip p3scan $(DESTDIR)/usr/sbin/|' Makefile
  make

  # Create base dirs, the Makefile assumes they're there
  install -d $pkgdir/usr/sbin $pkgdir/etc/rc.d $pkgdir/usr/man/man8

  # install with root set on $pkgdir
  make DESTDIR=$pkgdir install

  # delete init script provided and use an arch compatible one
  rm -f $pkgdir/etc/rc.d/rc.p3scan
  install -D -m755 -g root -o root $srcdir/rc.p3scan $pkgdir/etc/rc.d/p3scan

  # BUG?: make absolute link a relative one
  (
    cd $pkgdir/etc/p3scan
    ln -sf p3scan-en.mail p3scan.mail
  )

  # BUG: fix .conf file (the license has C style comments, they should be conf-style comments)
  sed -ri 's|^[/ ]\*/?|# |g' $pkgdir/etc/p3scan/p3scan.conf

  mv $pkgdir/usr/man $pkgdir/usr/share/
  mv $pkgdir/usr/doc $pkgdir/usr/share/
  rm -rf $pkgdir/var/run
}

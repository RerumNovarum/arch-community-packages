# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Brad Fanella <bradfanella@archlinux.us>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: tocer.deng <tocer.deng@gmail.com>

pkgname=apvlv
pkgver=0.1.4
pkgrel=1
pkgdesc='PDF Viewer which behaves like Vim'
arch=('i686' 'x86_64')
url="http://naihe2010.github.com/apvlv/"
license=('GPL')
depends=('gtk2' 'cairo' 'poppler-glib' 'djvulibre')
makedepends=('cmake' 'libwebkit')
source=("ftp://ftp.archlinux.org/other/community/$pkgname/$pkgname-$pkgver.tar.xz")
md5sums=('5518b842715f1a9e7d3c8d2b7ed16342')

build() {
  cd "$srcdir"/$pkgname-$pkgver

  mkdir -p build
  cd build
  
  cmake -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DAPVLV_WITH_UMD=no  ..
  make
}

package() {
  cd "$srcdir"/$pkgname-$pkgver/build

  make DESTDIR="$pkgdir" install
}

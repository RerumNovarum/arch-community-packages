# $Id$
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Roman Kyrylych <roman@archlinux.org>
# Contributor: Tor Krill <tor@krill.nu>

pkgname=nautilus-actions
pkgver=3.0.3
pkgrel=1
pkgdesc="Configures programs to be launched when files are selected in Nautilus"
arch=('i686' 'x86_64')
url="http://gnome.org"
license=('GPL')
depends=('nautilus' 'libgtop' 'desktop-file-utils' )
makedepends=('intltool' 'gnome-doc-utils>=0.20.0')
install=nautilus-actions.install
options=('!libtool')
source=(http://ftp.gnome.org/pub/GNOME/sources/nautilus-actions/3.0/$pkgname-$pkgver.tar.bz2)
sha256sums=('dfa3887b3878150c2bcdd3a1e06a21fa2f475ccaa6e6918fa6064984ebf2623e')

build() {
    cd ${srcdir}/${pkgname}-${pkgver}
    unset LDFLAGS
    ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
    make
}

package(){
    cd ${srcdir}/${pkgname}-${pkgver}
    make  GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR=${pkgdir} install
    mkdir -p ${pkgdir}/usr/share/gconf/schemas
    gconf-merge-schema ${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas --domain nautilus-actions \
        ${pkgdir}/etc/gconf/schemas/*.schemas
    rm -rf ${pkgdir}/etc/gconf/schemas/
}

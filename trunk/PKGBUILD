# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Kessia 'even' Pinheiro <kessiapinheiro at gmail.com>
# Contributor: Penguin <TGates81.at.gmail.dot.com>

pkgname=ibam
pkgver=0.5.2
pkgrel=5
pkgdesc="An intelligent battery monitor"
arch=('i686' 'x86_64')
url="http://ibam.sourceforge.net/"
license=('GPL')
depends=('gcc-libs')
makedepends=('sed' 'make')
optdepends=('gnuplot: graphs support')
install="ibam.install"
source=("http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.gz"
	ibam-0.5.2-sysfs.patch)
md5sums=('2d5222ff504dd19e7c1ea8acc2f13cf5'
         'c3aafebd5ad9159c89f71beab4a0f414')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  sed -i \
	-e "s|/local/bin|/bin|g" \
	-e "s|^CFLAGS=-O3|CFLAGS=${CFLAGS}|g" \
	Makefile
  patch -p1 <$srcdir/ibam-0.5.2-sysfs.patch

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  install -m0755 -d ${pkgdir}/usr/bin
  install -m0755 -D ${srcdir}/${pkgname}-${pkgver}/ibam ${pkgdir}/usr/bin/ibam
}

# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Douglas Soares de Andrade <dsa@aur.archlinux.org>
# Contributor: Benjamin Andresen <benny@klapmuetz.org>
# Contributor: bekks <eduard.warkentin@gmx.de>

pkgname=pgadmin3
pkgver=1.14.1
pkgrel=2
pkgdesc="Comprehensive design and management interface for PostgreSQL"
arch=('i686' 'x86_64')
url="http://www.pgadmin.org"
license=('custom')
depends=('wxgtk' 'postgresql-libs' 'libxslt')
makedepends=('libpqxx' 'krb5' 'postgresql' 'gendesk' 'imagemagick')
source=(ftp://ftp.de.postgresql.org/pub/packages/databases/PostgreSQL/pgadmin3/release/v${pkgver}/src/pgadmin3-${pkgver}.tar.gz)
md5sums=('ba943ac3a1d748bd32123c3d442606b2')

build() {
  cd "$srcdir"
  gendesk

  cd "pgadmin3-${pkgver}"
  [ -f Makefile ] || ./configure --prefix=/usr
  make

  convert pgadmin/include/images/pgAdmin3.ico pgadmin3.png
}

package() {
  cd $srcdir/pgadmin3-${pkgver}

  make DESTDIR=$pkgdir/ install
  install -Dm644 i18n/$pkgname.lng $pkgdir/usr/share/pgadmin3/i18n
  install -Dm644 LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
  install -Dm644 pgadmin/include/images/pgAdmin3.ico $pkgdir/usr/share/pgadmin3/pixmaps/pgAdmin3.ico
  install -Dm644 "$srcdir/pgadmin3.png" "$pkgdir/usr/share/pixmaps/pgadmin3.png"
  install -Dm644 "$srcdir/pgadmin3.desktop" "$pkgdir/usr/share/applications/pgadmin3.desktop"
}

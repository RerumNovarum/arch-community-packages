# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Sven Pfleiderer <pfleidi@roothausen.de>

pkgname=newsbeuter
pkgver=2.6
pkgrel=1
pkgdesc="A RSS feed reader for the text console with special Podcast support"
arch=('i686' 'x86_64')
url="http://www.newsbeuter.org/"
license=('custom: MIT')
depends=('curl' 'json-c' 'libxml2' 'sqlite' 'stfl')
makedepends=('swig' 'gettext')
optdepends=('ruby')
options=('!libtool')
install=$pkgname.install
changelog=$pkgname.changelog
source=(http://www.newsbeuter.org/downloads/$pkgname-$pkgver.tar.gz)
sha256sums=('3189fdd3c65b3207f9ffb3a10f6fda4e92ea927776cb49a79c1e4c0344f9dcc3')

build() {
  cd ${srcdir}/$pkgname-$pkgver

  make prefix=/usr
}

package() {
  cd ${srcdir}/$pkgname-$pkgver

  make prefix=/usr DESTDIR=$pkgdir install

#license
  install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/$pkgname/LICENSE
}

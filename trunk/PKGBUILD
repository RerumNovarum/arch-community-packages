# $Id$
# Maintainer : Ionut Biru <ionut@archlinux.ro>
# Contributor: Dieter Plaetinck <dieter@plaetinck.be>
pkgbase=uzbl
pkgname=('uzbl-core' 'uzbl-browser')
pkgver=2009.11.30
pkgrel=1
arch=('i686' 'x86_64')
url="http://www.uzbl.org"
license=('GPL3')
makedepends=('git' 'pkgconfig' 'libwebkit>=1.1.15.4')
source=(uzbl-$pkgver.tar.gz::http://github.com/Dieterbe/uzbl/tarball/$pkgver)
md5sums=('752b078894c8e1c16481f05ddccd1cf0')

build() {
    cd "$srcdir"/Dieterbe-uzbl-*
    make || return 1
}
package_uzbl-core(){
   pkgdesc="Main component meant for integration with other tools and scripts"
   depends=('libwebkit>=1.1.15.4')
   optdepends=('socat: to interface with the socket' 'dmenu: to run some of the example scripts'
               'zenity: to run some of the example scripts'
               'python: to run some of the example scripts' 'perl: for the example formfiller'
               'xclip: for clipboard related keybindings')

   cd "$srcdir"/Dieterbe-uzbl-*
   make DESTDIR="$pkgdir" PREFIX=/usr  install-uzbl-core || return 1
}

package_uzbl-browser(){
   pkgdesc="A complete browser experience based on uzbl-core"
   depends=("uzbl-core=$pkgver")
   cd "$srcdir"/Dieterbe-uzbl-*
   make DESTDIR="$pkgdir" PREFIX=/usr  install-uzbl-browser || return 1
}


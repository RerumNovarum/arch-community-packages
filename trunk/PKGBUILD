# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>

pkgname=processing
pkgver=2.0.3
pkgrel=1
arch=('x86_64' 'i686')
pkgdesc='Programming environment for creating images, animations and interactions'
url='http://www.processing.org/'
license=('GPL' 'LGPL')
depends=('libgl' 'sh' 'libxxf86vm')
makedepends=('gendesk')
options=(!strip)

if [ "$CARCH" == "x86_64" ]; then
  source=("http://download.$pkgname.org/$pkgname-$pkgver-linux64.tgz"
          "$pkgname.png::http://wiki.$pkgname.org/skins/${pkgname}skin/${pkgname}_wiki_logo.png")
  sha256sums=('8d36995fb1ea0390fe6c3110372316c4b3f86e81af6a34d4e0227f4bb109f707'
            '019ae700333409acbc628779747ab8f153f6647de25e4de3a9bd4a42d0d8056a')
else
  source=("http://download.$pkgname.org/$pkgname-$pkgver-linux32.tgz"
          "$pkgname.png::http://wiki.$pkgname.org/skins/${pkgname}skin/${pkgname}_wiki_logo.png")
  sha256sums=('4dc0700fabce04465f1183c87939076655b9b055263cd0c5900d41bb4fa284e3'
            '019ae700333409acbc628779747ab8f153f6647de25e4de3a9bd4a42d0d8056a')
fi

prepare() {
  gendesk --pkgname="$pkgname" --pkgdesc="$pkgdesc"
}

package() {
  cd "$pkgname-$pkgver"
  
  install -d "$pkgdir/usr/share/$pkgname/" "$pkgdir/usr/bin/"
  cp -r * "$pkgdir/usr/share/$pkgname/"
  ln -s "/usr/share/$pkgname/$pkgname" "$pkgdir/usr/bin/$pkgname"
  ln -s "/usr/share/$pkgname/$pkgname-java" "$pkgdir/usr/bin/$pkgname-java"
  install -Dm644 "$srcdir/$pkgname.png" \
    "$pkgdir/usr/share/pixmaps/$pkgname.png"
  install -Dm644 "$srcdir/$pkgname.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
}

# vim:set ts=2 sw=2 et:

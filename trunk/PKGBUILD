# $Id$
# Contributor: Francois Boulogne <fboulogne at april dot org>
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=subsurface-marble
pkgver=4.4.1
pkgrel=1
pkgdesc='Virtual Globe'
url='http://git.subsurface-divelog.org/'
license=('GPL' 'LGPL' 'FDL')
arch=('i686' 'x86_64')
makedepends=('git' 'cmake')
depends=('qt5-'{script,svg,tools,webkit})
source=("http://subsurface-divelog.org/downloads/${pkgname}-${pkgver}.tgz")
sha1sums=('4867a8219cd2dcf478414ad91ff4479ad2afc1a3')

build() {
	cd "${srcdir}/${pkgname}"
	cmake \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release \
		-DQT5BUILD=ON \
		-DQTONLY=ON \
		-DBUILD_MARBLE_APPS=OFF \
		-DBUILD_MARBLE_EXAMPLES=OFF \
		-DBUILD_MARBLE_TESTS=OFF \
		-DBUILD_MARBLE_TOOLS=OFF \
		-DBUILD_TESTING=OFF \
		-DWITH_DESIGNER_PLUGIN=OFF \
		-DBUILD_WITH_DBUS=OFF \
		.
}

package() {
	cd "${srcdir}/${pkgname}"
	make DESTDIR="${pkgdir}" install

	cd "${pkgdir}/usr"
	mv include subsurface
	install -d include
	mv subsurface include
        rm -fr share/{appdata,icons}
}

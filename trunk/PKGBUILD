# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Roman Kyrylych <Roman.Kyrylych@gmail.com>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=gnome-schedule
pkgver=2.2.0
pkgrel=3
pkgdesc='Graphical interface to crontab and at for GNOME'
arch=('any')
url="http://gnome-schedule.sourceforge.net/"
license=('GPL')
depends=('python2-gconf' 'at' 'cron')
makedepends=('gnome-python' 'intltool' 'gnome-doc-utils')
optdepends=('gnome-terminal: run tasks manually')
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz
        drop-gnome-python.patch)
md5sums=('5036cc2508b58a86b6c70e2f33030c65'
         '6d5663b21f369c1d5c681097eaf14b6d')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # https://bugzilla.gnome.org/show_bug.cgi?id=675753
  patch -Np1 -i "$srcdir/drop-gnome-python.patch"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}

# $Id$
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Roman Kyrylych <roman@archlinux.org>
# Contributor: Tor Krill <tor@krill.nu>

pkgname=nautilus-actions
pkgver=3.0.4
pkgrel=2
pkgdesc="Configures programs to be launched when files are selected in Nautilus"
arch=('i686' 'x86_64')
url="http://gnome.org"
license=('GPL')
depends=('nautilus' 'libgtop' 'desktop-file-utils' )
makedepends=('intltool' 'gnome-doc-utils>=0.20.0')
install=nautilus-actions.install
options=('!libtool')
source=(http://ftp.gnome.org/pub/GNOME/sources/nautilus-actions/3.0/$pkgname-$pkgver.tar.bz2
        fix_pathname_breakdown.patch)
sha256sums=('93cb7b81d3c1ea6fef5609a6df554157f49ec8799e5383925ff4e88e525c1947'
            '378a605e75f6edc7626dee5da58a598c00740717ff5112d69a090923c60e60f5')

build() {
    cd ${srcdir}/${pkgname}-${pkgver}
    patch -Np1 -i ${srcdir}/fix_pathname_breakdown.patch
    ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
    make
}

package(){
    cd ${srcdir}/${pkgname}-${pkgver}
    make  GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR=${pkgdir} install
    mkdir -p ${pkgdir}/usr/share/gconf/schemas
    gconf-merge-schema ${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas --domain nautilus-actions \
        ${pkgdir}/etc/gconf/schemas/*.schemas
    rm -rf ${pkgdir}/etc/gconf/schemas/
}

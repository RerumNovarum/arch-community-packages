# $Id$
# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: Corrado Primier <bardo@aur.archlinux.org>
# Contributor: Eric Belanger <belanger@astro.umontreal.ca>
# Contributor: Andrew Conkling <andrewski@fr.st>

pkgname=gourmet
pkgver=0.15.6
pkgrel=1
pkgdesc="A simple but powerful recipe-managing application"
arch=('i686' 'x86_64')
url="http://grecipe-manager.sourceforge.net"
license=('GPL')
depends=('gnome-python-extras' 'libglade' 'metakit' 'python-imaging'
         'python-pysqlite' 'python-reportlab' 'python2-sqlalchemy')
makedepends=('intltool')
optdepends=('libgnomeprintui: gnome printing support'
            'pyrtf: RTF support')
changelog=$pkgname.changelog
source=(http://downloads.sourceforge.net/grecipe-manager/$pkgname-$pkgver.tar.gz)
md5sums=('154e572825cc0201eb41332ca4341924')

package() {
  cd "$srcdir/$pkgname-$pkgver"

  # python2 fix
  for file in $(find . -name '*.py' -print); do
    sed -i 's_#!/usr/bin/python_#!/usr/bin/python2_' $file
    sed -i 's_#!/usr/bin/env python_#!/usr/bin/env python2_' $file
  done

  python2 setup.py install --root="$pkgdir"
}

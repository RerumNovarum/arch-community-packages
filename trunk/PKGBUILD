# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=(lua-zlib lua51-zlib)
pkgbase=lua-zlib
pkgver=0.2
pkgrel=6
_gitver=8d07669
url="http://github.com/brimworks/lua-zlib"
pkgdesc='Simple streaming interface to zlib for Lua.'
arch=('i686' 'x86_64')
license=('MIT')
makedepends=('cmake' 'lua' 'lua51' 'zlib')
conflicts=('lua-lzlib')
source=("lua-zlib-$pkgver.zip::https://github.com/brimworks/lua-zlib/zipball/v$pkgver")
md5sums=('e6c780c0c4b6dcb88cf16949bb751be7')

build() {
  cp -a $srcdir/brimworks-lua-zlib-${_gitver} $srcdir/brimworks-lua51-zlib-${_gitver}

  cd $srcdir/brimworks-lua-zlib-${_gitver}
  gcc -o zlib.so -O2 -fPIC -shared lua_zlib.c -llua

  cd $srcdir/brimworks-lua51-zlib-${_gitver}
  gcc -o zlib.so -O2 -fPIC -shared -I/usr/include/lua5.1 lua_zlib.c -llua5.1
}

package_lua-zlib() {
  depends=('zlib' 'lua')

  cd $srcdir/brimworks-lua-zlib-${_gitver}
  install -Dm0755 zlib.so $pkgdir/usr/lib/lua/5.2/zlib.so
  install -Dm644 "README" "$pkgdir/usr/share/licenses/$pkgname/README"
}

package_lua51-zlib() {
  depends=('zlib' 'lua51')

  cd $srcdir/brimworks-lua51-zlib-${_gitver}
  install -Dm0755 zlib.so $pkgdir/usr/lib/lua/5.1/zlib.so
  install -D -m644 "README" "$pkgdir/usr/share/licenses/$pkgname/README"
}

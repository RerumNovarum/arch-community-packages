# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: jlvsimoes

pkgname=osiris
pkgver=4.2.3
pkgrel=5
pkgdesc="A file integrity management system"
arch=('i686' 'x86_64')
url="http://osiris.shmoo.com/"
license=('custom')
depends=('openssl' 'readline')
install=$pkgname.install
#source=(http://osiris.shmoo.com/data/osiris-$pkgver.tar.gz
source=(http://arch.p5n.pp.ru/~sergej/dl/2011/osiris-$pkgver.tar.gz
	osirismd.init
	osirisd.init
	osirismd.service
	osirisd.service)
md5sums=('1951c7dc0fe729af9ffaf58910340d12'
         '96c1ba5433bd0ed7d9520583a07b9bdd'
         'b6dde0a539ca798fa81489f717507f25'
         '48644676b88b231803785b8e2da956e5'
         'c0355b453770f6bbc6a58f5cd80766b4')

build() {
  cd $srcdir/$pkgname-$pkgver

  ./configure --prefix=/usr --enable-fancy-cli  --with-md-root-dir=/usr/share/$pkgname
  make all

  # Arch-ify install locations
  sed -i "s|^INSTALL_OSIRISM=0|INSTALL_OSIRISM=1|" src/install/install.sh
  sed -i "s|^INSTALL_OSIRISD=0|INSTALL_OSIRISD=1|" src/install/install.sh
  sed -i "s|^ROOT_GROUP=wheel|ROOT_GROUP=root|" src/install/install.sh
}

package() {
  cd $srcdir/$pkgname-$pkgver

  mkdir -p $pkgdir/usr/share/licenses/$pkgname/
  install -m644 LICENSE $pkgdir/usr/share/licenses/$pkgname/license.txt
  mkdir -p $pkgdir/etc/rc.d

  make DESTDIR=$pkgdir install

  install -D -m755 $srcdir/osirismd.init $pkgdir/etc/rc.d/osirismd
  install -D -m755 $srcdir/osirisd.init $pkgdir/etc/rc.d/osirisd
  rm -rf $pkgdir/usr/osiris

  install -Dm0644 $srcdir/osirisd.service $pkgdir/usr/lib/systemd/system/osirisd.service
  install -Dm0644 $srcdir/osirismd.service $pkgdir/usr/lib/systemd/system/osirismd.service
}

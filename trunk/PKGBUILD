# $Id$
# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Hugo Doria <hugo@archlinux.org>

pkgname=billreminder
pkgver=0.4.0
pkgrel=1
pkgdesc="Small and quick accounting application designed to allow for easy tracking of bills"
arch=('any')
url="http://billreminder.gnulinuxbrasil.org/"
license=('GPL3')
#depends=('python-pysqlite' 'python-notify' 'dbus-python' 'setuptools' 'desktop-file-utils' 'pygobject' 'pygtk' 'pil' 'perlxml' 'xdg-utils')
depends=('python-pysqlite' 'dbus-python' 'setuptools' 'desktop-file-utils' 'pygtk' 'xdg-utils' 'python2-gconf' 'python2-sqlalchemy')
makedepends=('intltool')
install=billreminder.install
source=(http://ftp.gnome.org/pub/GNOME/sources/billreminder/${pkgver%.*}/${pkgname}-${pkgver}.tar.bz2)
md5sums=('7d7a3094258a02eaff611f2633cc34e6')
sha1sums=('cc9f67a40cd7339dbe94c6a2f01d1473c07855a7')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # python2 fix
  for file in billreminder.in billreminderd.in; do
    sed -i 's_#!/usr/bin/env python_#!/usr/bin/env python2_' $file
  done

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --with-gconf-schema-file-dir=/usr/share/gconf/schemas

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
}

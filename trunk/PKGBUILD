# $Id$
# Maintainer: speps <speps at aur dot archlinux dot org>

_name=QupZilla
pkgname=qupzilla
pkgver=1.3.0
pkgrel=1
pkgdesc="Cross-platform QtWebKit browser"
arch=(i686 x86_64)
url="http://www.qupzilla.com/"
license=('GPL3')
depends=('qtwebkit' 'gtk-update-icon-cache')
install="$pkgname.install"
source=("$pkgname-$pkgver.tar.gz::https://github.com/$_name/$pkgname/tarball/v$pkgver")
md5sums=('87073c6780473c2e9841123625642242')

build() {
  cd "$srcdir/$_name-$pkgname-"*

  # enable webgl support
  export USE_WEBGL=true

  qmake QUPZILLA_PREFIX=/usr/
  make
}

package() {
  cd "$srcdir/$_name-$pkgname-"*
  make INSTALL_ROOT="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:

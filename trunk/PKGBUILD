# $Id$
# Maintainer:
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: see .contrib

pkgname=gourmet
pkgver=0.16.0
pkgrel=2
pkgdesc="A simple but powerful recipe-managing application"
arch=('any')
url="http://thinkle.github.io/gourmet/"
license=('GPL')
depends=('pygtk' 'python2-pillow' 'python2-sqlalchemy' 'hicolor-icon-theme')
makedepends=('python2-distutils-extra')
optdepends=('gstreamer0.10-python: play timer sounds'
            'ipython2: Python Shell plugin'
            'pyrtf: RTF support'
            'python2-beautifulsoup3: Webpage import plugin'
            'python2-gtkspell: Spell Checking plugin'
            'python2-poppler: Printing and PDF Export plugin'
            'python2-reportlab: Printing and PDF Export plugin')
changelog=$pkgname.changelog
install=$pkgname.install
source=("https://launchpad.net/gourmet/main/$pkgver/+download/$pkgname-$pkgver.tar.bz2"
        "pil.patch")
md5sums=('6a4ddb80b40e1bee7797697f03c9ef82'
         '6e90c8021d5949525e565c2607d2e6e6')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  # python2 fix
  for file in $(find . -name '*.py' -print); do
    sed -i 's_#!/usr/bin/python_#!/usr/bin/python2_' $file
    sed -i 's_#!/usr/bin/env python_#!/usr/bin/env python2_' $file
  done

  # python-imaging -> python-pillow (relative PIL import)
  patch -Np1 -i "$srcdir/pil.patch"
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  python2 setup.py install --root="$pkgdir" --optimize=1
}

# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Adrian Sampson <adrian@radbox.org>

pkgname=python2-munkres
pkgver=1.0.8
pkgrel=1
pkgdesc='Munkres algorithm for the assignment problem'
arch=('any')
url='https://github.com/bmc/munkres'
license=('Apache')
depends=('python2')
makedepends=('python2-setuptools')
source=("https://pypi.python.org/packages/07/17/5debec658b151d39c7a29fcc830bbfb5cb22f18e7ff7d6537deab73943ab/munkres-${pkgver}.tar.gz")
sha256sums=('185f1a9c4d2c31f2f19afa48bc2ec726c11e945eded4784d272da2fd49bf7a55')

prepare() {
  cd munkres-${pkgver}

  sed -i 's|^#!.*python$|#!/usr/bin/python2|' $(grep -rl '^#!.*python')
}

build() {
  cd munkres-${pkgver}

  python2 setup.py build
}

package() {
  cd munkres-${pkgver}

  python2 setup.py install --root="${pkgdir}" --optimize='1'

  install -dm 755 "${pkgdir}"/usr/share/licenses/python2-munkres
  install -m 644 LICENSE "${pkgdir}"/usr/share/licenses/python2-munkres/
}

# vim: ts=2 sw=2 et:

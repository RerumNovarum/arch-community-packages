# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributer: mpie <michael.kyne-phillips1@ntlworld.com>

pkgname=parrot
pkgver=3.10.0
_rel=devel
#_rel=stable
pkgrel=2
pkgdesc="standalone virtual machine that can be used to execute bytecode compiled dynamic languages"
arch=(i686 x86_64)
url="http://www.parrotcode.org/"
license=('GPL')
depends=('gmp' 'gdbm' 'readline' 'icu' 'openssl' 'libffi')
makedepends=('perl-json')
optdepends=('freeglut')
options=(!emptydirs !makeflags)
source=(ftp://ftp.parrot.org/pub/parrot/releases/${_rel}/$pkgver/parrot-$pkgver.tar.gz)
md5sums=('36a839beaef511f178b0607a03f1ff0f')

build() {
  cd $srcdir/$pkgname-$pkgver

  sed -i 's#auto::sha1##' lib/Parrot/Configure/Step/List.pm
  sed -i 's#auto::git_describe##' lib/Parrot/Configure/Step/List.pm

  [ $NOEXTRACT -eq 1 ] || perl Configure.pl --prefix=/usr --parrot_is_shared

  find -type f -name Makefile | while read F; do
    grep "$srcdir" $F  && sed -i "s#-Wl,-rpath=$srcdir/parrot-$pkgver/blib/lib##" $F || true
    grep "$srcdir" $F  && sed -i "s#-rpath=$srcdir/parrot-$pkgver/blib/lib##" $F || true
  done

  OLD_LIB_PATH=$LD_LIBRARY_PATH
  export LD_LIBRARY_PATH=$LD_LIBRARY_PATH:$srcdir/parrot-$pkgver/blib/lib
  make -j1 all parrot_utils docs html RPATH_BLIB=

  export LD_LIBRARY_PATH=`pwd`/blib/lib
  make -j1 DESTDIR=$pkgdir install-dev

  sed -i "s#$srcdir#/usr/src#" $pkgdir/usr/lib/parrot/$pkgver/tools/lib/Parrot/Config/Generated.pm

  export LD_LIBRARY_PATH=$OLD_LIB_PATH
}

# $Id$
# Maintainer:
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: Stefano Zamprogno <stefano.zamprogno@gmail.com>

pkgname=kmess
pkgver=2.0.6.2
pkgrel=1
pkgdesc='MSN Messenger client for Linux'
arch=('i686' 'x86_64')
url='http://kmess.sourceforge.net/'
license=('GPL')
depends=('kdebase-lib')
makedepends=('automoc4' 'cmake')
optdepends=('cabextract: provides winks support')
install=${pkgname}.install
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-$pkgver.tar.bz2")
md5sums=('a2e57911713308bef81b9347135cc5dd')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}

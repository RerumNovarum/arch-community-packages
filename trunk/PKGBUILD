# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Det <nimetonmaili at gmail a-dot com>
# Contributor: Godane aka Christopher Rogers <slaxemulator@gmail.com>
pkgname=unetbootin
pkgver=565
pkgrel=1
pkgdesc="Create bootable Live USB drives"
url="http://unetbootin.sourceforge.net/"
license=('GPL')
arch=('x86_64' 'i686')
depends=('syslinux' 'p7zip' 'qt' 'mtools')
optdepends=('gksu: for graphically requesting the root password'
            'kdebase-runtime: for graphically requesting the root password')
install=$pkgname.install
source=("http://downloads.sourceforge.net/project/$pkgname/UNetbootin/$pkgver/$pkgname-source-$pkgver.tar.gz")
sha1sums=('4c9fa0ccad6940ac4fd96a79f891dc47fff232d0')

build() {
  lupdate "$pkgname.pro"
  lrelease "$pkgname.pro"
  qmake
  make
}

package() {
  install -Dm755 "$pkgname" "$pkgdir/usr/bin/$pkgname"
  mkdir -p "$pkgdir/usr/share/$pkgname"
  install -m644 "$pkgname"_*.qm "$pkgdir/usr/share/$pkgname/"
  install -Dm644 "$pkgname.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
  for i in 16 22 24 32 48 256; do
      install -D "${pkgname}_${i}.png" \
        "$pkgdir/usr/share/icons/hicolor/${i}x${i}/apps/$pkgname.png"
  done
}

# vim:set ts=2 sw=2 et:

# $Id$
# Maintainer: speps <speps at aur dot archlinux dot org>

_name=QupZilla
_commit=93d22ec # fixes https://github.com/QupZilla/qupzilla/issues/793
pkgname=qupzilla
pkgver=1.4.0_git20130312
pkgrel=1
pkgdesc="Cross-platform QtWebKit browser"
arch=(i686 x86_64)
url="http://www.qupzilla.com/"
license=('GPL3')
depends=('qtwebkit' 'hunspell')
optdepends=('bash-completion: bash completion support')
install="$pkgname.install"
source=("$pkgname-$pkgver.tar.gz::https://github.com/$_name/$pkgname/tarball/$_commit")
md5sums=('8fa69f60a26b55d68bfac2d7ec812bf8')

build() {
  cd "$srcdir/$_name-$pkgname-"*

  # enable webgl support
  export USE_WEBGL=true

  # manually install bash completion
  sed -i '/bashcompletion/d' src/install.pri

  qmake-qt4 QUPZILLA_PREFIX=/usr/
  make
}

package() {
  cd "$srcdir/$_name-$pkgname-"*
  make INSTALL_ROOT="$pkgdir/" install

  # bash completion
  install -Dm644 linux/completion/bash_completion.sh \
    "$pkgdir/usr/share/bash-completion/completions/$pkgname"

  # zsh completion
  install -Dm644 linux/completion/zsh_completion.sh \
    "$pkgdir/usr/share/zsh/site-functions/_$pkgname"
}

# vim:set ts=2 sw=2 et:

# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Det <nimetonmaili at gmail a-dot com>
# Contributor: Godane aka Christopher Rogers <slaxemulator@gmail.com>
# Contributor: Michai Coman <mihai@m1x.ro>

pkgname=unetbootin
pkgver=581
pkgrel=2
arch=('x86_64' 'i686')
license=('GPL')
pkgdesc="Create bootable Live USB drives"
url="http://unetbootin.sourceforge.net/"
depends=('syslinux' 'p7zip' 'qt' 'mtools' 'xorg-xmessage' 'desktop-file-utils' 'gtk-update-icon-cache' 'polkit')
install=$pkgname.install
source=("http://downloads.sourceforge.net/project/$pkgname/UNetbootin/$pkgver/$pkgname-source-$pkgver.tar.gz"
        "$pkgname.sh"
        'org.archlinux.pkexec.unetbootin.policy')
sha256sums=('b8bf5972653c023c159358f0e4c743dfaf1c6c27f28fd15cd97739d08c751509'
            'bf81b5ebb027d9b88fa70bb17a877e10319e171bb8c041b57f0bbfc30f79b66f'
            '4de02f754f86707c92d20b1c7cfda323893d3d4ed54047b06c036588e677a87c')

build() {
  cd "$srcdir"

  lupdate "$pkgname.pro"
  lrelease "$pkgname.pro"
  qmake
  make
}

package() {
  cd "$srcdir"

  install -Dm755 "$pkgname.sh" "$pkgdir/usr/bin/$pkgname"
  install -Dm755 "$pkgname" "$pkgdir/usr/bin/$pkgname.elf"
  mkdir -p "$pkgdir/usr/share/$pkgname"
  install -m644 "$pkgname"_*.qm "$pkgdir/usr/share/$pkgname/"
  install -Dm644 "$pkgname.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
  for i in 16 22 24 32 48 256; do
    install -D "${pkgname}_${i}.png" \
      "$pkgdir/usr/share/icons/hicolor/${i}x${i}/apps/$pkgname.png"
  done
  install -Dm644 'org.archlinux.pkexec.unetbootin.policy' \
    "$pkgdir/usr/share/polkit-1/actions/org.archlinux.pkexec.unetbootin.policy"
}

# vim:set ts=2 sw=2 et:

# $Id$
# Maintainer: speps <speps at aur dot archlinux dot org>

_name=QupZilla
pkgname=qupzilla
pkgver=1.3.5
pkgrel=2
pkgdesc="Cross-platform QtWebKit browser"
arch=(i686 x86_64)
url="http://www.qupzilla.com/"
license=('GPL3')
depends=('qtwebkit')
install="$pkgname.install"
source=("$pkgname-$pkgver.tar.gz::https://github.com/$_name/$pkgname/tarball/v$pkgver")
md5sums=('95d19c0e22c6dbf605b760125a4b7ef3')

build() {
  cd "$srcdir/$_name-$pkgname-"*

  # enable webgl support
  export USE_WEBGL=true

  qmake QUPZILLA_PREFIX=/usr/
  make
}

package() {
  cd "$srcdir/$_name-$pkgname-"*
  make INSTALL_ROOT="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:

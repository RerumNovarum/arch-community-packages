# $Id$
# Maintainer: Jonathan Conder <jonno.conder@gmail.com>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgbase=mythplugins
pkgname=('mythplugins-mytharchive'
         'mythplugins-mythbrowser'
         'mythplugins-mythgallery'
         'mythplugins-mythgame'
         'mythplugins-mythmusic'
         'mythplugins-mythnetvision'
         'mythplugins-mythnews'
         'mythplugins-mythvideo'
         'mythplugins-mythweather'
         'mythplugins-mythweb'
         'mythplugins-mythzoneminder')
pkgver=0.24.1
pkgrel=4
epoch=1
arch=('i686' 'x86_64')
url="http://www.mythtv.org"
license=('GPL')
makedepends=('cdparanoia' 'cdrkit' 'dvdauthor' 'dvd+rw-tools' 'ffmpeg' 'flac'
             'libcdaudio' 'libexif' 'libvisual' 'libvorbis' 'mplayer' 'mythtv'
	     'perl-datetime-format-iso8601' 'perl-date-manip' 'perl-image-size'
	     'perl-libwww' 'perl-soap-lite' 'perl-xml-sax' 'perl-xml-simple'
	     'perl-xml-xpath' 'python2-oauth' 'python-imaging' 'python-pycurl'
	     'sdl' 'taglib' 'zlib')
source=("ftp://ftp.osuosl.org/pub/mythtv/$pkgbase-$pkgver.tar.bz2"
        'mtd.rc')
md5sums=('76aeefeff70b550b2d35c2d9d751df18'
         '476c12ba074794ad7f4ae092bdf949d6')

build() {
  cd "$srcdir/$pkgbase-$pkgver"
  find . -name '*.py' -type f | xargs sed -i 's@^#!.*python$@#!/usr/bin/python2@'

  ./configure --prefix=/usr \
              --enable-all \
               --python=python2
  qmake mythplugins.pro
  make
}

package_mythplugins-mytharchive() {
  pkgdesc="Create DVDs or archive recorded shows in MythTV"
  depends=('cdrkit' 'dvdauthor' 'dvd+rw-tools' 'ffmpeg' 'mythtv'
           'python-imaging')

  cd "$srcdir/$pkgbase-$pkgver/mytharchive"
  make INSTALL_ROOT="$pkgdir" install
}

package_mythplugins-mythbrowser() {
  pkgdesc="Mini web browser for MythTV"
  depends=('mythtv')

  cd "$srcdir/$pkgbase-$pkgver/mythbrowser"
  make INSTALL_ROOT="$pkgdir" install
}

package_mythplugins-mythgallery() {
  pkgdesc="Image gallery plugin for MythTV"
  depends=('libexif' 'mythtv')

  cd "$srcdir/$pkgbase-$pkgver/mythgallery"
  make INSTALL_ROOT="$pkgdir" install
}

package_mythplugins-mythgame() {
  pkgdesc="Game emulator plugin for MythTV"
  depends=('mythtv')

  cd "$srcdir/$pkgbase-$pkgver/mythgame"
  make INSTALL_ROOT="$pkgdir" install
}

package_mythplugins-mythmusic() {
  pkgdesc="Music playing plugin for MythTV"
  depends=('cdparanoia' 'flac' 'libcdaudio' 'libvisual' 'libvorbis' 'mythtv'
           'sdl' 'taglib')

  cd "$srcdir/$pkgbase-$pkgver/mythmusic"
  make INSTALL_ROOT="$pkgdir" install
}

package_mythplugins-mythnetvision() {
  pkgdesc="MythNetvision plugin for MythTV"
  depends=('mythtv' 'python2-oauth' 'python-pycurl')

  cd "$srcdir/$pkgbase-$pkgver/mythnetvision"
  make INSTALL_ROOT="$pkgdir" install
}

package_mythplugins-mythnews() {
  pkgdesc="News checking plugin for MythTV"
  depends=('mythtv')

  cd "$srcdir/$pkgbase-$pkgver/mythnews"
  make INSTALL_ROOT="$pkgdir" install
}

package_mythplugins-mythvideo() {
  pkgdesc="Video playback and browsing plugin for MythTV"
  depends=('mplayer' 'mythtv')

  cd "$srcdir/$pkgbase-$pkgver/mythvideo"
  make INSTALL_ROOT="$pkgdir" install

  install -D -m755 "$srcdir/mtd.rc" "$pkgdir/etc/rc.d/mtd"
}

package_mythplugins-mythweather() {
  pkgdesc="Weather checking plugin for MythTV"
  depends=('mythtv' 'perl-date-manip' 'perl-libwww'
           'perl-soap-lite' 'perl-xml-sax' 'perl-xml-simple' 'perl-xml-xpath'
	   'perl-image-size' 'perl-datetime-format-iso8601')

  cd "$srcdir/$pkgbase-$pkgver/mythweather"
  make INSTALL_ROOT="$pkgdir" install
}

package_mythplugins-mythweb() {
  pkgdesc="Web interface for the MythTV scheduler"
  depends=('mythtv')
  optdepends=('lighttpd'
              'php-apache')

  mkdir -p "$pkgdir/srv/http/mythweb"/{image_cache,php_sessions}
  cp -R "$srcdir/$pkgbase-$pkgver/mythweb"/* "$pkgdir/srv/http/mythweb"
  chown -R http:http "$pkgdir/srv/http/mythweb"
  chmod g+rw "$pkgdir/srv/http/mythweb"/{image_cache,php_sessions}
}

package_mythplugins-mythzoneminder() {
  pkgdesc="View CCTV footage from zoneminder in MythTV"
  depends=('mythtv')

  install='mythplugins-mythzoneminder.install'
  cd "$srcdir/$pkgbase-$pkgver/mythzoneminder"
  make INSTALL_ROOT="$pkgdir" install
}

# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org> 

pkgname=discover
pkgver=5.5.0
pkgrel=2
pkgdesc='A collection of package management tools for KDE'
arch=(i686 x86_64)
url='https://projects.kde.org/projects/kde/workspace/discover'
license=(LGPL)
depends=(packagekit-qt5 knewstuff kdeclarative appstream-qt archlinux-appstream-data hicolor-icon-theme)
makedepends=(extra-cmake-modules python plasma-framework)
conflicts=(muon)
replaces=(muon)
source=("http://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"
	fix-metadata.patch::"https://quickgit.kde.org/?p=discover.git&a=commitdiff&h=fbb13a4b&o=plain")
install=$pkgname.install
md5sums=('992c92983197ddc4826eddfd72721e2e'
         'a913837026f52c34f602a59063d65c4c')

prepare() {
  mkdir -p build

# Fix desktiop file metadata http://bugs.kde.org/show_bug.cgi?id=356568
  cd $pkgname-$pkgver
  patch -p1 -i ../fix-metadata.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}

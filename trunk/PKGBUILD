# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Vinay S Shastry <vinayshastry@gmail.com>

pkgname=qmpdclient
pkgver=1.1.3
pkgrel=2
pkgdesc="A Qt4 client for MPD"
arch=('i686' 'x86_64')
url="http://bitcheese.net/wiki/QMPDClient"
license=('GPL')
depends=('qt' 'openssl')
makedepends=('cmake')
provides=('qmpdclient-ne')
conflicts=('qmpdclient-ne')
replace=('qmpdclient-ne')
source=(http://dump.bitcheese.net/files/${pkgname}-${pkgver}.tar.bz2 \
	$pkgname.desktop)
md5sums=('535f4dd5aa853d7f24317f0619dc748c'
         '22715a2dbf8501d097e10f7bc0ba122c')

build() {
  cd ${srcdir}

  mkdir build
  cd build

  cmake ../${pkgname} \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd ${srcdir}/build

  make DESTDIR=${pkgdir} install

  # install icons and desktop file
  install -d ${pkgdir}/usr/share/pixmaps/
  install -m644 ${srcdir}/${pkgname}/icons/qmpdclient{16,22,64}.png \
    ${pkgdir}/usr/share/pixmaps/
  install -m644 -D ${srcdir}/$pkgname.desktop \
    ${pkgdir}/usr/share/applications/$pkgname.desktop
}

# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski
# Contributor: Brad Fanella <bradfanella@archlinux.us>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Tomas A. Schertel <tschertel@gmail.com>

pkgname=cherrytree
pkgver=0.30.0
pkgrel=1
pkgdesc='Hierarchical note taking application featuring rich text and syntax highlighting'
arch=('any')
url='http://www.giuspen.com/cherrytree/'
license=('GPL')
depends=('pygtksourceview2' 'python2-pyenchant' 'hicolor-icon-theme' 'desktop-file-utils')
install=$pkgname.install
source=(http://www.giuspen.com/software/$pkgname-$pkgver.tar.xz)
sha256sums=('b2280322f006e6cc1269a78d8041298b655a422a39671cb417e5faaebf8bda12')

prepare() {
  cd $pkgname-$pkgver
  # python2 fix
  for file in $(find . -name '*.py') cherrytree; do
    sed -i 's_/usr/bin/env python_/usr/bin/env python2_' ${file}
  done
}
	
package() {
  cd $pkgname-$pkgver
  python2 setup.py install --root="$pkgdir/" --optimize=1
}

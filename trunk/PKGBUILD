# $Id$
# Maintainer: Jonathan Conder <jonno.conder@gmail.com>
# Contributor: Jaroslaw Swierczynski <swiergot@aur.archlinux.org>
# Contributor: Camille Moncelier <pix@devlife.org>

pkgname=linuxtv-dvb-apps
pkgver=1452
pkgrel=1
_hgrev=d4e8bf5658ce
pkgdesc='Viewer programs for DVB cards'
arch=('i686' 'x86_64')
url='http://www.linuxtv.org/'
license=('GPL')
source=("http://linuxtv.org/hg/dvb-apps/archive/$_hgrev.tar.bz2")
sha256sums=('c3bb865ea5c8dd97050d7b3a68ee158dadff50a2fb948d955b3a559be3cf31c5')

build() {
  cd "dvb-apps-$_hgrev"
  rm -rf include

  make -C lib
  make -C util
}

package() {
  cd "dvb-apps-$_hgrev"

  make -C lib DESTDIR="$pkgdir" install
  make -C util DESTDIR="$pkgdir" install

  chmod 755 "$pkgdir"/usr/lib/*.so*
}

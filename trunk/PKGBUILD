# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Ryan Coyner <rcoyner@gmail.com>

pkgname=('mod_wsgi' 'mod_wsgi2')
pkgbase=mod_wsgi
pkgver=3.4
pkgrel=2
pkgdesc='Python WSGI adapter module for Apache'
arch=('x86_64' 'i686')
url='http://www.modwsgi.org/'
license=('APACHE')
depends=('apache')
makedepends=('python' 'python2' 'apache' 'setconf')
install=mod_wsgi.install
source=("http://modwsgi.googlecode.com/files/$pkgname-$pkgver.tar.gz")
sha256sums=('ae85c98e9e146840ab3c3e4490e6774f9bef0f99b9f679fca786b2adb5b4b6e8')

package_mod_wsgi() {
  depends=('python' 'apache')
  cd "$srcdir/$pkgbase-$pkgver"

  rm -f Makefile
  ./configure --prefix=/usr \
    --with-apxs=/usr/sbin/apxs \
    --with-python=/usr/bin/python
  setconf Makefile LDLIBS '-lpython3 -lpthread -ldl -lutil -lm'
  make DESTDIR="$pkgdir" install
}

package_mod_wsgi2() {
  depends=('python2' 'apache')
  cd "$srcdir/$pkgbase-$pkgver"

  rm -f Makefile
  ./configure --prefix=/usr \
    --with-apxs=/usr/sbin/apxs \
    --with-python=/usr/bin/python2
  make DESTDIR="$pkgdir" install
  mv "$pkgdir/usr/lib/httpd/modules/$pkgbase.so" \
    "$pkgdir/usr/lib/httpd/modules/$pkgname.so"
}

# vim:set ts=2 sw=2 et:

# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: Doehni <dohny@gmx.de>

pkgname=gnome-do
pkgver=0.8.5
pkgrel=2
pkgdesc="A launcher application for gnome (like Launchy or Katapult)"
arch=('i686' 'x86_64')
url="http://do.davebsd.com/"
license=('GPL3')
depends=('gconf-sharp' 'libgnome-desktop-sharp' 'gnome-keyring-sharp' 'libgnome-sharp'
         'mono-addins' 'notify-sharp-svn' 'hicolor-icon-theme')
makedepends=('intltool' 'ca-certificates' 'rsvg2-sharp' 'wnck-sharp')
options=('!libtool')
install=$pkgname.install
source=("http://edge.launchpad.net/do/trunk/$pkgver/+download/gnome-do-$pkgver.tar.gz")
md5sums=('71e0e653021f895f151a6f46a97f75ba')

build() {
  export MONO_SHARED_DIR="$srcdir/.wabi"
  mkdir -p "$MONO_SHARED_DIR"
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr --sysconfdir=/etc \
              --disable-schemas-install \
              --with-gconf-schema-file-dir=/usr/share/gconf/schemas
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

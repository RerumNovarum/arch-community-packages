# $Id$
# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Geoffroy Carrier <geoffroy.carrier@koon.fr>
# Contributor: lang2 <wenzhi.liang@gmail.com>

pkgname=imdbpy
_realname=IMDbPY
pkgver=4.7
pkgrel=1
pkgdesc="Python bindings for the Internet Movie Database (IMDb)"
url="http://imdbpy.sourceforge.net/"
arch=('i686' 'x86_64')
license=('GPL')
depends=('python2')
makedepends=('python2-distribute')
optdepends=('python-lxml' 'python2-sqlalchemy' 'python-sqlobject')
source=(http://downloads.sourceforge.net/$pkgname/$_realname-$pkgver.tar.gz)
md5sums=('4deaed7b55ba3098af5ac559da7dde1a')

build(){
  cd "$srcdir/$_realname-$pkgver"

  # python2 fix
  for file in $(find . -name '*.py' -print); do
    sed -i 's_#!.*/usr/bin/python_#!/usr/bin/python2_' $file
    sed -i 's_#!.*/usr/bin/env.*python_#!/usr/bin/env python2_' $file
  done

  python2 setup.py install --root="$pkgdir" --optimize=1

  # fix paths
  mv "$pkgdir/usr/etc" "$pkgdir"
  install -d "$pkgdir/usr/share"
  mv "$pkgdir/usr/imdb" "$pkgdir/usr/share"
  mv "$pkgdir/usr/doc" "$pkgdir/usr/share/imdb/"
}

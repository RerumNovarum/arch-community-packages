# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=blam
pkgver=1.8.10
# Version 1.9.10 is really 1.8.10
_pkgver=1.9.10
pkgrel=1
pkgdesc='Simple newsreader for GNOME written in C Sharp'
arch=('any')
url="http://blam.relatitude.org/"
license=('GPL')
depends=('webkit-sharp' 'gconf-sharp' 'notify-sharp' 'hicolor-icon-theme' 'xdg-utils')
makedepends=('intltool')
install=$pkgname.install
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${_pkgver%.*}/$pkgname-$_pkgver.tar.xz)
sha256sums=('ec59e952de7cb4aa4dcfc22ffd45394ff4b3e172d684da2a1e3151b09fb557d0')

build() {
  cd "$srcdir/$pkgname-$_pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-schemas-install \
              --with-gconf-schema-file-dir=/usr/share/gconf/schemas
  make
}

package() {
  cd "$srcdir/$pkgname-$_pkgver"

  make DESTDIR="$pkgdir/" install
}

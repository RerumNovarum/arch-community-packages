# $Id$
# Maintainer : Martin Wimpress <code@flexion.org>

_ver=1.10
pkgbase=mate-session-manager
pkgname=(${pkgbase}-gtk3)
pkgver=${_ver}.0
pkgrel=1
pkgdesc="The MATE Session Handler (GTK3 version [EXPERIMENTAL])"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL')
depends=('dbus-glib' 'gtk3' 'libsm' 'libxtst' 'mate-desktop-gtk3>=1.10'
         'mate-polkit-gtk3>=1.10' 'mate-settings-daemon-gtk3>=1.10' 'ttf-dejavu'
         'xdg-user-dirs')
makedepends=('mate-common' 'pangox-compat' 'xmlto')
optdepends=('lightdm-gtk3-greeter: A lightweight display manager'
            'onboard: On-screen keyboard useful for mobility impaired users'
            'orca: Screen reader for blind or visually impaired individuals'
            'xdg-user-dirs-gtk: Add autostart that prompts to rename user directories when switching locales')
groups=('mate-gtk3')
conflicts=("${pkgbase}")
source=("http://pub.mate-desktop.org/releases/${_ver}/${pkgbase}-${pkgver}.tar.xz")
sha1sums=('c2cbd3919e9f15498b0293eb8ff9d95b0b20f542')
install=${pkgbase}.install

build() {
    cd "${srcdir}/${pkgbase}-${pkgver}"
    ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib/${pkgbase} \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --with-gtk=3.0 \
        --disable-upower
    make
}

package_mate-session-manager-gtk3() {
    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}

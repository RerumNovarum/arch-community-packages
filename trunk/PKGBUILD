# $Id$
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Maintainer: Kevin Piche <kevin@archlinux.org>

pkgname=hercules
pkgver=3.07
pkgrel=2
pkgdesc="A software implementation of the mainframe System/370 and ESA/390 architectures"
url="http://www.hercules-390.org/"
source=("http://www.hercules-390.org/$pkgname-$pkgver.tar.gz")
arch=('i686' 'x86_64')
license=('custom')
options=('!libtool')
depends=('bzip2' 'libgcrypt' 'zlib')
md5sums=('a12aa1645b0695b25b7fc0c9a3ccab3a')

build() {
  cd $srcdir/$pkgname-$pkgver
  # Change module extension from .la to .so.
  sed '/HDL_MODULE_SUFFIX/ s/\.la/.so/' -i hdl.h
  ./configure --prefix=/usr
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make prefix=$pkgdir/usr install
  install -D -m644 COPYRIGHT \
    $pkgdir/usr/share/licenses/hercules/qpl1
}

# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: dojo <joerg.doll[at]gmx.de>

pkgname=gtkdialog
pkgver=0.8.2
pkgrel=1
pkgdesc='Small utility for fast and easy GUI building'
arch=('i686' 'x86_64')
#url="http://linux.pte.hu/~pipas/gtkdialog/"
url="http://code.google.com/p/gtkdialog/"
license=("GPL")
makedepends=('pkgconfig')
depends=('gtk2' 'libglade')
install=gtkdialog.install
options=('zipman')
source=(http://gtkdialog.googlecode.com/files/gtkdialog-$pkgver.tar.gz)
md5sums=('4038a04f4ff78ca7b7e52e998754c8c2')

build() {
  cd $srcdir/$pkgname-$pkgver
  ./configure --prefix=/usr --mandir=/usr/share/man --infodir=/usr/share/info
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir UPDATE_ICON_CACHE=true install
  rm -f $pkgdir/usr/share/info/dir
  mkdir -p $pkgdir/usr/share/doc/$pkgname
  cp -a examples $pkgdir/usr/share/doc/$pkgname/examples
}

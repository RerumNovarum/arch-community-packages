# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: Mathias Stearn <mathias@10gen.com>
# Contributor: Alec Thomas

pkgname=mongodb
pkgver=1.6.5
pkgrel=5
pkgdesc='A high-performance, open source, schema-free document-oriented database.'
arch=('i686' 'x86_64')
url='http://www.mongodb.org'
license=('AGPL')
depends=('boost-libs' 'spidermonkey' 'pcre')
makedepends=('scons' 'boost')
optdepends=('libpcap: needed for mongosniff')
backup=('etc/conf.d/mongodb' 'etc/mongodb.conf')
source=("http://downloads.mongodb.org/src/${pkgname}-src-r${pkgver}.tar.gz"
        'mongodb.rc'
        'mongodb.conf')
md5sums=('99f1c4c256be1611da6068aea30f9a30'
         '859f8f9bb32ef2bd21fec55ae9a87d0a'
         '9e0ea3f96732bb7811f0b64dace56440')
install="mongodb.install"

build() {
  export SCONSFLAGS="$MAKEFLAGS"

  cd ${pkgname}-src-r${pkgver}

  scons \
    --pch all \
    --full
}

package() {
  cd ${pkgname}-src-r${pkgver}

  scons \
    --pch install \
    --full \
    --prefix=${pkgdir}/usr

  install -D -m755 ${srcdir}/mongodb.rc \
    ${pkgdir}/etc/rc.d/mongodb
  install -D -m644 ${srcdir}/mongodb.conf \
    ${pkgdir}/etc/mongodb.conf
  install -d -m700 ${pkgdir}/var/state/mongodb

  install -D -m644 GNU-AGPL-3.0.txt \
    ${pkgdir}/usr/share/licenses/${pkgname}/GNU-AGPL-3.0.txt

  if [ -d ${pkgdir}/usr/lib64 ]; then
    mv ${pkgdir}/usr/lib64 ${pkgdir}/usr/lib
  fi
}

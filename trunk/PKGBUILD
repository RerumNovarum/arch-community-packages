# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Lex Black <autumn-wind at web dot de>
# Contributor: Alasdair Haswell <ali at arhaswell dot co dot uk>

pkgbase=apache-libcloud
pkgname=(python{,2}-$pkgbase)
pkgver=0.20.1
pkgrel=1
pkgdesc="Apache Libcloud is a standard Python library that abstracts away differences among multiple cloud provider APIs"
arch=('any')
url="https://libcloud.apache.org"
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools')
source=(http://www.mirrorservice.org/sites/ftp.apache.org/libcloud/$pkgbase-$pkgver.tar.bz2)
sha256sums=('a5afb1263329e6b60a69277d1fa5026d88fa65ec2964aaf4086d59d061c76faf')

prepare() {
    cp -a "$srcdir/$pkgbase-$pkgver" "$srcdir/${pkgbase}2-$pkgver"
}

package_python-apache-libcloud() {
  depends=('python')

  cd "$srcdir/$pkgbase-$pkgver"
  python setup.py install --root="$pkgdir"
}

package_python2-apache-libcloud() {
  depends=('python2')
  replaces=('apache-libcloud')
  conflicts=('apache-libcloud')

  cd "$srcdir/${pkgbase}2-$pkgver"
  python2 setup.py install --root="$pkgdir"
}

# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: William Rea <sillywilly@gmail.com>
# Contributor: Hans Janssen <hans@janserv.xs4all.nl>

pkgname=flightgear
pkgver=2.4.0
pkgrel=2
pkgdesc="An open-source, multi-platform flight simulator"
arch=(i686 x86_64)
depends=('simgear' 'libxmu' 'libxi' 'zlib' 'openscenegraph' 'subversion')
makedepends=('boost')
license=("GPL")
url="http://www.flightgear.org"
options=('!makeflags')
source=("$pkgname-$pkgver.tar.gz::https://gitorious.org/fg/flightgear/archive-tarball/release/$pkgver")
md5sums=('4ef4d8267f3b46d8ceea58acbb8a31c5')

build() {
  cd $srcdir/fg-flightgear
  [ -x configure ] || ./autogen.sh
  [ -f Makefile ] || ./configure --prefix=/usr
  make
}

package() {
  depends=('simgear' 'libxmu' 'libxi' 'zlib' 'openscenegraph' 'subversion' 'flightgear-data')
  cd $srcdir/fg-flightgear
  make DESTDIR=$pkgdir install
  install -Dm0644 package/flightgear.desktop $pkgdir//usr/share/applications/flightgear.desktop
  install -Dm0644 package/RedHat/flightgear.48.png $pkgdir/usr/share/icons/flightgear.png
}

# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Geoffroy Carrier <geoffroy@archlinux.org>
# Contributor: JoÃ«l Schaerer <joel.schaerer@laposte.net>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=autojump
pkgver=20
pkgrel=2
pkgdesc='Faster way to navigate your filesystem from the command line'
arch=('any')
url="http://wiki.github.com/joelthelion/autojump"
license=('GPL3')
depends=('bash' 'python')
optdepends=('python2: needed for jumpapplet')
install=$pkgname.install
source=(https://github.com/downloads/joelthelion/${pkgname}/${pkgname}_v${pkgver}.tar.gz)
sha256sums=('a2a8cf2dcda97fef227a53b3a634ebb7173e0f8da97545da247e7f4de88ba408')

package() {
  cd ${pkgname}_v${pkgver}

  sed -i "s:/env python:/python3:g" $pkgname

  install -Dm755 $pkgname ${pkgdir}/usr/bin/$pkgname
  install -Dm755 jumpapplet ${pkgdir}/usr/bin/jumpapplet
  install -Dm644 $pkgname.1 ${pkgdir}/usr/share/man/man1/$pkgname.1
  install -Dm644 icon.png ${pkgdir}/usr/share/$pkgname/icon.png
  install -Dm755 $pkgname.bash ${pkgdir}/etc/profile.d/$pkgname.bash
  install -Dm755 $pkgname.sh ${pkgdir}/etc/profile.d/$pkgname.sh
  install -Dm755 $pkgname.zsh ${pkgdir}/etc/profile.d/$pkgname.zsh
  install -Dm644 _j ${pkgdir}/usr/share/zsh/site-functions/_j
}

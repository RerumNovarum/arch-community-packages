# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Rubin Simons <rubin@xs4all.nl>

pkgname=trac
pkgver=0.12
pkgrel=6
pkgdesc="Trac Integrated SCM & Project Management using Apache & Subversion."
arch=('any')
url="http://trac.edgewall.org/"
license=('BSD')
depends=(python2 setuptools python-genshi python-babel)
makedepends=(clearsilver)
install=trac.install
backup=("etc/conf.d/tracd.conf")
source=(http://ftp.edgewall.com/pub/trac/Trac-$pkgver.tar.gz \
	license.txt \
	tracd \
	tracd.conf)
md5sums=('3e8fabbaf8fe889e9c03a7dff09d5d05'
         'af9a4064fd7a5759fee0fc288f749977'
         '023c5662be90a1e81174baffc46a33f5'
         '67db455554d661c725baf3057bd13534')

build() {
  cd $srcdir/Trac-$pkgver

  # python2 fix
  for file in trac/admin/console.py trac/test.py trac/tests/functional/*.py \
              trac/web/*.py trac/wiki/default-pages/TracFastCgi \
              cgi-bin/trac.{f,}cgi; do
      sed -i 's_#!/usr/bin/env python_#!/usr/bin/env python2_' $file
      sed -i 's_#!/usr/bin/python_#!/usr/bin/python2_' $file
  done

  python2 setup.py install --prefix=/usr --root=$pkgdir
  install -D -m755 ../tracd $pkgdir/etc/rc.d/tracd
  install -D -m644 ../tracd.conf $pkgdir/etc/conf.d/tracd.conf
  install -D -m644 ../license.txt $pkgdir/usr/share/licenses/trac/license.txt
  install -D -m0755 cgi-bin/trac.cgi $pkgdir/usr/share/trac/trac.cgi
  install -D -m0755 cgi-bin/trac.fcgi $pkgdir/usr/share/trac/trac.fcgi
}

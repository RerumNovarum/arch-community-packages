# $Id$
# Maintainer: Angel Velasquez <angvp@archlinux.org> 
# Contributor: Roberto Alsina <ralsina@kde.org>

pkgname=python2-rst2pdf
pkgver=0.91
pkgrel=1
pkgdesc="Create PDFs from simple text markup, no LaTeX required"
arch=('any')
url="http://rst2pdf.googlecode.com"
license=('MIT')
depends=('python2-distribute' 'docutils' 'python-reportlab')
optdepends=('uniconvertor: vector images support'
            'python-svglib: some SVG support'
            'inkscape: best SVG support'
	    'python-wordaxe: hyphenation'
            'python2-pygments: Syntax Highlighting' 
            'python-imaging: non-JPEG bitmap image formats')
source=(http://rst2pdf.googlecode.com/files/rst2pdf-$pkgver.tar.gz)
md5sums=('9ca219b0460be89585f96588e12543b9')

package() {
  cd "$srcdir/rst2pdf-$pkgver"
  python2 setup.py install --root="$pkgdir"
  (cd doc && rst2man rst2pdf.txt rst2pdf.1)
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/python2-rst2pdf/LICENSE.txt"
  install -Dm644 doc/rst2pdf.1 "$pkgdir/usr/share/man/man1/rst2pdf.1"
  find "$pkgdir" -type f -name '*.py' -exec sed -i -e 's~^#.*env python~#!/usr/bin/env python2~' \
    -e 's~#!/usr/bin/python~#!/usr/bin/python2~' {} +
}

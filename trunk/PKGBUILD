# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Daniel Isenmann <daniel.isenmann [at] gmx.de>

pkgname=gcompris-data
pkgver=11.12
pkgrel=1
pkgdesc="Data files for GCompris"
arch=('any')
url="http://gcompris.net/"
license=('GPL3')
makedepends=('texinfo' 'texi2html' 'intltool' 'gettext' 'gnuchess'
             'pygtk' 'pyxml' 'librsvg' 'gnet' 'python-pysqlite'
             'gstreamer0.10-base-plugins')
options=('!libtool')
install=gcompris-data.install
source=(http://downloads.sourceforge.net/project/gcompris/$pkgver/gcompris-$pkgver.tar.gz)
sha256sums=('ab57e7c96df66114f7298947f6725364f4a8b811f1bb1e9470cbd63eea9e6d81')

build() {
  cd "$srcdir/gcompris-$pkgver"

  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/gcompris-$pkgver"
  make DESTDIR="$pkgdir" install-data

  # Place GCompris menu entry under Education
  sed -i 's|^Categories=.*|Categories=Education;|' \
    "$pkgdir/usr/share/applications/gcompris.desktop"
}

# vim:set ts=2 sw=2 et:

# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Mateusz Herych
# Contributor: Jaroslaw Swierczynski <swiergot@aur.archlinux.org>

pkgname=kadu
pkgver=0.12.3
pkgrel=1
pkgdesc='A Qt-based Jabber/XMPP and Gadu-Gadu client'
arch=('i686' 'x86_64')
url='http://www.kadu.net/'
license=('GPL')
depends=('libgadu' 'libxss' 'enchant' 'phonon' 'qca-ossl' 'libidn' 'libmpdclient' 'qtwebkit' 'xdg-utils')
makedepends=('cmake' 'libao' 'libsndfile' 'libxtst' 'curl')
install=kadu.install
source=(http://download.kadu.im/stable/$pkgname-$pkgver.tar.bz2)
md5sums=('c5d6f0bd384cd96090819ea018eb8ad8')

build() {
  cd $srcdir
  mkdir build
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd $srcdir/build
  make DESTDIR=$pkgdir install
  mv $pkgdir/usr/sdk "$pkgdir"/usr/share/kadu/sdk
  rm -rf $pkgdir/usr/{lib,include}/{libgadu*,pkgconfig}
  rm -rf $pkgdir/usr/share/kadu/{HISTORY,README}
}

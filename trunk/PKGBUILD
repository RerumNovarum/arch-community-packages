# $Id$
# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Initial contributor: flixie <69one@gmx.net>
# Contributor: Imanol Celaya <ornitorrincos@archlinux-es.org>
pkgname=luxrender
pkgver=1.0rc4
_pkgver=0b4c3e188707
pkgrel=1
pkgdesc="Rendering system for physically correct, unbiased image synthesis"
arch=('i686' 'x86_64')
url="http://www.luxrender.net/"
license=('GPL')
depends=('boost-libs' 'freeimage' 'openexr' 'libcl')
optdepends=('luxblend25: Blender exporter' 'qt: Qt GUI' \
            'python: Python interface (pylux)'
            'nvidia-utils: OpenCL support for nVidia GPUs' \
            'amdstream: OpenCL support for AMD GPUs' \
            'intel-opencl-sdk: OpenCL support for Intel CPUs')
makedepends=('cmake' 'boost' 'qt' "luxrays=$pkgver" 'python' 'opencl-headers')
source=(https://bitbucket.org/luxrender/lux/get/$_pkgver.tar.bz2 \
        boost_1.50_fix.diff)
md5sums=('0578f2500f51363ab4d8ea5004fea9dc'
         '4cddc606ef3ea7f0bbbc9b3415be38b0')

build() {
  cd "$srcdir"/luxrender-lux-$_pkgver

  # fix for boost 1.50
  # the boost::TIME_UTC has been renamed due to the conflict with C++11
  patch -Np1 < "$srcdir"/boost_1.50_fix.diff || true

  export CXXFLAGS="$CXXFLAGS -lpthread"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
    -DLUXRAYS_DISABLE_OPENCL=OFF \
    -DPYTHON_CUSTOM=ON \
    -DPYTHON_LIBRARIES=/usr/lib/libpython3.2mu.so \
    -DPYTHON_INCLUDE_PATH=/usr/include/python3.2mu/ \
    .
  make
}

package() {
  cd "$srcdir"/luxrender-lux-$_pkgver
  make DESTDIR="$pkgdir" install

  # fix library path on x86_64
  [ "$CARCH" = "x86_64" ] && mv "$pkgdir"/usr/lib64 "$pkgdir"/usr/lib

  #install pylux
  install -D -m644 pylux.so "$pkgdir"/usr/lib/python3.2/pylux.so
}

# vim:set ts=2 sw=2 et:

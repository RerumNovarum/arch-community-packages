# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Jameson Pugh <imntreal@gmail.com>
# Contributor: Tim Jester-Pfadt <t.jp<at>gmx.de>

pkgbase=appstream
pkgname=(appstream appstream-qt)
pkgver=0.9.5
pkgrel=1
pkgdesc="Provides a standard for creating app stores across distributions"
arch=(i686 x86_64)
url="http://distributions.freedesktop.org/wiki/AppStream"
license=(GPL)
depends=(xapian-core polkit libyaml libxml2 protobuf)
makedepends=(cmake xmlto intltool gobject-introspection python2 docbook-xsl qt5-base)
source=("http://www.freedesktop.org/software/appstream/releases/AppStream-$pkgver.tar.xz" update-appstream-db.hook)
sha256sums=('13faec5280fd68be38e6b1094bea9079a1439824023b333bde9884070be9f188'
            '6aced8f86854a3701879333de84cb5a0cc77f345d655a91e1a65f63c459f00dc')

prepare() {
  mkdir -p build
}

build() {
  cd build

  cmake ../AppStream-$pkgver \
	-DQT=ON \
	-DCMAKE_INSTALL_LIBDIR=lib \
	-DCMAKE_BUILD_TYPE=Release
  make
}

package_appstream() {
  cd build

  make DESTDIR="$pkgdir" install
  
# provided by -qt subpackage
  rm -r "$pkgdir"/usr/{include/AppstreamQt,lib/cmake,lib/libAppstreamQt.*}

  install -Dm644 "$srcdir"/update-appstream-db.hook "$pkgdir"/usr/share/libalpm/hooks/update-appstream-db.hook
}

package_appstream-qt() {
  pkgdesc='Qt5 interface for AppStream'
  depends=(appstream qt5-base)

  cd build/qt
  make DESTDIR="$pkgdir" install
}

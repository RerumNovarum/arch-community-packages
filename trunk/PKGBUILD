# $Id$
# Maintainer: Corrado 'bardo' Primier <corrado.primier@mail.polimi.it>
# Contributor: Tate "Tatey" Johnson <tatey86@tpg.com.au>

pkgname=rss-glx
pkgver=0.9.1
pkgrel=2
pkgdesc="The Really Slick Screensavers port to GLX"
arch=('i686' 'x86_64')
url="http://rss-glx.sourceforge.net/"
license=('GPL')
depends=('desktop-file-utils' 'freealut' 'glew' 'imagemagick')
optdepends=('xscreensaver: xscreensaver integration')
install=rss-glx.install
options=('!libtool')
source=(http://downloads.sourceforge.net/rss-glx/${pkgname}_${pkgver}.tar.bz2
        rss-glx-gnome.tar.bz2)
md5sums=('a772bd143cd8d141edf4d9eff9860ab3'
         'b63086a5e15c175d89c0265965e66265')

build() {
  cd ${srcdir}/${pkgname}_${pkgver}

  [ "$CARCH" = "x86_64" ] && (sed -i -e 's|@LIBS@|@LIBS@ -fopenmp|g' src/Makefile.in || return 1)

  ./configure --prefix=/usr \
  	      --mandir=/usr/share/man \
	      --program-transform-name='s/plasma/plasma_rss/' \
	      --with-configdir=/usr/share/xscreensaver/config \
	      --with-kdessconfigdir=/usr/share/applnk/System/ScreenSavers \
	      --enable-shared=yes --enable-static=no
  make CFLAGS="$CFLAGS -I/usr/include/ImageMagick" || return 1
  make DESTDIR=${pkgdir} install || return 1

#FS#18300
  install -d ${pkgdir}/usr/lib/xscreensaver/
  list=$(ls ${pkgdir}/usr/bin --ignore rss-glx_install.pl)
  for i in $list; do
    ln -s "/usr/bin/$i" "$pkgdir/usr/lib/xscreensaver/"
  done

  install -d ${pkgdir}/usr/share/applications/screensavers
  install -m644 ${srcdir}/${pkgname}-gnome/*.desktop ${pkgdir}/usr/share/applications/screensavers
}

# vim:set ts=2 sw=2 et:

# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Shaika-Dzari <shaikadzari at gmail dot com>

pkgname=qtmpc
pkgver=0.6.1
pkgrel=2
pkgdesc="Qt4-based front-end for MPD"
arch=('i686' 'x86_64')
url="http://qtmpc.lowblog.nl/"
license=('GPL')
depends=('hicolor-icon-theme' 'kdebase-runtime' 'qt')
makedepends=('automoc4' 'cmake')
install=$pkgname.install
changelog=$pkgname.changelog
source=(http://files.lowblog.nl/$pkgname/QtMPC-$pkgver.tar.bz2)
sha256sums=('82287a26788b36d49db26518b66203f332cf686eefb81ee2d9be095e61123a62')

build() {
  cd ${srcdir}/QtMPC

  cmake . -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd ${srcdir}/QtMPC

  make DESTDIR=${pkgdir} install 
}

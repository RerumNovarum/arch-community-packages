# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>

pkgname=processing
pkgver=2.0b7
pkgrel=2
pkgdesc='Programming language and environment for creating images, animations and interactions'
arch=('x86_64' 'i686')
url='http://www.processing.org/'
license=('GPL' 'LGPL')
depends=('libgl' 'libxxf86vm')
makedepends=('gendesk')
options=(!strip)
source=("http://$pkgname.googlecode.com/files/$pkgname-$pkgver-linux32.tgz"
        "$pkgname.png::http://wiki.$pkgname.org/skins/${pkgname}skin/${pkgname}_wiki_logo.png")
sha256sums=('55db8370672f9e2658443a7fe577e23f06ca1ab2435719c865a059a0e65cdc83'
            '019ae700333409acbc628779747ab8f153f6647de25e4de3a9bd4a42d0d8056a')

build() {
    cd "$srcdir"
    gendesk
}

package() {
    cd "$srcdir/$pkgname-$pkgver"

    install -d "$pkgdir/usr/share/$pkgname/" "$pkgdir/usr/bin/"
    cp -r ./* "$pkgdir/usr/share/$pkgname/"
    ln -s "/usr/share/$pkgname/$pkgname" "$pkgdir/usr/bin/$pkgname"
    ln -s "/usr/share/$pkgname/$pkgname-java" "$pkgdir/usr/bin/$pkgname-java"
    install -Dm644 "$srcdir/$pkgname.png" \
      "$pkgdir/usr/share/pixmaps/$pkgname.png"
    install -Dm644 "$srcdir/$pkgname.desktop" \
      "$pkgdir/usr/share/applications/$pkgname.desktop"
}

# vim:set ts=2 sw=2 et:

# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Lukas Jirkovsky <l.jirkovsky@gmail.com>
pkgname=ufoai
pkgver=2.3.1
pkgrel=1
pkgdesc="In UFO: Alien Invasion you fight aliens trying to capture Earth"
arch=('i686' 'x86_64')
url="http://ufoai.ninex.info/"
license=('GPL')
depends=('libjpeg' 'libpng' 'curl' 'sdl_image' 'sdl_mixer' 'sdl_ttf' 'xvidcore' 'ufoai-data')
makedepends=('mesa')
source=(http://downloads.sourceforge.net/project/ufoai/UFO_AI%202.x/$pkgver/ufoai-$pkgver-source.tar.bz2)
md5sums=('87b3e1bf3439dc6e5413936034f47d17')

build() {
  cd "$srcdir"/$pkgname-$pkgver-source

  #./configure --prefix=/usr --enable-release --disable-uforadiant
  # remove --enable-release because it causes crashes on some systems
  mkdir base
  ./configure --prefix=/usr --disable-uforadiant --enable-release
  make
  make lang
}

package() {
  cd "$srcdir"/$pkgname-$pkgver-source

  make DESTDIR="$pkgdir" install_exec

  install -D -m644 debian/ufoai.desktop "$pkgdir"/usr/share/applications/ufoai.desktop
  install -D -m644 debian/ufoai-safe.desktop "$pkgdir"/usr/share/applications/ufoai-safe.desktop
  install -D -m644 debian/ufoded.desktop "$pkgdir"/usr/share/applications/ufoded.desktop

  sed -i 's|/usr/games/||' "$pkgdir"/usr/share/applications/*

  install -D -m644 debian/ufoai.xpm "$pkgdir"/usr/share/pixmaps/ufoai.xpm
  install -D -m644 debian/ufoded.xpm "$pkgdir"/usr/share/pixmaps/ufoded.xpm
}

# vim: sw=2:ts=2 et:

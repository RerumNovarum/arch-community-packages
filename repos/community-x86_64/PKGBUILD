# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Jameson Pugh <imntreal@gmail.com>
# Contributor: Tim Jester-Pfadt <t.jp<at>gmx.de>

pkgbase=appstream
pkgname=(appstream appstream-qt)
pkgver=0.9.4
pkgrel=3
pkgdesc="Provides a standard for creating app stores across distributions"
arch=(i686 x86_64)
url="http://distributions.freedesktop.org/wiki/AppStream"
license=(GPL)
depends=(xapian-core polkit libyaml libxml2 protobuf)
makedepends=(cmake xmlto intltool gobject-introspection python2 docbook-xsl qt5-base)
source=("http://www.freedesktop.org/software/appstream/releases/AppStream-$pkgver.tar.xz" update-appstream-db.hook
        https://github.com/ximion/appstream/commit/28f90c3f.patch)
sha256sums=('b9ed14e2af7956252b0326661c8b558466a0297560851ba7a35ecb65489c8815'
            '6aced8f86854a3701879333de84cb5a0cc77f345d655a91e1a65f63c459f00dc'
            'e03679c31eb2ac9d3b410f50cc02b37fed53ced5ba4bdf9cbc414aa0261169e9')

prepare() {
  mkdir -p build

# Fix segfault when generating cache
  cd AppStream-$pkgver
  patch -p1 -i ../28f90c3f.patch
}

build() {
  cd build

  cmake ../AppStream-$pkgver \
	-DQT=ON \
	-DCMAKE_INSTALL_LIBDIR=lib \
	-DCMAKE_BUILD_TYPE=Release
  make
}

package_appstream() {
  cd build

  make DESTDIR="$pkgdir" install
  
# provided by -qt subpackage
  rm -r "$pkgdir"/usr/{include/AppstreamQt,lib/cmake,lib/libAppstreamQt.*}

  install -Dm644 "$srcdir"/update-appstream-db.hook "$pkgdir"/usr/share/libalpm/hooks/update-appstream-db.hook
}

package_appstream-qt() {
  pkgdesc='Qt5 interface for AppStream'
  depends=(appstream qt5-base)

  cd build/qt
  make DESTDIR="$pkgdir" install
}

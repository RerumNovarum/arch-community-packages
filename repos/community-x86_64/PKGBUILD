# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Danial Olsen <do@meyl.fo>

pkgname=kvpnc
pkgver=0.9.6a
pkgrel=3
pkgdesc="A KDE Desktop Environment frontend for various vpn clients"
arch=(i686 x86_64)
url="http://home.gna.org/kvpnc/"
license=("GPL")
depends=('kdelibs' 'hicolor-icon-theme')
makedepends=('cmake' 'automoc4' 'optipng')
install=kvpnc.install
source=(http://download.gna.org/$pkgname/$pkgname-${pkgver}-kde4.tar.bz2)
md5sums=('bf8b7224284f5d3f8ad5235c599fe9e7')

build() {
  cd $srcdir
  sed -i '1,1i#include <unistd.h>' ${pkgname}-${pkgver}-kde4/src/kvpnc.cpp

  export LDFLAGS=-lX11
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver}-kde4 \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
  find -name '*.png' -exec optipng -quiet -force -fix {} \;
}

package() {
  cd $srcdir/build
  make DESTDIR=$pkgdir install
}

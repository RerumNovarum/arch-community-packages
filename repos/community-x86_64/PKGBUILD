# $Id$
# Maintainer: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: Allan McRae <mcrae_allan@hotmail.com>
# Contributor: fancris3 <fancris3 at aol.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=gecko-mediaplayer
pkgver=1.0.0
pkgrel=3
pkgdesc='Browser plugin that uses gnome-mplayer to play media in a web browser.'
arch=('i686' 'x86_64')
url='http://code.google.com/p/gecko-mediaplayer/'
license=('GPL')
depends=("gnome-mplayer>=${pkgver}" 'dbus-glib')
makedepends=('xulrunner' 'pkgconfig')
replaces=('mplayer-plugin')
source=("http://gecko-mediaplayer.googlecode.com/files/${pkgname}-${pkgver}.tar.gz" "gecko-mediaplayer-xulrunner2.0.patch")
md5sums=('80ccb671aea90153be9f9e6dc41b7eae'
         '36e4332f67d74605530c12a804966d4f')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -Np0 -i $srcdir/gecko-mediaplayer-xulrunner2.0.patch
  ./configure --prefix=/usr --sysconfdir=/etc --without-gconf
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}

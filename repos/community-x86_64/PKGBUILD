# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Douglas Soares de Andrade <dsa@aur.archlinux.org>
# Contributor: Benjamin Andresen <benny@klapmuetz.org>
# Contributor: bekks <eduard.warkentin@gmx.de>

pkgname=pgadmin3
pkgver=1.14.3
pkgrel=1
pkgdesc="Comprehensive design and management interface for PostgreSQL"
arch=('i686' 'x86_64')
url="http://www.pgadmin.org"
license=('custom')
depends=('wxgtk' 'postgresql-libs' 'libxslt')
makedepends=('libpqxx' 'krb5' 'postgresql' 'gendesk' 'imagemagick')
source=(http://ftp.postgresql.org/pub/pgadmin3/release/v${pkgver}/src/pgadmin3-${pkgver}.tar.gz)
md5sums=('0669074fda039f5128abb40f63948ff5')

build() {
  cd "$srcdir"
  convert pgadmin3-${pkgver}/pgadmin/include/images/pgAdmin3.ico pgadmin3.png
  cp pgadmin3-3.png pgadmin3.png
  gendesk

  cd "pgadmin3-${pkgver}"
  [ -f Makefile ] || ./configure --prefix=/usr
  make
}

package() {
  cd $srcdir/pgadmin3-${pkgver}

  make DESTDIR=$pkgdir/ install
  install -Dm644 i18n/$pkgname.lng $pkgdir/usr/share/pgadmin3/i18n
  install -Dm644 LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
  install -Dm644 pgadmin/include/images/pgAdmin3.ico $pkgdir/usr/share/pgadmin3/pixmaps/pgAdmin3.ico
  install -Dm644 "$srcdir/pgadmin3.png" "$pkgdir/usr/share/pixmaps/pgadmin3.png"
  install -Dm644 "$srcdir/pgadmin3.desktop" "$pkgdir/usr/share/applications/pgadmin3.desktop"
}

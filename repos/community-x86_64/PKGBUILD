# $Id: PKGBUILD,v 1.13 2009/07/05 16:13:35 dgriffiths Exp $
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>
# Maintainer: Allan McRae <mcrae_allan@hotmail.com>
# Contributor: fancris3 <fancris3 at aol.com>

pkgname=gecko-mediaplayer
pkgver=0.9.6
pkgrel=2
pkgdesc="Browser plugin that uses gnome-mplayer to play media in a web browser"
arch=('i686' 'x86_64')
url="http://code.google.com/p/gecko-mediaplayer/"
license=('GPL')
depends=("gnome-mplayer=${pkgver}" 'dbus-glib')
makedepends=('xulrunner' 'pkgconfig')
source=(http://gecko-mediaplayer.googlecode.com/files/${pkgname}-${pkgver}.tar.gz)
md5sums=('c96487ef49a7e3514da55b0c2acc5a1d')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  ./configure --prefix=/usr --sysconfdir=/etc --disable-schemas-install --without-gconf \
  GECKO_LIBS="-L/usr/lib/xulrunner-devel-1.9.1/lib -lxpcomglue_s -lxul -lxpcom -lplds4 -lplc4 -lnspr4 -lpthread -ldl" \
  GECKO_CFLAGS="-fshort-wchar -I/usr/include/xulrunner-1.9.1/stable -I/usr/include/nspr" \
  GECKO_XPIDL="/usr/lib/xulrunner-1.9.1/xpidl" \
  GECKO_IDLDIR="/usr/share/idl/xulrunner-1.9.1/stable"
  make || return 1
  make DESTDIR=${pkgdir} install
}

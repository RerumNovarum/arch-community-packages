# $Id$
# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Initial contributor: flixie <69one@gmx.net>
# Contributor: Imanol Celaya <ornitorrincos@archlinux-es.org>
pkgname=luxrender
pkgver=0.8
_pkgver=45d3e13eb94c
_luxblend=9cb3fcee0be8
_blender=2.57
pkgrel=4
pkgdesc="Rendering system for physically correct, unbiased image synthesis"
arch=('i686' 'x86_64')
url="http://www.luxrender.net/"
license=('GPL')
depends=('boost-libs' 'freeimage' 'openexr')
optdepends=('blender: Blender exporter' 'qt: Qt GUI')
makedepends=('cmake' 'boost' 'qt' 'luxrays' 'python')
source=(http://src.luxrender.net/lux/archive/$_pkgver.tar.bz2 \
        http://src.luxrender.net/luxblend25/archive/$_luxblend.tar.bz2)
md5sums=('ad8d2ede00aa244cad884d9696ff5fbb'
         '29c34f97791db3af1b827d9e9a1b3d79')

build() {
  cd "$srcdir"/lux-$_pkgver

  sed -i 's/FIND_PACKAGE(Boost 1.43 COMPONENTS python REQUIRED)/FIND_PACKAGE(Boost 1.43 COMPONENTS python3 REQUIRED)/' CMakeLists.txt

  export CXXFLAGS="$CXXFLAGS -lpthread"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
    -DLUXRAYS_DISABLE_OPENCL=ON \
    -DPYTHON_CUSTOM=ON \
    -DPYTHON_LIBRARIES=/usr/lib/libpython3.2mu.so \
    -DPYTHON_INCLUDE_PATH=/usr/include/python3.2mu/ \
    .
  make
}

package() {
  cd "$srcdir"/lux-$_pkgver
  make DESTDIR="$pkgdir" install

  # fix library path on x86_64
  [ "$CARCH" = "x86_64" ] && mv "$pkgdir"/usr/lib64 "$pkgdir"/usr/lib

  # install blender exporter
  install -d -m755 "$pkgdir"/usr/share/blender/$_blender/scripts/addons
  cp -a "$srcdir"/luxblend25-$_luxblend/src/luxrender \
    "$pkgdir"/usr/share/blender/$_blender/scripts/addons
  cp -a "$srcdir"/lux-$_pkgver/pylux.so "$pkgdir"/usr/share/blender/$_blender/scripts/addons/luxrender/
}

# vim:set ts=2 sw=2 et:

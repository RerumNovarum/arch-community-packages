# $Id$
# Maintainer: speps <speps at aur dot archlinux dot org>

_name=QupZilla
pkgname=qupzilla
pkgver=1.3.5
pkgrel=3
pkgdesc="Cross-platform QtWebKit browser"
arch=(i686 x86_64)
url="http://www.qupzilla.com/"
license=('GPL3')
depends=('qtwebkit')
install="$pkgname.install"
source=("$pkgname-$pkgver.tar.gz::https://github.com/$_name/$pkgname/tarball/v$pkgver"
        "https://github.com/QupZilla/qupzilla/commit/37da53731c6c92b4ece0bdb6e264472adf2f6d9.patch"
        "https://github.com/QupZilla/qupzilla/commit/7a1bbd993d6f54d01b88a8d1f05bcfe0dec1c2b5.patch")
md5sums=('95d19c0e22c6dbf605b760125a4b7ef3'
         'c40fbec4f2d6877b6536834c45ec6323'
         'ace1933025c3fba757911336560b00d0')

build() {
  cd "$srcdir/$_name-$pkgname-"*

  # FS#32840
  patch -p1 -i "$srcdir/${source[1]##*/}" #624 - Don't crash on adblock menu when adblock is disabled.
  patch -p1 -i "$srcdir/${source[2]##*/}" #652 - Infinite loop on bookmarking a page while history is empty

  # enable webgl support
  export USE_WEBGL=true

  qmake QUPZILLA_PREFIX=/usr/
  make
}

package() {
  cd "$srcdir/$_name-$pkgname-"*
  make INSTALL_ROOT="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:

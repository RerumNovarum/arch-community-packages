# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>

pkgname=processing
pkgver=2.0
pkgrel=1
arch=('x86_64' 'i686')
pkgdesc='Programming language and environment for creating images, animations and interactions'
url='http://www.processing.org/'
license=('GPL' 'LGPL')
depends=('libgl' 'sh' 'libxxf86vm')
makedepends=('gendesk')
options=(!strip)

if [ "$CARCH" == "x86_64" ]; then
  source=("http://$pkgname.googlecode.com/files/$pkgname-$pkgver-linux64.tgz"
          "$pkgname.png::http://wiki.$pkgname.org/skins/${pkgname}skin/${pkgname}_wiki_logo.png")
  sha256sums=('91b218d69bfe681c671d029346c671612f443f429ea68b63e0e08ae3aa2ffcf6'
            '019ae700333409acbc628779747ab8f153f6647de25e4de3a9bd4a42d0d8056a')
else
  source=("http://$pkgname.googlecode.com/files/$pkgname-$pkgver-linux32.tgz"
          "$pkgname.png::http://wiki.$pkgname.org/skins/${pkgname}skin/${pkgname}_wiki_logo.png")
  sha256sums=('5f09d79b13ec24384d64385c625b559cc24f2682d5ca3d6a8944ce95873e8e26'
            '019ae700333409acbc628779747ab8f153f6647de25e4de3a9bd4a42d0d8056a')
fi

prepare() {
  cd "$srcdir"

  gendesk --pkgname="$pkgname" --pkgdesc="$pkgdesc"
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  
  install -d "$pkgdir/usr/share/$pkgname/" "$pkgdir/usr/bin/"
  cp -r * "$pkgdir/usr/share/$pkgname/"
  ln -s "/usr/share/$pkgname/$pkgname" "$pkgdir/usr/bin/$pkgname"
  ln -s "/usr/share/$pkgname/$pkgname-java" "$pkgdir/usr/bin/$pkgname-java"
  install -Dm644 "$srcdir/$pkgname.png" \
    "$pkgdir/usr/share/pixmaps/$pkgname.png"
  install -Dm644 "$srcdir/$pkgname.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
}

# vim:set ts=2 sw=2 et:

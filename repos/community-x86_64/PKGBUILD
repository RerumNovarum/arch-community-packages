# $Id$
# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: dieghen89 <dieghen89@gmail.com>
# Contributor: Alessio 'Bl@ster' Biancalana <dottorblaster@gmail.com>

pkgname=docky
pkgver=2.0.6
pkgrel=1
pkgdesc="The Finest Dock No Money Can Buy"
url="https://launchpad.net/docky"
arch=('i686' 'x86_64')
license=('GPL')
depends=('gnome-desktop-sharp' 'gnome-keyring-sharp' 'gtk2' 'mono-addins'
         'hicolor-icon-theme' 'ndesk-dbus-glib' 'notify-sharp-svn' 'xdg-utils')
makedepends=('intltool')
options=('!libtool' '!emptydirs')
install=docky.install
source=("http://launchpad.net/$pkgname/${pkgver%.*}/${pkgver}/+download/$pkgname-$pkgver.tar.gz")

build() {
  cd "$srcdir/$pkgname-$pkgver"

  export MONO_SHARED_DIR="${srcdir}/.wabi"
  mkdir -p "${MONO_SHARED_DIR}"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 install

  mkdir -p "$pkgdir/usr/share/gconf/schemas"
  gconf-merge-schema "$pkgdir/usr/share/gconf/schemas/docky.schemas" "$pkgdir"/etc/gconf/schemas/*.schemas
  rm -rf "$pkgdir/etc/gconf"
}

md5sums=('7c30f6d3c4114e370fb45b78644dd4a5')

# $Id$
# Maintainer: Thorsten TÃ¶pper <atsutane-tu@freethoughts.de>
# Contributor: Alexander Fehr <pizzapunk gmail com>

pkgname=roxterm
pkgver=1.20.2
pkgrel=1
pkgdesc="Tabbed, VTE-based terminal emulator"
arch=('i686' 'x86_64')
url="http://roxterm.sourceforge.net/"
license=('GPL3')
depends=('libglade' 'dbus-glib' 'vte' 'hicolor-icon-theme' 'dbus')
makedepends=('docbook-xsl' 'xmlto' 'po4a')
install=roxterm.install
source=(http://downloads.sourceforge.net/roxterm/roxterm-$pkgver.tar.gz)
md5sums=('24f92202c5a68c788cb9cddfd88ed0ba')

build() {
  cd "${srcdir}/roxterm-${pkgver}"

  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/roxterm-${pkgver}"
  make DESTDIR="${pkgdir}" install
}


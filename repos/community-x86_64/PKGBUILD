# $Id$
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Callan Barrett <wizzomafizzo@gmail.com>
# Contributor: Scott Horowitz <stonecrest@gmail.com>

pkgname=python2-tagpy
pkgver=0.94.8
pkgrel=1
pkgdesc="Python bindings for TagLib"
arch=('i686' 'x86_64')
url="http://pypi.python.org/pypi/tagpy"
license=('MIT')
depends=('python2' 'taglib' 'boost-libs')
makedepends=('python2-distribute' 'boost')
replaces=('tagpy')
conflicts=('tagpy')
source=("http://pypi.python.org/packages/source/t/tagpy/tagpy-$pkgver.tar.gz"
        'taglib1.7.patch')
md5sums=('6baff63318cf90b9bc5a2497a0597802'
         '5b47cefe5fad98a093232256779da345')

build() {
  cd tagpy-${pkgver}

  patch -p1 -i "${srcdir}"/taglib1.7.patch

  sed -i -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
    $(find . -name '*.py') 
 
  ./configure.py --taglib-lib-dir=/usr/lib/ \
  	--taglib-inc-dir=/usr/include/taglib/ \
	--boost-inc-dir=/usr/include/boost/ \
	--boost-lib-dir=/usr/lib/ \
	--boost-python-libname=boost_python
  python2 setup.py build
}

package(){
  cd tagpy-${pkgver}
  python2 setup.py install --root="${pkgdir}" --optimize=1
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/$pkgname/LICENSE"
}

check(){
  cd tagpy-${pkgver}
  python2 setup.py test
}

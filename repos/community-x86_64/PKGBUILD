# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Matthias Maennich <arch@maennich.net>
# Contributor: <boenki@gmx.de>

pkgname=qlandkartegt
pkgver=1.0.1
pkgrel=2
pkgdesc="Use your GPS with Linux"
arch=('i686' 'x86_64')
url="http://www.qlandkarte.org/"
license=('GPL')
depends=('cfitsio' 'gdal' 'libmysqlclient' 'mesa' 'postgresql-libs' 'proj' 'qt' 'shared-mime-info')
optdepends=('garmindev: to connect garmin devices'
            'libexif: to create wayoints from geotagged images'
            'libdmtx: to summarize waypoints as  2D barcode'
            'gpsd: to enable gpsd support')
makedepends=('cmake' 'garmindev' 'gpsd' 'libdmtx' 'libexif')
install=$pkgname.install
changelog=$pkgname.changelog
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz \
        $pkgname-mimetypes.xml \
        51-garmin.rules)
sha256sums=('1b6b720903e52c6a14a08978d468456bdcb6d61e8f2b7ec97dd673251009c2af'
            'e8d33948831f8a66edd752c71c653085d6c4d9f1969e70dd8c40b9c7ca37fba0'
            'fab0bf0bbda536015cb66d00b55c5ae4078051d89f82b5b9ee231020316361d6')

build() {
  cd ${srcdir}/$pkgname-$pkgver

  install -d ${srcdir}/$pkgname-$pkgver/build
  cd ${srcdir}/$pkgname-$pkgver/build

  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release ..
  make
}

package(){
  cd ${srcdir}/$pkgname-$pkgver/build

  make DESTDIR=${pkgdir} install

  install -D -m644 ${srcdir}/$pkgname-$pkgver/src/icons/orig.backGlobe128x128.png \
    ${pkgdir}/usr/share/pixmaps/$pkgname.png
  install -D -m644 ${srcdir}/$pkgname-$pkgver/$pkgname.desktop \
    ${pkgdir}/usr/share/applications/$pkgname.desktop  
  install -D -m644 ${srcdir}/$pkgname-mimetypes.xml \
    ${pkgdir}/usr/share/mime/packages/$pkgname-mimetypes.xml

  install -D -m644 ${srcdir}/51-garmin.rules \
    ${pkgdir}/usr/share/$pkgname/51-garmin.rules
}

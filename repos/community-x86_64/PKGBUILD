# $Id$
# Maintainer:  Federico Cinelli <cinelli@aur.archlinux.org>
# Contributor: Kevin Piche <kevin@archlinux.org>
# Contributor: Aaron Griffin <aaron@archlinux.org>

pkgname=gtkpod
pkgver=2.1.3
pkgrel=3
pkgdesc="A platform independent GUI for Apple's iPod using GTK3"
arch=('i686' 'x86_64')
url="http://gtkpod.sourceforge.net"
license=('GPL')
depends=('libanjuta' 'curl' 'flac' 'libid3tag' 'libgpod' 'awk' 'hicolor-icon-theme' 'libwebkit3')
makedepends=('flex' 'intltool' 'faad2' 'libvorbis')
optdepends=('libmp4v2: MP4/h264 support'
            'vorbis-tools: OGG support'
	    'libvorbis: OGG support'
            'id3v2: mp3 conversion support'
            'faad2: m4a conversion support')
install=gtkpod.install
options=('!libtool')
source=("http://downloads.sourceforge.net/gtkpod/$pkgname-$pkgver.tar.gz"
	gtkpod-2.1.3-gdl-3.6.patch)
md5sums=('57f04578de6e3262a436ec574422b144'
	 '94a4cc0b47ba8895c0a51e53e94da517')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  patch -p1 -i "$srcdir/gtkpod-2.1.3-gdl-3.6.patch"
  sed -i 's#python#python2#' scripts/sync-palm-jppy.py

  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

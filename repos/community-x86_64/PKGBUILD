# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: yugrotavele <yugrotavele at archlinux dot us>
# Contributor: Paolo Stivanin <admin AT polslinux DOT it>

pkgname=winff
pkgver=1.5.1
pkgrel=1
pkgdesc='GUI for ffmpeg'
url='http://code.google.com/p/winff/'
license=('GPL3')
arch=('x86_64' 'i686')
depends=('ffmpeg' 'gtk2' 'xterm' 'hicolor-icon-theme')
makedepends=('lazarus' 'lazarus-gtk2' 'gendesk')
install='winff.install'
source=("http://winff.googlecode.com/files/winff-$pkgver-source.tar.gz")
sha256sums=('9d0c46c4245633ed4754fcc0dc104c850daeeefb032a9c5690eda63e84cd367b')

prepare() {
  gendesk -n \
    --pkgname "$pkgname" \
    --pkgdesc "$pkgdesc" \
    --genericname 'Video converter' \
    --categories 'AudioVideo;AudioVideoEditing;GTK'
}

build() {
  cd "$pkgname"

  lazbuild --lazarusdir=/usr/lib/lazarus winff.lpr --ws=gtk2
}

package() {
  install -Dm755 "$srcdir/$pkgname/$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -dm755 "$pkgdir/usr/share/winff/"
  find . -name "*.xml" \
    -exec install -Dm644 {} "$pkgdir/usr/share/winff/" \; &> /dev/null
  install -Dm644 "$srcdir/winff.desktop" \
    "$pkgdir/usr/share/applications/winff.desktop"
  install -Dm644 "$srcdir/$pkgname/winff-icons/48x48/winff.png" \
    "$pkgdir/usr/share/pixmaps/winff.png"
  for size in 16x16 24x24 32x32 48x48; do
    install -Dm644 "$srcdir/$pkgname/winff-icons/$size/winff.png" \
      "$pkgdir/usr/share/icons/hicolor/$size/apps/winff.png"
  done
  install -dm755 "$pkgdir/usr/share/winff/languages/"
  find . -name "*.po" \
    -exec install -Dm644 {} "$pkgdir/usr/share/winff/languages/" \; &> /dev/null
  rm -f "$srcdir/$pkgname/presets.xml"
}

# vim:set ts=2 sw=2 et:

# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: IgnorantGuru http://igurublog.wordpress.com/contact-ignorantguru/
# Contributor: ridikulus_rat <the.ridikulus.rat@gmail.com>

pkgname=spacefm
pkgver=0.7.8
pkgrel=1
pkgdesc="A multi-panel tabbed file manager"
arch=('i686' 'x86_64')
url="http://ignorantguru.github.com/spacefm/"
license=('GPL2')
install=$pkgname.install
depends=('gtk2' 'shared-mime-info' 'desktop-file-utils' 'startup-notification' 'udev')
makedepends=('intltool' 'gettext')
optdepends=('lsof: device processes'
            'eject: eject media'
            'wget: plugin download'
            'gksu: perform as root functionality'
            'pmount: mount as non-root user'
            'udisks: mount as non-root user'
            'udisks2: mount as non-root user')
           #'ktsuss: perform as root functionality' (AUR)
           #'udevil-git: mount as non-root user and mount networks' (AUR)
source=(https://raw.github.com/IgnorantGuru/spacefm/master/packages/${pkgver}/spacefm-${pkgver}.tar.xz)
md5sums=('746b8f55735217a0e6d48e4e666a7534')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    
    # NOTE: To enable hal support add --enable-hal to
    # configure line below. See README for details.
    # NOTE: To disable inotify and use fam or gamin instead, add:
    # --disable-inotify to configure line below.
    # NOTE: To add a custom su program (mysu in this example), add:
    # --with-preferable-sudo="mysu"  to configure line below.
    
    ./configure --prefix=/usr
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}/" install
}


# $Id$
# Maintainer: speps <speps at aur dot archlinux dot org>

_name=QupZilla
_commit=953ca72 # 
pkgname=qupzilla
pkgver=1.4.1_git20130326
pkgrel=1
pkgdesc="Cross-platform QtWebKit browser"
arch=(i686 x86_64)
url="http://www.qupzilla.com/"
license=('GPL3')
depends=('qtwebkit' 'hunspell')
optdepends=('bash-completion: bash completion support')
install="$pkgname.install"
source=("$pkgname-$pkgver.tar.gz::https://github.com/$_name/$pkgname/tarball/$_commit")
md5sums=('1b1b94a53a422436975b19f9ca05f006')

build() {
  cd "$srcdir/$_name-$pkgname-"*

  # enable webgl support
  export USE_WEBGL=true

  qmake-qt4 QUPZILLA_PREFIX=/usr/
  make
}

package() {
  cd "$srcdir/$_name-$pkgname-"*
  make INSTALL_ROOT="$pkgdir/" install

  # zsh completion
  install -Dm644 linux/completion/zsh_completion.sh \
    "$pkgdir/usr/share/zsh/site-functions/_$pkgname"
}

# vim:set ts=2 sw=2 et:

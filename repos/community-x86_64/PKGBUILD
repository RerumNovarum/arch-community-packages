# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-gir-generator
pkgver=0.9.1
_commit=6e53a3b74f2076b2f472920cd2bc1ae4752e0608
pkgrel=1
pkgdesc='Generate static golang bindings for GObject'
arch=('i686' 'x86_64')
url="https://gitcafe.com/Deepin/go-gir-generator"
license=('GPL3')
depends=('gobject-introspection' 'gcc-libs>=5.3.0-3')
makedepends=('git' 'gcc-go')
source=("git+https://github.com/linuxdeepin/go-gir-generator.git#commit=$_commit")
md5sums=('SKIP')

prepare() {
  export GOPATH="$srcdir/go-gir-generator/vender:$srcdir/go-gir-generator"
}

build() {
  cd go-gir-generator/src/gir-generator
  go build -gccgoflags "$(pkg-config --libs gobject-introspection-1.0) $CFLAGS $LDFLAGS" -o gir-generator
}

package() {
  install -Dm 755 go-gir-generator/src/gir-generator/gir-generator "${pkgdir}"/usr/bin/gir-generator
}

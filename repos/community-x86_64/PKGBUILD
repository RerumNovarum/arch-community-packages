# $Id$
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Roman Kyrylych <roman@archlinux.org>
# Contributor: Tor Krill <tor@krill.nu>

pkgname=nautilus-actions
pkgver=3.1.5
pkgrel=1
pkgdesc="Configures programs to be launched when files are selected in Nautilus"
arch=('i686' 'x86_64')
url="http://gnome.org"
license=('GPL')
depends=('nautilus' 'libunique3' 'libgtop' 'gconf' 'libsm')
makedepends=('intltool' 'gnome-doc-utils')
install=nautilus-actions.install
options=('!libtool')
source=(http://ftp.gnome.org/pub/GNOME/sources/nautilus-actions/3.1/$pkgname-$pkgver.tar.xz)
sha256sums=('dc6b7522d04e1ed1505d6cb2e3be7888ffbac3f094ba6204da81a19c92cb230a')

build() {
    cd $srcdir/$pkgname-$pkgver
    ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
        --libexecdir=/usr/lib/nautilus-actions
    make
}

package(){
    cd $srcdir/$pkgname-$pkgver
    make  GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR=$pkgdir install
    mkdir -p $pkgdir/usr/share/gconf/schemas
    gconf-merge-schema $pkgdir/usr/share/gconf/schemas/$pkgname.schemas --domain nautilus-actions \
        $pkgdir/etc/gconf/schemas/*.schemas
    rm -rf $pkgdir/etc/gconf/schemas/
}

# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Harry Jeffery <harry|@|exec64|.|co|.|uk>
# Contributor: Jacob Emmert-Aronson <jacob at mlaronson dot com>
# Contributor: Xiao-Long Chen <chenxiaolong at cxl dot epac dot to>
# Contributor: Paul Viren <paul dot viren at ircameras dot com>
# Contributor: Dominic Meiser <dosm dot mail at gmail dot com>

pkgname=gmock
_gitname=googlemock
pkgver=1.7.0
pkgrel=3
pkgdesc='Google Mock - A library for writing and using C++ mock classes'
url='https://github.com/google/googlemock'
arch=('i686' 'x86_64')
license=('BSD')
depends=('python2' 'gtest' 'gcc-libs' 'sh')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/google/googlemock/archive/release-${pkgver}.tar.gz
        gtest-${pkgver}.tar.gz::https://github.com/google/googletest/archive/release-${pkgver}.tar.gz
        install.patch)
sha512sums=('adf33728dde55d95e1c65c38a10b0578dd63a44e6beeb550dc976dc134f83a3af8096094ae2258f16cd5bebe2326f344db8981f11088bac3ffe04b384d9d2f2d'
            'c623d5720c4ed574e95158529872815ecff478c03bdcee8b79c9b042a603533f93fe55f939bcfe2cd745ce340fd626ad6d9a95981596f1a4d05053d874cd1dfc'
            'b33cc8626dbbe4f634d015f3c988c79c7ef95c36ec741d390ce52d3a2ed2ec40c1279f2da1df29a90e99918a26743f6ec997c8a3a56fe4b092482278255c8945')

prepare() {
  cd ${_gitname}-release-${pkgver}
  mv "${srcdir}/googletest-release-${pkgver}" gtest

  patch -p1 < "${srcdir}/install.patch"
  find -name '*.py' -exec sed -i 's|env python|env python2|g' {} \;

  rm -r gtest/{Makefile,configure}*
  autoreconf -fiv
}

build() {
  cd ${_gitname}-release-${pkgver}
  ./configure --prefix=/usr --with-gtest --enable-external-gtest
  make
}

package() {
  cd ${_gitname}-release-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README CHANGES CONTRIBUTORS -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 755 scripts/gmock-config -t "${pkgdir}/usr/bin"
}

# vim: ts=2 sw=2 et:

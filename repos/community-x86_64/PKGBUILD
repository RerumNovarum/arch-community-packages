# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgbase=cabal-helper
pkgname=(cabal-helper haskell-cabal-helper)
pkgver=0.7.1.0
pkgrel=2
pkgdesc="Simple interface to some of Cabals configuration state used by ghc-mod"
url="http://hackage.haskell.org/package/${pkgbase}"
license=("AGPL3")
arch=('i686' 'x86_64')
makedepends=("ghc=7.10.3" "haskell-mtl" "haskell-temporary" "haskell-utf8-string")
options=('staticlibs')
source=("http://hackage.haskell.org/packages/archive/${pkgbase}/${pkgver}/${pkgbase}-${pkgver}.tar.gz")
sha256sums=('7c78b875b5f246b8422b641923d2ffe593bf65ce9e99373e19ef8372df8db66f')

build() {
    cd "${srcdir}/${pkgbase}-${pkgver}"
    
    runhaskell Setup configure -O --enable-library-profiling --enable-shared \
        --prefix=/usr --docdir="/usr/share/doc/${pkgbase}" --libexecdir="/usr/lib" \
        --libsubdir=\$compiler/site-local/\$pkgid \
            -f-dev
    runhaskell Setup build
    runhaskell Setup haddock --hoogle --html
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package_cabal-helper() {
    depends=('gmp' 'libffi')

    cd "${srcdir}/${pkgbase}-${pkgver}"
    runhaskell Setup copy --destdir="${pkgdir}"
    rm -r "$pkgdir"/usr/{lib/ghc-*,share}
}

package_haskell-cabal-helper() {
    pkgdesc="Simple interface to some of Cabal's configuration state used by ghc-mod (docs and libraries)"
    depends=("${makedepends[@]}" 'cabal-helper')
    install="haskell-cabal-helper.install"
    cd "${srcdir}/${pkgbase}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/${pkgname}/register.sh"
    install    -m744 unregister.sh "${pkgdir}/usr/share/haskell/${pkgname}/unregister.sh"
    install -d -m755 "${pkgdir}/usr/share/doc/ghc/html/libraries"
    ln -s "/usr/share/doc/${pkgbase}/html" "${pkgdir}/usr/share/doc/ghc/html/libraries/${pkgbase}"
    runhaskell Setup copy --destdir="${pkgdir}"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
    rm "$pkgdir/usr/lib/cabal-helper-wrapper-v0.7"
}

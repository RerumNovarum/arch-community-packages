# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>

pkgname=pidgin-lwqq
pkgver=0.1d
_pkgver=0.1-d
pkgrel=3
pkgdesc="A pidgin plugin based on lwqq, a excellent safe useful library for webqq protocol"
arch=('i686' 'x86_64')
url="https://github.com/xiehuc/pidgin-lwqq"
license=('GPL3')
depends=('libpurple' 'curl' 'libev' 'hicolor-icon-theme' 'gtk-update-icon-cache')
optdepends=('pidgin: Multi-protocol instant messaging client'
	    'telepathy-haze: Empathy plugin support')
makedepends=('git' 'cmake')
install=$pkgname.install
source=("https://github.com/xiehuc/$pkgname/archive/${_pkgver}.tar.gz"
	https://github.com/xiehuc/pidgin-lwqq/commit/afdefd3fb8ecce36f6387c5895339671f5833b4e.patch)

build()
{
  cd "${srcdir}/$pkgname-${_pkgver}"
  patch -Np1 -i ../afdefd3fb8ecce36f6387c5895339671f5833b4e.patch

  cmake -DCMAKE_INSTALL_PREFIX=/usr .
  make
}

package()
{
  cd "$srcdir/$pkgname-${_pkgver}"
  make DESTDIR="${pkgdir}" install
}
md5sums=('5ec83a5a3328dac2ded431bcd7c5c034'
         '18fcaf7c7c9a4fb944d34e90e622a9dd')

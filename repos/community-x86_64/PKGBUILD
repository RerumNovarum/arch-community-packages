# $Id$
# Contributor: Biru Ionut <ionut@archlinux.ro>
# Contributor: Luke McCarthy <luke@iogopro.co.uk>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=dvdisaster
pkgver=0.72.3
pkgrel=1
pkgdesc="Provides a margin of safety against data loss on CD and DVD media caused by aging or scratches"
arch=('i686' 'x86_64')
url="http://www.dvdisaster.com/"
license=('GPL')
depends=('gtk2')
options=('!makeflags')
source=(http://dvdisaster.net/downloads/${pkgname}-${pkgver}.tar.bz2 dvdisaster.desktop)
md5sums=('4eb09c1aa3cdbc1dafdb075148fb471d'
         '3a0d6657c47b20a43b93c0e5d58d755a')
sha1sums=('6ed08f8e52f4b90b39134b0caf9b5f0df221b0da'
          'b1e6f372508157923bbb83da7b9af2a51137c589')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # fix png tools building with libpng-1.5.x
  sed -i '24i#include <zlib.h>' tools/pngio.h

  ./configure --prefix=/usr \
    --mandir=/usr/share/man --docdir=/usr/share/doc \
    --localedir=/usr/share/locale \
    --with-nls=yes 
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make BUILDROOT="${pkgdir}" install
  rm -f "${pkgdir}/usr/bin/dvdisaster-uninstall.sh"
  install -D -m 644 ${srcdir}/dvdisaster.desktop \
    "${pkgdir}/usr/share/applications/dvdisaster.desktop"
  install -D -m 644 icons/create.png \
    "${pkgdir}/usr/share/pixmaps/dvdisaster.png"
}

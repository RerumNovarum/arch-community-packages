# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Thomas S Hatch <thatch45@gmail.com>
# Contributor: Aaron Griffin <aaron@archlinux.org>
# Contributor: Todd Maynard <arch@toddmaynard.com>

pkgname=python2-sqlobject
pkgver=1.4.0
pkgrel=1
pkgdesc='Object Relational Manager for providing an object interface to your database'
arch=('any')
license=('LGPL')
url='http://sqlobject.org/'
depends=('python2' 'python2-distribute' 'python2-formencode')
optdepends=('python2-psycopg2: PostgreSQL database support'
            'python2-pysqlite-legacy: SQLite database support')
source=("https://pypi.python.org/packages/source/S/SQLObject/SQLObject-$pkgver.tar.gz")
sha256sums=('d7c27f488c9d28ededad0420416d67b5abce07e92d339cb90dc2be9febaa4a18')

package() {
  cd "$srcdir/SQLObject-$pkgver"

  python2 setup.py install --root="$pkgdir"
  find $pkgdir/usr/lib -type f -exec sed -i 's,^#!/usr/bin/env python,#!/usr/bin/env python2,' {} \;
  echo "/usr/lib/python2.7/site-packages/SQLObject-$pkgver-py2.6.egg" > \
    "$pkgdir/usr/lib/python2.7/site-packages/$pkgname.pth"
}

# vim:set ts=2 sw=2 et:

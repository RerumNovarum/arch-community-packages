# $Id$
# Maintainer:
# Contributor: Roman Kyrylych <roman@archlinux.org>
# Contributor: javonera <javonera@gmail.com>

pkgname=screenlets
pkgver=0.1.2
pkgrel=6
pkgdesc="Screenlets are small owner-drawn applications"
arch=('any')
url="http://screenlets.org/"
license=('GPL3')
depends=('dbus-python' 'gstreamer0.10-python' 'python-evolution' 'python2-gconf' 'python-gnomekeyring' 'python2-gnomevfs' 'python-rsvg' 'python-wnck' 'pyxdg' 'notification-daemon' 'xdg-utils')
optdepends=('gnome-menus: required for Main Menu Screenlet'
        'orca: required for Speech Screenlet'
        'python-imaging: required for Wallpaper Clock Screenlet')
install=screenlets.install
source=(https://code.launchpad.net/screenlets/trunk/$pkgver/+download/$pkgname-$pkgver.tar.bz2)
md5sums=('8bab8052ff5555481fdbe8a5a6310706')
sha1sums=('de5a9776afbbd283a7fc83c63816f3f750b72baf')

build() {
  cd "$srcdir/$pkgname"
  sed -i "s;/usr/local;/usr;g" \
    src/lib/__init__.py setup.py \
    src/bin/screenlets-* desktop-menu/applications/* \
    desktop-menu/*.desktop \
    desktop-menu/desktop-directories/Screenlets.directory

  sed -i 's|/usr/bin/env python|/usr/bin/env python2|' src/share/screenlets/*/*.py \
    src/share/screenlets-manager/*.py \
    src/lib/plugins/mpdclient2.py \
    src/lib/plugins/LastFMProxy.py

  sed -i 's|python|python2|' src/bin/* \
    src/lib/utils.py \
    src/lib/session.py \
    src/lib/__init__.py \
    desktop-menu/applications/*.desktop

  sed -i 's|python |python2 |' \
    src/share/screenlets/MainMenu/gnomeApplet.py \
    src/share/screenlets/MainMenu/MainMenuScreenlet.py \
    src/share/screenlets/Youtube/YoutubeScreenlet.py \
    src/share/screenlets-manager/screenlets-manager.py \
    src/share/screenlets-manager/WidgetScreenlet.py \
    src/share/screenlets-manager/WebappScreenlet.py
}

package() {
  cd "$srcdir/$pkgname"
  mkdir -p "$pkgdir"/etc/screenlets
  echo /usr > "$pkgdir"/etc/screenlets/prefix

  python2 setup.py install --root="$pkgdir"
 
  install -D -m644 desktop-menu/desktop-directories/Screenlets.directory \
    "$pkgdir/usr/share/desktop-directories/Screenlets.directory"
  install -D -m644 desktop-menu/screenlets.svg "$pkgdir/usr/share/icons/screenlets.svg"

  cd desktop-menu/
  for i in $(find -name '*.desktop'); do install -D -m644 $i "$pkgdir"/usr/share/applications/$(echo $i | sed 's|./applications||') ; done
}

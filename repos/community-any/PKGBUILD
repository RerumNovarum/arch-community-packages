# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: cuihao <cuihao dot leo at gmail dot com>
# Contributor: Guten <ywzhaifei@gmail.com> 

pkgname=goagent
pkgver=2.1.13
pkgrel=1
pkgdesc="A gae proxy forked from gappproxy/wallproxy"
arch=("any")
url="http://goagent.googlecode.com"
license=("GPL2")
depends=('python2' 'python2-pyopenssl')
optdepends=('python2-gevent-beta: Optional Gevent Support')
conflicts=('python2-gevent<=0.99')
source=(
  "$pkgname.service"
  "https://github.com/goagent/goagent/archive/v$pkgver.tar.gz"
)
backup=('etc/goagent')
install=goagent.install

package() {
  cd "$srcdir/$pkgname-$pkgver"

  # python2 fix
  sed -i -re "1s/python2?/python2/" local/*.py
  chmod +x local/proxy.py

  mkdir -p "$pkgdir/opt/goagent"
  cp -r local server "$pkgdir/opt/goagent"
  
  # remove windows-only files
  rm -f "$pkgdir/opt/goagent/"*/*.{vbs,dll,exe,manifest,bat}
  rm -f "$pkgdir/opt/goagent/local/python27.zip"

  # remove CA.crt CA.key for security issues
  rm -f "$pkgdir/opt/goagent/local/CA.crt" "$pkgdir/opt/goagent/local/CA.key"
  rm -rf "$pkgdir/opt/goagent/local/certs"

  # config file
  install -Dm644 "${pkgdir}/opt/goagent/local/proxy.ini" "${pkgdir}/etc/goagent"
  ln -sf "/etc/goagent" "${pkgdir}/opt/goagent/local/proxy.ini"

  # systemd service
  install -Dm644 "${srcdir}/goagent.service" "${pkgdir}/usr/lib/systemd/system/goagent.service"
}

# vim:set ts=2 sw=2 et:
md5sums=('a0223e4e436a4d5cc17f76fc1fbbc140'
         '2d9a13fde523a6da4bcbab4f45569260')

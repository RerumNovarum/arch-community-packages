# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Ronald van Haren <ronald.archlinux.org>

pkgname=pyneighborhood
pkgver=0.5.3
pkgrel=1
pkgdesc="A GTK+ 2 rewrite of the well-known LinNeighborhood tool"
license=('GPL')
url="https://launchpad.net/pyneighborhood"
source=("http://launchpad.net/pyneighborhood/0.5/0.5.3/+download/${pkgname}-${pkgver}.tar.bz2")
depends=('pygtk' 'smbclient')
install=pyneighborhood.install
arch=('any')
sha1sums=('78519e97cfbd0562f2f45ec700e4949a8dc18601')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # python2 fix
  for file in $(find . -name '*.py' -print); do
    sed -i 's_^#!.*/usr/bin/python_#!/usr/bin/python2_' $file
    sed -i 's_^#!.*/usr/bin/env.*python_#!/usr/bin/env python2_' $file
  done

  python2 setup.py build
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  python2 setup.py install --root=${pkgdir}
}

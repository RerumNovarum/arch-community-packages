# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
pkgname=speed-dreams-data
pkgver=1.4.0
pkgver2=1.4.0-r2307 # For managing files and directories
pkgrel=3
pkgdesc="A racing simulator with rich graphics and physics (data files)"
arch=('any')
url="http://speed-dreams.sourceforge.net/"
license=('GPL2')
depends=()
makedepends=('freealut' 'freeglut' 'libpng' 'libxmu' 'libxrandr' 'plib' 'libjpeg' 'zlib' 'enet')
source=("http://sourceforge.net/projects/speed-dreams/files/1.4.0/speed-dreams-${pkgver2}-src.tar.bz2/download")
md5sums=('92c324c2c47a598dea9278d9a66c5b78')

build() {
  cd "$srcdir/speed-dreams-${pkgver}-src"
  
  unset LDFLAGS MAKEFLAGS # How can a build system be this broken?

  ./configure --prefix="/opt/speed-dreams" --x-includes=/usr/include --x-libraries=/usr/lib 
  
  make
}

package() {
  cd "$srcdir/speed-dreams-${pkgver}-src"
  
  make DESTDIR="${pkgdir}/" install datainstall

  find "${pkgdir}/" -type d -exec chmod 755 {} \;
  rm -r $pkgdir/opt/speed-dreams/bin
  rm -r $pkgdir/opt/speed-dreams/lib
}

# vim:set ts=2 sw=2 et:

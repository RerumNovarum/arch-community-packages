# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Tiago Pierezan Camargo <tcamargo@gmail.com>

pkgname=python-south
pkgver=0.7.3
pkgrel=3
pkgdesc="Intelligent database migrations library for the Django web framework"
arch=('any')
url="http://south.aeracode.org/"
license=('APACHE')
depends=('django')
makedepends=('python2-distribute')
provides=('south')
conflicts=('south')
replaces=('south')
source=(http://www.aeracode.org/releases/south/south-$pkgver.tar.gz
        south-21a635231327.diff)
sha256sums=('ab1c72cd013fab2ddbef3100f2bf7a7f3d9fbc7169cd3e6c181060c17fdeea97'
            '107b0ddab26fa683f2c72b5c731247e5d8a0378328c079c11e4dc3e966133302')

build() {
  cd "$srcdir/south"

  # Avoid flushing the database during create_test_db to preserve data
  # migrations (FS#23420)
  patch -Np1 -i "$srcdir/south-21a635231327.diff"

  python2 setup.py build
}

package() {
  cd "$srcdir/south"
  python2 setup.py install --root="$pkgdir" -O1
}

# vim:set ts=2 sw=2 et:

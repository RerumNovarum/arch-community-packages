# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Arkham <arkham at archlinux dot us>
# Contributor: Christoph Zeiler <rabyte*gmail>

pkgname=widelands-data
pkgver=16
_realver=build16
pkgrel=2
pkgdesc="Widelands data files"
arch=('any')
url="http://widelands.org/"
license=('GPL')
makedepends=('cmake' 'boost' 'sdl_mixer' 'sdl_image' 'sdl_net' 'sdl_ttf' 'sdl_gfx' 'ggz-client-libs' 'lua' 'glew' 'python2')
source=(http://launchpad.net/widelands/build16/$_realver/+download/widelands-$_realver-src.tar.bz2
        widelands-0.16-libpng15.patch)
md5sums=('3d8c28e145b73c64d8ed1625319d25a2'
         'e492620b071cbd1db04280173653a67c')

build() {
    cd $srcdir/widelands-$_realver-src

    patch -Np0 < $srcdir/widelands-0.16-libpng15.patch

    mkdir -p build/compile && cd build/compile

    cmake ../.. -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DWL_INSTALL_PREFIX=/usr \
        -DWL_INSTALL_DATADIR=share/widelands \
        -DWL_INSTALL_BINDIR=bin
    make
}

package() {
    cd $srcdir/widelands-$_realver-src/build/compile

    make DESTDIR="$pkgdir" install

    rm -r "$pkgdir"/usr/{bin,share/widelands/{ChangeLog,COPYING,CREDITS,VERSION}}
}

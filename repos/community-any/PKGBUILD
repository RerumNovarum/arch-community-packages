# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Hugo Doria <hugo@archlinux.org>

pkgname=billreminder
pkgver=0.3.2
pkgrel=8
pkgdesc="Small and quick accounting application designed to allow for easy tracking of bills"
arch=('any')
url="http://billreminder.gnulinuxbrasil.org/"
license=('MIT')
#depends=('python-pysqlite' 'python-notify' 'dbus-python' 'setuptools' 'desktop-file-utils' 'pygobject' 'pygtk' 'pil' 'perlxml' 'xdg-utils')
depends=('python-pysqlite' 'dbus-python' 'setuptools' 'desktop-file-utils' 'pygtk' 'xdg-utils' 'python2-gconf')
makedepends=('intltool')
install=billreminder.install
source=(http://ftp.gnome.org/pub/GNOME/sources/billreminder/0.3/${pkgname}-${pkgver}.tar.bz2)
md5sums=('dddf919af92bc778d823b92f63e50dd9')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # python2 fix
  for file in billreminder.in billreminderd.in; do
    sed -i 's_#!/usr/bin/env python_#!/usr/bin/env python2_' $file
  done

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --with-gconf-schema-file-dir=/usr/share/gconf/schemas

  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install

  install -d ${pkgdir}/usr/share/licenses/billreminder/
  install -m644 ${srcdir}/billreminder-${pkgver}/COPYING \
    ${pkgdir}/usr/share/licenses/billreminder/
}

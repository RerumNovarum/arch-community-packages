# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>

pkgbase=python-update_checker
pkgname=('python-update_checker' 'python2-update_checker')
pkgver=0.12
pkgrel=1
pkgdesc="A python module that will check for package updates"
arch=('any')
license=('BSD')
url='https://github.com/bboe/update_checker'
makedepends=('python-setuptools' 'python2-setuptools' 'python-requests' 'python2-requests')
source=("https://pypi.io/packages/source/u/update_checker/update_checker-$pkgver.tar.gz")
md5sums=('7d28258ef9db4ca7123c44c80740aaf1')

prepare() {
  cp -a update_checker-$pkgver{,-py2}
}

build() {
  cd "$srcdir/update_checker-$pkgver"
  python setup.py build

  cd "$srcdir/update_checker-$pkgver-py2"
  python2 setup.py build
}

check() {
  cd "$srcdir/update_checker-$pkgver"
  python setup.py test

  cd "$srcdir/update_checker-$pkgver-py2"
  python2 setup.py test
}

package_python-update_checker() {
  depends=('python-requests')

  cd update_checker-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}

package_python2-update_checker() {
  depends=('python2-requests')

  cd update_checker-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}

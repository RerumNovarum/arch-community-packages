# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=blam
pkgver=1.8.11
pkgrel=1
pkgdesc="Simple newsreader for GNOME written in C Sharp"
arch=('any')
url="https://git.gnome.org/browse/blam"
license=('GPL')
depends=('webkit-sharp' 'gconf-sharp' 'notify-sharp' 'hicolor-icon-theme' 'xdg-utils')
makedepends=('intltool')
install=$pkgname.install
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz
        unicode-escape.patch)
sha256sums=('865f0d569bc29c30bda170dc4e457235dc149981f9b79205efd916453da45cd6'
            '2cd3e11dd67b579bd42a8b4ce5005eeb94dc5a19701353aa3d6ae6a607eabf74')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # Fix build
  patch -Np1 -i "$srcdir/unicode-escape.patch"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-schemas-install \
              --with-gconf-schema-file-dir=/usr/share/gconf/schemas
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}

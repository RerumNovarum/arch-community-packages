# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Thomas S Hatch <thatch45@gmail.com>
# Contributor: Aaron Griffin <aaron@archlinux.org>
# Contributor: Todd Maynard <arch@toddmaynard.com>

pkgname=python2-sqlobject
pkgver=1.3.2
pkgrel=1
pkgdesc='Object Relational Manager for providing an object interface to your database'
arch=('any')
license=('LGPL')
url='http://sqlobject.org/'
depends=('python2' 'python2-distribute' 'python2-formencode')
optdepends=('python2-psycopg2: PostgreSQL database support'
            'python2-pysqlite-legacy: SQLite database support')
source=("https://pypi.python.org/packages/source/S/SQLObject/SQLObject-$pkgver.tar.gz")
sha256sums=('f45ab1ef285b32e83732d5a2539245353fcf89e107e087713cb1e2f0aaeb0b89')

package() {
  cd "$srcdir/SQLObject-$pkgver"

  python2 setup.py install --root="$pkgdir"
  find $pkgdir/usr/lib -type f -exec sed -i 's,^#!/usr/bin/env python,#!/usr/bin/env python2,' {} \;
  echo "/usr/lib/python2.7/site-packages/SQLObject-$pkgver-py2.6.egg" > \
    "$pkgdir/usr/lib/python2.7/site-packages/$pkgname.pth"
}

# vim:set ts=2 sw=2 et:

# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>

pkgname=pidgin-lwqq
pkgver=0.1a
_pkgver=0.1-a
pkgrel=3
pkgdesc="A pidgin plugin based on lwqq, a excellent safe useful library for webqq protocol"
arch=('i686' 'x86_64')
url="https://github.com/xiehuc/pidgin-lwqq"
license=('GPL3')
depends=('libpurple' 'curl' 'libev' 'hicolor-icon-theme' 'gtk-update-icon-cache')
optdepends=('telepathy-haze: Empathy plugin support')
makedepends=('git' 'cmake')
install=$pkgname.install
source=("https://github.com/xiehuc/$pkgname/archive/${_pkgver}.tar.gz"
	0.1a_fix.patch)

build()
{
  cd "${srcdir}/$pkgname-${_pkgver}"
  patch -Np1 -i "$srcdir/0.1a_fix.patch"
  
  rm -rf build
  mkdir build
  cd build

  cmake -DCMAKE_INSTALL_PREFIX=/usr -DWITH_LIBEV=On ..
  make
}

package()
{
  cd "$srcdir/$pkgname-${_pkgver}/build"
  make DESTDIR="${pkgdir}" install
}
md5sums=('d0efece1c2b2301f49c68f8a2b7bae95'
         '18baf836e3a1198cdd9dbed287bf6609')

# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: IgnorantGuru http://igurublog.wordpress.com/contact-ignorantguru/
# Contributor: ridikulus_rat <the.ridikulus.rat@gmail.com>

pkgname=spacefm
pkgver=0.8.5
pkgrel=1
pkgdesc='Multi-panel tabbed file manager'
arch=('i686' 'x86_64')
url='http://ignorantguru.github.com/spacefm/'
license=('GPL3')
install=$pkgname.install
depends=('gtk3' 'shared-mime-info' 'desktop-file-utils' 'startup-notification' 'udev')
makedepends=('intltool' 'gettext')
optdepends=('lsof: device processes'
			'eject: eject media'
            'wget: plugin download'
            'gksu: perform as root functionality'
            'udevil: mount as non-root user and mount networks'
            'udisks: mount as non-root user'
            'udisks2: mount as non-root user')
source=(https://raw.github.com/IgnorantGuru/$pkgname/master/packages/$pkgver/$pkgname-$pkgver.tar.xz)
sha256sums=('dd04444ba45196a1bc710ff5e73e91f3aaff7661db56e6c86389579c2a1da54e')

build() {
	cd $srcdir/$pkgname-$pkgver
	./configure --prefix=/usr \
				--disable-pixmaps \
				--with-gtk3
	make
}

package() {
	cd $srcdir/$pkgname-$pkgver
	make DESTDIR=$pkgdir install
}

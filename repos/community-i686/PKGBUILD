# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=librcc
pkgver=0.2.6
pkgrel=5
pkgdesc="Charset Conversion Library"
arch=(i686 x86_64)
url="http://rusxmms.sourceforge.net/"
license=('GPL')
depends=(aspell enca libxml2 db librcd)
makedepends=(patch gtk gtk2)
install=librcc.install
options=('!libtool')
source=(http://downloads.sourceforge.net/rusxmms/${pkgname}-${pkgver}.tar.bz2
        librcc-strnlen.patch)
md5sums=('9bbf248c7312c73c0b6ca19b9c5a2af1'
         '040313d1d8f166ccf2b128cea4c05f21')

build() {
  cd $srcdir/$pkgname-${pkgver}
  ./configure --prefix=/usr
  patch -p1 <$srcdir/librcc-strnlen.patch
  make
}

package() {
  cd $srcdir/$pkgname-${pkgver}
  mkdir -p $pkgdir/etc/rcc
  mkdir -p $pkgdir/usr/lib/rcc/engines
  mkdir -p $pkgdir/usr/bin

  make DESTDIR=$pkgdir install

  make -C examples
  make -C examples install DESTDIR=$pkgdir
  rm -f $pkgdir/usr/bin/example*

  install -m 644 examples/rcc.xml $pkgdir/etc

  if [ -f $pkgdir/usr/bin/rcc-gtk2-config ]; then
      ln -s rcc-gtk2-config $pkgdir/usr/bin/rcc-config
  elif [ -f $pkgdir/usr/bin/rcc-gtk-config ]; then
      ln -s rcc-gtk-config $pkgdir/usr/bin/rcc-config
  else
      echo "#!/bin/bash" > $pkgdir/usr/bin/rcc-config
      echo "echo \"Configuration UI is not available!\"" >> $pkgdir/usr/bin/rcc-config
  fi
}

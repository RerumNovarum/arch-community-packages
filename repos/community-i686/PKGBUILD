# $Id$
# Maintainer: tobias <tobias@archlinux.org>
# Contributor: Tobias Kieslich <tobias@justdreams.de>

pkgname=monica
pkgver=3.7
pkgrel=2
pkgdesc="A monitor calibration tool"
arch=('i686' 'x86_64')
url="http://www.pcbypaul.com/software/monica.html"
license=('BSD')
depends=('fltk' 'xorg-xgamma')
makedepends=('librsvg')
#http://www.pcbypaul.com/software/dl/${pkgname}-${pkgver}.tar.bz2
source=(ftp://ftp.archlinux.org/other/monica/${pkgname}-${pkgver}.tar.bz2 \
        monica.desktop monica.svg)
md5sums=('490aabc35b830e4a3dc32a2f893ba805'
         'a337bfda1fca7228420db0ce92256816'
         '4569f5df7d7b3eaf20108adf48e8dfe4')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  install -Dm755 monica "${pkgdir}/usr/bin/monica"
  install -Dm644 "${srcdir}/monica.desktop" "${pkgdir}/usr/share/applications/monica.desktop"
  install -Dm644 "${srcdir}/monica.svg" "${pkgdir}/usr/share/pixmaps/monica.svg"
  rsvg-convert -w 64 -h 57 -f png -o "${pkgdir}/usr/share/pixmaps/monica.png" "${srcdir}/monica.svg"
  install -Dm644 licence "${pkgdir}/usr/share/licenses/monica/license.txt"
}

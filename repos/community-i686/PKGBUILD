# $Id:$
# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: Nicky726 (Nicky726 <at> gmail <dot> com)
# Contributor: Allan McRae (allan <at> archlinux <dot> org)
# Contributor: Alexander 'bas' Brovikov (bas <at> it-core <dot> org)

pkgname=wine_gecko
pkgver=1.1.0
pkgrel=1
pkgdesc="Wine's built-in replacement for Microsoft's Internet Explorer"
arch=(i686 x86_64)
url="http://wiki.winehq.org/Gecko"
license=(MPL)
depends=('wine>=1.3.2')
source=(http://downloads.sourceforge.net/project/wine/Wine%20Gecko/${pkgver}/${pkgname}-${pkgver}-x86{,_64}.cab)
md5sums=('7217cb91106f2efd85d6d1a84f5ee305'
         '6005df85ed436aa8b1132414aff8ce8e')

if [[ $CARCH == i686 ]]; then
  # Strip x86_64 cab
  source=(${source[0]})
  md5sums=(${md5sums[0]})
fi

package() {
  cd "$srcdir"

  # Install 32-bit
  install -Dm644 $pkgname-$pkgver-x86.cab \
    "$pkgdir/usr/share/wine/gecko/$pkgname-$pkgver-x86.cab"

  if [[ $CARCH == x86_64 ]]; then 
    # Install 64-bit
    install -m644 $pkgname-$pkgver-x86_64.cab "$pkgdir/usr/share/wine/gecko/"
  fi
}

# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: Doehni <dohny@gmx.de>

pkgname=gnome-do
pkgver=0.8.5
pkgrel=1
pkgdesc="A launcher application for gnome (like Launchy or Katapult)"
arch=('i686' 'x86_64')
url="http://do.davebsd.com/"
license=('GPL3')
depends=('ndesk-dbus-glib' 'gnome-desktop-sharp' 'mono-addins' 'gtk2' 'gnome-vfs-sharp'
	 'gnome-keyring-sharp' 'notify-sharp-svn' 'xdg-utils' 'desktop-file-utils'
	 'gconf-sharp' 'libgnome-sharp')
makedepends=('intltool' 'ca-certificates')
options=('!libtool')
install=$pkgname.install
source=("http://edge.launchpad.net/do/trunk/$pkgver/+download/gnome-do-$pkgver.tar.gz")
md5sums=('71e0e653021f895f151a6f46a97f75ba')

build() {
  export MONO_SHARED_DIR="${srcdir}/.wabi"
  mkdir -p "${MONO_SHARED_DIR}"
  cd "${srcdir}/${pkgname}-$pkgver"
  ./configure --prefix=/usr --sysconfdir=/etc
  make
  make DESTDIR="$pkgdir" install
}

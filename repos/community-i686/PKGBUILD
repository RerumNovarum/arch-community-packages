# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Mathias Stearn <mathias@10gen.com>
# Contributor: Alec Thomas

pkgname=mongodb
pkgver=2.2.0
pkgrel=1
pkgdesc='A high-performance, open source, schema-free document-oriented database'
arch=('i686' 'x86_64')
url='http://www.mongodb.org'
license=('AGPL3')
depends=('boost-libs')
makedepends=('scons' 'boost' 'libpcap')
checkdepends=('python2-pymongo')
optdepends=('libpcap: needed for mongosniff')
backup=('etc/mongodb.conf')
install="mongodb.install"
source=("http://downloads.mongodb.org/src/mongodb-src-r${pkgver}.tar.gz"
        'mongodb.rc' 'mongodb.conf' 'mongodb.service'
        'SConscript.client.patch')
md5sums=('832bdb6cc659176fd8d6b16a660ccfc6'
         '9c67e00f4626ad761a8f7d4e037a54d7'
         '4839fe1d638187ca3226e8267b947318'
         '96ab4517b48974ce0e566d9746a75a4f'
         'a9529e2a6e392ffecef7a9178394c814')

build() {
  export SCONSFLAGS="$MAKEFLAGS"

  cd mongodb-src-r${pkgver}

  # fix https://jira.mongodb.org/browse/SERVER-5575
  patch -Np1 -i ${srcdir}/SConscript.client.patch

  sed -i '/nixLibPrefix/s/lib64/lib/' SConstruct

  # --sharedclient generates an error that the source .so could not be found
  # --use-system-sm --use-system-pcre
  scons all --full
}

<<COMMENT
check() {
  export SCONSFLAGS="$MAKEFLAGS"

  cd mongodb-src-r${pkgver}

  scons smokeAll --smokedbprefix=$srcdir
}
COMMENT

package() {
  export SCONSFLAGS="$MAKEFLAGS"

  cd mongodb-src-r${pkgver}

  scons install --full --prefix=$pkgdir/usr

  install -D -m755 $srcdir/mongodb.rc $pkgdir/etc/rc.d/mongodb
  install -D -m644 $srcdir/mongodb.conf $pkgdir/etc/mongodb.conf
  install -D -m644 $srcdir/mongodb.service $pkgdir/usr/lib/systemd/system/mongodb.service
  install -d -m700 $pkgdir/var/lib/mongodb
  install -d -m755 $pkgdir/var/log/mongodb
}

# $Id$
# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
pkgname=pion
pkgver=4.0.13.1
# use the git snapshot until a new version is out
_pkgver=dddce5e02d8a60f958e466e690d38eee924475b0
pkgrel=4
pkgdesc="A C++ development library for implementing lightweight HTTP interfaces"
url="http://www.cloudmeter.com/pion/support/"
arch=('i686' 'x86_64')
license=('custom:Boost')
depends=('boost-libs' 'openssl' 'log4cpp')
makedepends=('boost')
provides=('pion-net')
conflicts=('pion-net')
replaces=('pion-net')
source=(https://github.com/cloudmeter/pion/archive/${_pkgver}.zip)
md5sums=('4b312452058a6f2580b5d88b03321457')

build() {
  cd "$srcdir/$pkgname-$_pkgver"

  sh autogen.sh

  # override configure bug (forces debug even when nobody asked for it)
  sed -i "s/ -ggdb//g" configure
  # override Makefile bug
  sed -i "/docs:/ s/doxygen-doc//" Makefile.in

  ./configure --prefix=/usr --disable-doxygen-doc

  make
}

check() {
  cd "$srcdir/$pkgname-$_pkgver"
  make -k check
}

package() {
  cd "$srcdir/$pkgname-$_pkgver"
  make DESTDIR="$pkgdir" install

  # install license
  install -D -m0755 COPYING "$pkgdir/usr/share/licenses/$pkgname/Boost"
}

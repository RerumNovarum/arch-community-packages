# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: maz-1 <loveayawaka@gmail.com>

pkgname=ndiswrapper-dkms
pkgver=1.60.20160717
_commit=3b81fa7a5d72a19256cf91aff4d201652040702f
pkgrel=1
pkgdesc="Module for NDIS (Windows Network Drivers) drivers supplied by vendors.Use with DKMS"
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/ndiswrapper/"
license=('GPL')
depends=('dkms')
makedepends=('git')
#optdepends=('ndisgtk: GTK+ based frontend for ndiswrapper.')
provides=('ndiswrapper')
conflicts=('ndiswrapper')
source=("git+https://github.com/pgiri/ndiswrapper.git#commit=$_commit"
        "dkms.conf")
md5sums=('SKIP'
         '801e409f0b93a0434e5c923831c49d50')

build() {
  cd "$srcdir/ndiswrapper/ndiswrapper"
  make -C utils
}

package() {
  cd "$srcdir/ndiswrapper/ndiswrapper"
  mkdir -p "$pkgdir/usr/src"
  cp -RL ./driver "$pkgdir/usr/src/ndiswrapper-$pkgver"
  cp "$srcdir/dkms.conf" "$pkgdir/usr/src/ndiswrapper-$pkgver"
  make -C utils sbindir=/usr/bin usrsbindir=/usr/bin DESTDIR="$pkgdir" install
}

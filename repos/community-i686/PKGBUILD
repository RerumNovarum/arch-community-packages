# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=qtav
pkgver=1.8.0
pkgrel=1
pkgdesc="A cross-platform multimedia playback framework based on Qt and FFmpeg"
arch=('i686' 'x86_64')
url="http://www.qtav.org/"
license=('LGPL')
depends=('qt5-base' 'ffmpeg' 'libxv' 'openal' 'portaudio' 'desktop-file-utils')
makedepends=('git')
install=$pkgname.install
source=("git+https://github.com/wang-bin/QtAV.git#tag=v$pkgver")
sha256sums=('SKIP')

prepare() {
  cd QtAV
  git submodule update --init
}

build() {
  cd QtAV

  qmake-qt5
  make
}

package() {
  cd QtAV

  make INSTALL_ROOT="${pkgdir}" install 
}

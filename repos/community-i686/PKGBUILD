# $Id$
# Maintainer: speps <speps at aur dot archlinux dot org>

pkgname=guitarix2
pkgver=0.28.0
pkgrel=1
pkgdesc="A simple mono guitar amplifier and FX for JACK using Faust"
arch=('i686' 'x86_64')
url="http://guitarix.sourceforge.net/"
license=('GPL')
depends=('gtkmm' 'jack' 'liblrdf' 'boost-libs' 'zita-convolver' 'zita-resampler')
makedepends=('python2' 'boost' 'intltool' 'lv2')
optdepends=('meterbridge: sound meters')
provides=('guitarix' 'gx_head')
conflicts=('guitarix' 'gx_head')
replaces=('guitarix' 'gx_head')
install="$pkgname.install"
source=("http://download.sourceforge.net/project/guitarix/guitarix/$pkgname-$pkgver.tar.bz2")
md5sums=('1f515c13d4577a6dbc7c4b1606395989')

build() {
  cd "$srcdir/guitarix-$pkgver"
  python2 waf configure --prefix=/usr \
                        --no-desktop-update \
                        --no-ldconfig \
                        --no-faust \
                        --optimization \
                        --shared-lib \
                        --lib-dev
  python2 waf build
}

package() {
  cd "$srcdir/guitarix-$pkgver"
  python2 waf install --destdir="$pkgdir"
}

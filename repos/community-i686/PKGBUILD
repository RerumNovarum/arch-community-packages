# $Id$
# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: dieghen89 <dieghen89@gmail.com>
# Contributor: Alessio 'Bl@ster' Biancalana <dottorblaster@gmail.com>

pkgname=docky
pkgver=2.1.1
pkgrel=1
pkgdesc="The finest dock no money can buy!"
url="https://launchpad.net/docky"
arch=(i686 x86_64)
license=(GPL)
depends=(gnome-desktop-sharp gnome-keyring-sharp gtk2 mono-addins
         hicolor-icon-theme ndesk-dbus-glib notify-sharp-svn xdg-utils
         desktop-file-utils dockmanager gio-sharp dbus-sharp-glib)
makedepends=(intltool)
options=('!libtool' '!emptydirs')
install=docky.install
source=("http://launchpad.net/$pkgname/${pkgver%.*}/$pkgver/+download/$pkgname-$pkgver.tar.gz"
        "bzr-1798.diff")
md5sums=('628418e11088b96d47287dac6e838507'
         'e7fddf932967d37ad9bc060ba3bda15f')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  export MONO_SHARED_DIR="$srcdir/.wabi"
  mkdir -p "$MONO_SHARED_DIR"

  patch -Np0 -i "$srcdir/bzr-1798.diff"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 install

  mkdir -p "$pkgdir/usr/share/gconf/schemas"
  gconf-merge-schema "$pkgdir/usr/share/gconf/schemas/docky.schemas" "$pkgdir"/etc/gconf/schemas/*.schemas
  rm -rf "$pkgdir/etc/gconf"
}

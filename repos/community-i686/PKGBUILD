# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=purescript
pkgver=0.9.2
pkgrel=1
pkgdesc="PureScript Programming Language Compiler"
url="http://www.purescript.org/"
license=("MIT")
arch=('i686' 'x86_64')
depends=('gmp' 'libffi' 'zlib')
makedepends=("ghc=8.0.1" "haskell-aeson" "haskell-aeson-better-errors" "haskell-ansi-terminal"
             "haskell-ansi-wl-pprint" "haskell-base-compat" "haskell-bower-json" "haskell-boxes"
             "haskell-clock" "haskell-dlist" "haskell-edit-distance" "haskell-file-embed"
             "haskell-fsnotify" "haskell-glob" "haskell-http-types" "haskell-language-javascript"
             "haskell-lifted-base" "haskell-monad-control" "haskell-monad-logger" "haskell-mtl"
             "haskell-optparse-applicative" "haskell-parallel" "haskell-parsec"
             "haskell-pattern-arrows" "haskell-pipes" "haskell-pipes-http" "haskell-protolude"
             "haskell-regex-tdfa" "haskell-safe" "haskell-semigroups" "haskell-sourcemap"
             "haskell-spdx" "haskell-split" "haskell-stm" "haskell-syb" "haskell-text"
             "haskell-transformers-base" "haskell-transformers-compat"
             "haskell-unordered-containers" "haskell-utf8-string" "haskell-vector" "haskell-wai"
             "haskell-wai-websockets" "haskell-warp" "haskell-websockets")
optdepends=('pulp: for development environment')
source=("http://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz"
        protolude-0.1.6.patch)
sha256sums=('08a09bb101a900bf5ec3954cddfbfac11e81550d1ec92221559922f0e5b1acb3'
            'a85f3fb3e78c7dec7c69f0b86815395c8a58dfcee726dca4757ed266c9644451')

prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    # https://github.com/purescript/purescript/pull/2225
    patch -p1 -i ../protolude-0.1.6.patch
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    runhaskell Setup configure -O --enable-library-profiling --enable-shared \
        --prefix=/usr --docdir="/usr/share/doc/${pkgbase}" --datasubdir="$pkgbase" \
        --libsubdir=\$compiler/site-local/\$pkgid
    LC_CTYPE=en_US.UTF-8 runhaskell Setup build
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    runhaskell Setup copy --destdir="${pkgdir}"
    rm -r "$pkgdir"/usr/{lib,share}
}

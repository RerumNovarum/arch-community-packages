# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: dojo <joerg.doll[at]gmx.de>

pkgname=gtkdialog
pkgver=0.7.20
pkgrel=3
pkgdesc="Gtkdialog is a small utility for fast and easy GUI building."
arch=('i686' 'x86_64')
url="http://linux.pte.hu/~pipas/gtkdialog/"
license=("GPL")
makedepends=('pkgconfig')
depends=('gtk2' 'libglade')
install=gtkdialog.install
options=('zipman')
source=(ftp://linux.pte.hu/pub/gtkdialog/$pkgname-$pkgver.tar.gz
	build-fix.patch)
md5sums=('597103b3ec575dd7af9e84931a62c532'
         'e759df95bf729957df34f5c4310d34d1')

build() {
  cd $srcdir/$pkgname-$pkgver
  patch -p1 <$srcdir/build-fix.patch
  ./configure --prefix=/usr --mandir=/usr/share/man --infodir=/usr/share/info
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make prefix=$pkgdir/usr install
  install -Dm0644 doc/gtkdialog.info $pkgdir/usr/share/info/gtkdialog.info
  mkdir -p $pkgdir/usr/share/doc/$pkgname
  cp -a examples $pkgdir/usr/share/doc/$pkgname/examples
  rm -f $pkgdir/usr/share/doc/$pkgname/examples/Makefile*
}

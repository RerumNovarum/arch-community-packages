# $Id: PKGBUILD 140196 2011-10-09 08:27:16Z angvp $
# Maintainer: Angel Velasquez <angvp@archlinux.org> 
# Contributor: Sergej Pupykin <sergej@aur.archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=python2-m2crypto
pkgver=0.23.0
pkgrel=2
pkgdesc="A crypto and SSL toolkit for Python"
arch=('i686' 'x86_64')
url="https://pypi.python.org/pypi/M2Crypto"
license=('BSD')
depends=('python2' 'openssl')
makedepends=('swig' 'python2-setuptools')
#source=("m2crypto-${pkgver}.tar.gz::https://github.com/martinpaljak/M2Crypto/archive/v$pkgver.tar.gz"
source=("https://pypi.python.org/packages/source/M/M2Crypto/M2Crypto-$pkgver.tar.gz"
	"https://gitlab.com/m2crypto/m2crypto/commit/d6b509030dfa12ee299f0de623fb78f2223e6579.diff"
	"LICENSE")
md5sums=('89557730e245294a6cab06de8ad4fb42'
         'd055e161546b8f529910f15a496784b6'
         'b0e1f0b7d0ce8a62c18b1287b991800e')

prepare() {
  cd "${srcdir}/M2Crypto-${pkgver}"
  patch -p1 -i $srcdir/d6b509030dfa12ee299f0de623fb78f2223e6579.diff
}

build() {
  cd "${srcdir}/M2Crypto-${pkgver}"
  python2 setup.py build
}

package() {
  cd "${srcdir}/M2Crypto-${pkgver}"
  python2 setup.py install --root="${pkgdir}/" --optimize=1
  install -D -m644 $srcdir/LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

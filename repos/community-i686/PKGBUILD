# $Id$
# Maintainer: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: Allan McRae <mcrae_allan@hotmail.com>
# Contributor: fancris3 <fancris3 at aol.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=gecko-mediaplayer
pkgver=1.0.4
pkgrel=2
pkgdesc='Browser plugin that uses gnome-mplayer to play media in a web browser.'
arch=('i686' 'x86_64')
url='http://code.google.com/p/gecko-mediaplayer/'
license=('GPL')
depends=("gnome-mplayer>=${pkgver}" 'dbus-glib')
makedepends=('xulrunner' 'pkgconfig')
replaces=('mplayer-plugin')
source=("http://gecko-mediaplayer.googlecode.com/files/${pkgname}-${pkgver}.tar.gz"
        'firefox-8.patch')
md5sums=('7bb7665f8a1e6832907d052863e8f7aa'
         'e58a8fc33c1cf7a2453e1f6612acf028')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -p1 -i ../firefox-8.patch

  ./configure --prefix=/usr --sysconfdir=/etc --without-gconf --enable-new-libxul
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}

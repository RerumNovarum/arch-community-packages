# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Jaroslaw Swierczynski <swiergot@aur.archlinux.org>

pkgname=xnee
pkgver=3.15
pkgrel=2
pkgdesc="A program to record, distribute, and replay X (X11) protocol data"
arch=('i686' 'x86_64')
url="http://www.sandklef.com/xnee/"
license=('GPL3')
depends=('gtk2' 'libxtst')
makedepends=('texinfo')
optdepends=('xosd: feedback support')
options=('!libtool' '!emptydirs')
source=(ftp://ftp.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.gz
        gnee.desktop)
sha256sums=('56e0b1cca36ebf95b6f7c82c7028ab8a9b5003728ca1044016031b489bc3e8cd'
            'de42d419a46083e39a9b6d60c045c1cd68deb793cc5166a86df81dff608387c1')

build() {
  cd ${srcdir}/$pkgname-$pkgver

  ./configure --prefix=/usr --enable-gui --enable-lib --disable-static --disable-static-programs
  make
}

package() {
  cd ${srcdir}/$pkgname-$pkgver

  make DESTDIR=${pkgdir} install
  install -Dm644 ${srcdir}/gnee.desktop ${pkgdir}/usr/share/applications/gnee.desktop
}

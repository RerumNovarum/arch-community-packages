# $Id$
# Maintainer: Peter Lewis <plewis@aur.archlinux.org>
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Panagiotis Papadopoulos <pano_90 AT gmx DOT net>

pkgname=rekonq
pkgver=1.3
pkgrel=1
pkgdesc='A WebKit based web browser for KDE'
arch=('i686' 'x86_64')
url='http://rekonq.kde.org/'
license=('GPL')
depends=('kdebase-keditbookmarks' 'qoauth')
makedepends=('cmake' 'automoc4')
optdepends=('qca-ossl: to use the sync handler')
install=${pkgname}.install
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.bz2")
sha256sums=('8db67e674b3dd013a6511f9612829f958ca95a2d515f89fb999f5568bfed7ffb')

build(){
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}

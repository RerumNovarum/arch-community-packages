# $Id$
# Maintainer: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=clamz
pkgver=0.4
pkgrel=4
pkgdesc="Command-line program to download MP3 files from Amazon.com's music store."
arch=('i686' 'x86_64')
url='http://code.google.com/p/clamz/'
license=('GPL3')
depends=('libgcrypt' 'curl' 'expat' 'shared-mime-info' 'desktop-file-utils')
install='clamz.install'
source=("http://clamz.googlecode.com/files/clamz-${pkgver}.tar.gz"
        'unencrypted-amz.patch')
md5sums=('ab7661340ee27d206f36064cfbd5bfa4'
         '603ef334d297614edabbd1bf5bfe585d')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	# Fix issue with update-mime-database
	sed -i 's|$(UPDATE_DESKTOP_DATABASE)\ $(DESTDIR)$(applications_dir)||' Makefile.in
	sed -i 's|$(UPDATE_MIME_DATABASE)\ $(DESTDIR)$(mime_dir)||' Makefile.in

  # Fix download from Amazon Cloud Player (FS#26532, FS#26577).
  patch -Np1 -i "${srcdir}/unencrypted-amz.patch"

	./configure --prefix=/usr/
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}

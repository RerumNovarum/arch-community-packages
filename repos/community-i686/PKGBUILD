# $Id: PKGBUILD,v 1.15 2009/04/12 12:17:40 StefanHusmann Exp $
# Maintainer: Roman Kyrylych <roman@archlinux.org>
# Contributor: Johannes Sjolund <j.sjolund@gmail.com>

pkgname=gnome-commander
pkgver=1.2.7
pkgrel=3
pkgdesc="A graphical two-pane filemanager for Gnome"
arch=('i686' 'x86_64')
url="http://www.nongnu.org/gcmd/"
license=('GPL')
depends=('libgnomeui' 'gnome-vfs' 'gconf>=2.18.0.1-4' 'exiv2' 'lcms' 'taglib'
         'chmlib' 'libgsf' 'python')
makedepends=('perlxml' 'gnome-doc-utils>=0.11.2')
options=(!libtool)
install=gnome-commander.install
source=(ftp://ftp.gnome.org/pub/GNOME/sources/gnome-commander/1.2/$pkgname-$pkgver.tar.bz2)
md5sums=('7b0d1d50fda548be6600696bc752b6de')

build() {
  cd $startdir/src/$pkgname-$pkgver
  ./configure --prefix=/usr --libdir=/usr/lib --sysconfdir=/etc \
    --localstatedir=/var --disable-scrollkeeper --enable-python

  make || return 1
  make -j1 GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR=$startdir/pkg install || return 1

  mkdir -p ${startdir}/pkg/usr/share/gconf/schemas
  gconf-merge-schema ${startdir}/pkg/usr/share/gconf/schemas/${pkgname}.schemas \
    ${startdir}/pkg/etc/gconf/schemas/*.schemas
  rm -f ${startdir}/pkg/etc/gconf/schemas/*.schemas
}

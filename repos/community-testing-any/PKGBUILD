# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: cuihao <cuihao dot leo at gmail dot com>
# Contributor: Guten <ywzhaifei@gmail.com> 

pkgname=goagent
pkgver=3.0.1
pkgrel=1
pkgdesc="A gae proxy forked from gappproxy/wallproxy"
arch=("any")
url="http://goagent.googlecode.com"
license=("GPL2")
depends=('python' 'python-pyopenssl')
optdepends=('python2: if you want to use uploader.zip to upload server')
source=(https://github.com/goagent/goagent/archive/v$pkgver.tar.gz
        $pkgname.service)
backup=('etc/goagent')
install=goagent.install

package() {
  cd "$srcdir/$pkgbase-$pkgver"
  
  install -Dm755 local/proxy.py "$pkgdir/usr/share/$pkgname/local/proxy.py"
  install -Dm644 local/proxy.pac "$pkgdir/usr/share/$pkgname/local/proxy.pac"
  install -Dm644 local/cacert.pem "$pkgdir/usr/share/$pkgname/local/cacert.pem"
  install -Dm644 local/SwitchyOptions.bak "$pkgdir/usr/share/$pkgname/local/SwitchyOptions.bak"
  install -Dm644 local/SwitchySharp_1_9_52.crx "$pkgdir/usr/share/$pkgname/local/SwitchySharp_1_9_52.crx"

  install -Dm644 local/proxy.ini "$pkgdir/etc/goagent"
  ln -sf "/etc/goagent" "${pkgdir}/usr/share/$pkgname/local/proxy.ini"
  
  cp -r server "$pkgdir/usr/share/$pkgname/"
  rm "$pkgdir/usr/share/$pkgname/server/uploader.exe"

  # systemd service
  install -Dm644 "${srcdir}/goagent.service" "${pkgdir}/usr/lib/systemd/system/goagent.service"
}

# vim:set ts=2 sw=2 et:
sha512sums=('ad2f3f67a411172f395979b2bf849393044a3b2eb60dd4d212f1993e8a4dea432e9909c668994fa497cd4e75ce3a1b52d3ca5e7ae545a8928b98ea072b7191df'
            'b8f5f6342303edd520a577d0348deef7017ba79aa5c6f43c4e1c368d7b204f6364f4537f2a5e21f3b63a321bdcf3d407e5dea9883211c11a4d11801b057cddc2')

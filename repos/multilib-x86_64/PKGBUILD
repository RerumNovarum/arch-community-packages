# $Id$
# Maintainer: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Andrej Gelenberg <andrej.gelenberg@udo.edu>

_pkgbasename=elfutils
pkgname=lib32-elfutils
pkgver=0.155
pkgrel=1
pkgdesc="Collection of libraries for working with ELF object files and DWARF debugging information (32-bit)"
arch=('x86_64')
url="https://fedorahosted.org/elfutils/"
license=('LGPL3' 'GPL' 'GPL3')
depends=('lib32-bzip2' 'lib32-zlib')
makedepends=('gcc-multilib')
options=('!makeflags')
source=(https://fedorahosted.org/releases/e/l/elfutils/${pkgver}/elfutils-${pkgver}.tar.bz2
        elfutils-0.155-binutils-pr-ld-13621.patch
        git-fixes.patch)
sha1sums=('1b96084cd9fe037c1c0bd2c280aaa701f913462b'
          '8a8be7c0686c6311c1ce969ba10494e704ead48c'
          '0caab67ed6073a81ca836a4172fa4af35cc1d817')

prepare() {
  cd ${srcdir}/${_pkgbasename}-${pkgver}

  patch -Np1 -i ../elfutils-0.155-binutils-pr-ld-13621.patch
  patch -Np1 -i ../git-fixes.patch
}

build() {
  cd ${srcdir}/${_pkgbasename}-${pkgver}
	
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  CFLAGS+=" -g"  # required for test-suite success
  
  ./configure --prefix=/usr --libdir=/usr/lib32
  make
}

check() {
  cd ${srcdir}/${_pkgbasename}-${pkgver}

  make check
}

package() {
  cd ${srcdir}/${_pkgbasename}-${pkgver}

  make DESTDIR=${pkgdir} install
  rm -rf ${pkgdir}/usr/{bin,include,share}
}

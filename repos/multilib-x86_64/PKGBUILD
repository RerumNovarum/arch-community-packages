# $Id$
# Contributor : Ionut Biru <ibiru@archlinux.org>

pkgname=lib32-libdbus
_pkgbasename=libdbus
pkgver=1.10.10
pkgrel=3
pkgdesc="Freedesktop.org message bus system (32-bit)"
url="https://wiki.freedesktop.org/www/Software/dbus/"
arch=(x86_64)
license=(GPL custom)
depends=(lib32-systemd dbus)
makedepends=(gcc-multilib lib32-glib2 lib32-expat)
provides=(lib32-dbus-core lib32-dbus)
conflicts=(lib32-dbus-core lib32-dbus)
replaces=(lib32-dbus-core lib32-dbus)
source=(https://dbus.freedesktop.org/releases/dbus/dbus-$pkgver.tar.gz{,.asc})
sha256sums=('9d8f1d069ab4d1a0255d7b400ea3bcef4430c42e729b1012abb2890e3f739a43'
            'SKIP')
validpgpkeys=('DA98F25C0871C49A59EAFF2C4DE8FF2A63C7CC90'  # Simon McVittie <simon.mcvittie@collabora.co.uk>
              '3C8672A0F49637FE064AC30F52A43A1E4B77B059') # Simon McVittie <simon.mcvittie@collabora.co.uk>

prepare() {
  cd dbus-$pkgver
  autoreconf -fvi
}

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd dbus-$pkgver

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libdir=/usr/lib32 \
      --libexecdir=/usr/lib/dbus-1.0 --with-dbus-user=dbus \
      --with-system-pid-file=/run/dbus/pid \
      --with-system-socket=/run/dbus/system_bus_socket \
      --with-console-auth-dir=/run/console/ \
      --enable-inotify --disable-static \
      --disable-verbose-mode --disable-asserts \
      --with-systemdsystemunitdir=/usr/lib/systemd/system \
      --enable-systemd --enable-user-session
  make
}

check() {
  cd dbus-$pkgver
  make check
}

package() {
  cd dbus-$pkgver

  make DESTDIR="$pkgdir" install

  rm -r "$pkgdir"/usr/{bin,include,lib,share}
  rm -r "$pkgdir"/{etc,var}

  install -d "$pkgdir/usr/share/licenses"
  ln -s dbus "$pkgdir/usr/share/licenses/$pkgname"
}

# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>

pkgname=pari-sage
_pkgver=2.8-2044-g89b0f1e
pkgver=${_pkgver//-/.}
pkgrel=1
pkgdesc='Computer algebra system designed for fast computations in number theory. Development snapshot required by SageMath'
url='http://pari.math.u-bordeaux.fr/'
license=(GPL)
arch=(i686 x86_64)
depends=(gmp libx11)
makedepends=(perl texlive-core)
optdepends=('perl: gphelp, tex2mail')
conflicts=(pari)
provides=(pari)
source=("http://mirrors.mit.edu/sage/spkg/upstream/pari/pari-$_pkgver.tar.gz"
 'public_memory_functions.patch')
md5sums=('ca27af679818f4cfef7e8eef9edc83cb'
         '9172b9faee975cd3fe0f97126ea61af8')

prepare() {
  cd pari-${_pkgver}

# make some private functions public
  patch -p1 -i "$srcdir"/public_memory_functions.patch
}

build() {
  cd pari-${_pkgver}
  ./Configure --prefix=/usr --with-readline --mt=pthread --with-gmp
  make all
}

package() {
  cd pari-${_pkgver}
  make DESTDIR="$pkgdir" install
  ln -sf gp.1.gz "$pkgdir"/usr/share/man/man1/pari.1
}

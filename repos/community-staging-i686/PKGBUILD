# $Id$
# Maintainer: Ray Rashif <schivmeister@gmail.com>
# Contributor: Corrado Primier <bardo@aur.archlinux.org>
# Contributor: Eric Belanger <belanger@astro.umontreal.ca>
# Contributor: Andrew Conkling <andrewski@fr.st>

pkgname=gourmet
pkgver=0.15.4
pkgrel=2
pkgdesc="A simple but powerful recipe-managing application"
arch=('i686' 'x86_64')
url="http://grecipe-manager.sourceforge.net"
license=('GPL')
depends=('gnome-python-extras' 'libglade' 'metakit' 'python-imaging'
         'python-pysqlite' 'python-reportlab' 'python-sqlalchemy')
makedepends=('intltool')
optdepends=('libgnomeprintui: gnome printing support'
            'pyrtf: RTF support')
changelog=($pkgname.changelog)
source=(http://downloads.sourceforge.net/grecipe-manager/$pkgname-$pkgver.tar.gz
        semantics.patch)

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # python2 fix
  for file in $(find . -name '*.py' -print); do
    sed -i 's_#!/usr/bin/python_#!/usr/bin/python2_' $file
    sed -i 's_#!/usr/bin/env python_#!/usr/bin/env python2_' $file
  done

  # patch for wrong conditional statement
  patch -Np1 -i ../semantics.patch

  python2 setup.py install --root="$pkgdir"
}
md5sums=('f8b6f6375fe2e0a8a5a050fdddafb7a8'
         'a2c91fe3f877e9478dd607b2424bd6b7')

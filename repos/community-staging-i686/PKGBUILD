# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Matthias Maennich <arch@maennich.net>
# Contributor: <boenki@gmx.de>

pkgname=qlandkartegt
pkgver=1.2.4
pkgrel=2
pkgdesc="Use your GPS with Linux"
arch=('i686' 'x86_64')
url="http://www.qlandkarte.org/"
license=('GPL')
depends=('gdal' 'gpsd' 'libdmtx' 'libexif' 'libmysqlclient' 'mesa' 'postgresql-libs' 'qt' 'shared-mime-info' 'qtwebkit')
makedepends=('cmake' 'garmindev')
optdepends=('garmindev: to connect garmin devices')
install=$pkgname.install
changelog=$pkgname.changelog
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz
        $pkgname-mimetypes.xml
        'fix-glu-header.patch')
sha256sums=('3f87667ceffde60a7c533df7e25f375793a093970d89bfa1cd842b3595c58f2c'
            'e8d33948831f8a66edd752c71c653085d6c4d9f1969e70dd8c40b9c7ca37fba0'
            '8e8c6440e1674a87d5a221c978f2be832457fb43980894725044837581625618')

build() {
  cd "${srcdir}/$pkgname-$pkgver"

  patch -R -p2 -i "${srcdir}"/fix-glu-header.patch

  cd "${srcdir}"
  mkdir build
  cd build

  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package(){
  cd "${srcdir}/build"

  make DESTDIR="${pkgdir}" install

  install -D -m644 "${srcdir}/$pkgname-mimetypes.xml" \
    "${pkgdir}/usr/share/mime/packages/$pkgname-mimetypes.xml"
}

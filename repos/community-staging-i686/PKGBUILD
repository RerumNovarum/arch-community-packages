# $Id$
# Maintainer: Mateusz Herych <heniekk@gmail.com>
# Contributor: Sergio Jovani Guzman <moret.sjg@gmail.com>
# Adopted: royrocks <royrocks13@gmail.com>
pkgname=tuxpaint
pkgver=0.9.21
pkgrel=5
pkgdesc="Tux Paint is a free drawing program designed for young children."
arch=('i686' 'x86_64')
url="http://www.tuxpaint.org"
license=('GPL')
backup=('etc/tuxpaint/tuxpaint.conf')
depends=('python2' 'fribidi' 'sdl_ttf' 'sdl_mixer' 'sdl_image' 'sdl_pango' 'libpaper' 'librsvg')
source=(http://downloads.sourceforge.net/sourceforge/tuxpaint/$pkgname-$pkgver.tar.gz tuxpaint-0.9.21-libpng1.5.patch)
md5sums=('a88401d1860648098eeed819cff038fa'
         '11d11119b3316c13f7b7a77048317bce')
sha1sums=('082a179a3cb44ee17af47421bc92125a890846e4'
          '5595a65ebabc46bebd2d0670589effc8dbdd4e0c')

build() {
 cd "$srcdir/$pkgname-$pkgver"

 # python2 fix
 for file in docs/zh_tw/mkTuxpaintIM.py fonts/locale/zh_tw_docs/maketuxfont.py; do
    sed -i 's_#!/usr/bin/env python_#!/usr/bin/env python2_' $file
 done

 patch -p0 -i ../tuxpaint-0.9.21-libpng1.5.patch
 sed -i 's/$(ARCH_LINKS)/$(ARCH_LINKS) -lpng/' Makefile
 echo "Categories=Game;KidsGame;Graphics;RasterGraphics;" >> src/tuxpaint.desktop
 make PREFIX=/usr
}

package() {
 cd "$srcdir/$pkgname-$pkgver"
 make PREFIX=/usr DESTDIR="$pkgdir" GNOME_PREFIX=/usr X11_ICON_PREFIX="$pkgdir/usr/share/pixmaps" install
 chmod -R 644 "$pkgdir"/usr/share/doc/tuxpaint-dev/*
 chmod 755 "$pkgdir/usr/share/doc/tuxpaint-dev/html"
}

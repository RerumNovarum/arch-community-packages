# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=medit
pkgver=1.0.0
pkgrel=2
pkgdesc="A GTK text editor"
arch=('i686' 'x86_64')
url="http://mooedit.sourceforge.net"
options=('!emptydirs')
license=('GPL')
depends=('pygtk' 'libxml2' 'pcre' 'libsm' 'python2' 'gtk2' 'gcc-libs')
optdepends=('ctags')
makedepends=(pkgconfig perlxml intltool gcc-objc)
install=medit.install
source=(http://downloads.sourceforge.net/mooedit/medit-$pkgver.tar.bz2)
md5sums=('fa15f74b04185b0423837ab812c66b08')

build() {
  cd $srcdir/medit-$pkgver
  export PYTHON=/usr/bin/python2
  [ -f Makefile ] || ./configure --prefix=/usr
  make
}

package() {
  cd $srcdir/medit-$pkgver

  make DESTDIR=${pkgdir} install

  cd ${pkgdir}/usr/share/medit-1/language-specs
  sed -i "s|*.sh|*.sh;PKGBUILD|" sh.lang

  rm -rf $pkgdir/usr/share/mime
  rm -rf $pkgdir/usr/share/icons/hicolor/icon-theme.cache
}

# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=haskell-gtk
pkgver=0.12.3
pkgrel=2
pkgdesc="Binding to the gtk library for Gtk2Hs."
url="http://hackage.haskell.org/package/gtk"
license=('LGPL2.1')
arch=('i686' 'x86_64')
depends=('ghc' 'gtk2' "haskell-pango=0.12.3-2" 'haskell-mtl=2.1.1-1')
makedepends=("gtk2hs-buildtools")
provides=('gtk2hs-gtk')
replaces=('gtk2hs-gtk')
conflicts=('gtk2hs-gtk')
options=('strip')
install=gtk2hs-gtk.install
source=(http://hackage.haskell.org/packages/archive/gtk/$pkgver/gtk-$pkgver.tar.gz
        'gtk-gthread.h-include.patch')
md5sums=('65a65b5cc1e1db0fbf722779a7044ded'
         '5277990f8ca28d2b12e77dac97c10354')

build() {
    cd ${srcdir}/gtk-${pkgver}

    # fix compile error with latest glib2 >= 2.32
    # http://hackage.haskell.org/trac/gtk2hs/ticket/1253
    # got the patch from fedora
    patch -Np1 -i ${srcdir}/gtk-gthread.h-include.patch

    runhaskell Setup configure -O -p --enable-split-objs --enable-shared \
      --prefix=/usr --docdir=/usr/share/doc/haskell-gtk \
      --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup haddock
    runhaskell Setup register   --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    #_ghcver=`pacman -Q ghc | cut -f2 -d\  | cut -f1 -d-`
    #depends=("ghc=${_ghcver}" 'gtk2' "haskell-pango=$pkgver" 'haskell-mtl=2.0.1.0')

    cd ${srcdir}/gtk-${pkgver}
    install -D -m744 register.sh   ${pkgdir}/usr/share/haskell/gtk2hs-gtk/register.sh
    install    -m744 unregister.sh ${pkgdir}/usr/share/haskell/gtk2hs-gtk/unregister.sh
    install -d -m755 ${pkgdir}/usr/share/doc/ghc/html/libraries
    ln -s /usr/share/doc/haskell-gtk/html ${pkgdir}/usr/share/doc/ghc/html/libraries/gtk2hs-gtk
    runhaskell Setup copy --destdir=${pkgdir}
}

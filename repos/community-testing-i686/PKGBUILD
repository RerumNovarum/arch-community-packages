# $Id$
# Maintainer: Thorsten TÃ¶pper <atsutane-tu@freethoughts.de>
# Contributor: Alexander Fehr <pizzapunk gmail com>

pkgname=roxterm
pkgver=3.3.1
pkgrel=1
pkgdesc='Tabbed, VTE-based terminal emulator'
arch=('i686' 'x86_64')
url='http://roxterm.sourceforge.net/'
license=('GPL3')
depends=('dbus-glib' 'vte3' 'hicolor-icon-theme' 'libsm')
makedepends=('docbook-xsl' 'xmlto' 'po4a' 'python2' 'python2-lockfile'
             'imagemagick' 'librsvg' 'itstool')
install=roxterm.install
source=("http://downloads.sourceforge.net/roxterm/roxterm-$pkgver.tar.xz"
        "http://downloads.sourceforge.net/roxterm/roxterm-$pkgver.tar.xz.sign")
md5sums=('c52b89434a4f185a7625c1e1f0802509'
         'SKIP')
sha1sums=('1452d60ef6d3bf41ba1bad0efe43266a3d5d7455'
          'SKIP')
validpgpkeys=('2FF283656D0745E54850B1C0BF0EBCD13D97CD09') # Tony Houghton

build() {
  cd "${srcdir}/roxterm-${pkgver}"

  python2 mscript.py configure --prefix='/usr'
  python2 mscript.py build
}

package() {
  cd "${srcdir}/roxterm-${pkgver}"
  python2 mscript.py install --destdir="${pkgdir}"
}

# vim:set ts=2 sw=2 et:

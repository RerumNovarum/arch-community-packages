# $Id$
# Maintainer: Brad Fanella <bradfanella@archlinux.us>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: tocer.deng <tocer.deng@gmail.com>

pkgname=apvlv
pkgver=0.1.2
pkgrel=2
pkgdesc="A PDF Viewer which behaves like Vim"
arch=('i686' 'x86_64')
url="http://naihe2010.github.com/apvlv/"
license=('GPL')
depends=('gtk2' 'cairo' 'poppler-glib' 'djvulibre')
source=(https://github.com/downloads/naihe2010/$pkgname/$pkgname-${pkgver}-Source.tar.gz apvlv-poppler015.patch poppler-gdk.patch)
md5sums=('381d83aa9c253fac5e0be165fca39222'
         '308b17a563ed470ea47d408f324ab745'
         '076d794ab865e9bd53a5dfd2db1eaa8b')

makedepends=('cmake') 

build() {
  cd $srcdir/$pkgname-$pkgver-Source
  patch -Np1 -i $srcdir/apvlv-poppler015.patch
  patch -Np1 -i $srcdir/poppler-gdk.patch

  mkdir -p build
  cd build
  
  cmake -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DAPVLV_WITH_UMD=no  ..
  make
}
package() {
  cd $srcdir/$pkgname-$pkgver-Source/build

  make DESTDIR=$pkgdir install
}

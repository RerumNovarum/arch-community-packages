# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: garion < garion @ mailoo.org >
# Contributor: leemars

pkgname=ibus-qt
pkgver=1.3.1
pkgrel=8
pkgdesc='IBus qt library and IBus qt input method plugin'
arch=('i686' 'x86_64')
license=('LGPL')
url='http://ibus.googlecode.com'
depends=('qt4' 'ibus' 'icu')
makedepends=('cmake' 'automoc4' 'doxygen')
source=("http://ibus.googlecode.com/files/${pkgname}-${pkgver}-Source.tar.gz"
	0001-Do-not-crash-on-missing-invalid-DISPLAY-envvar.patch)

build() {
  cd "${srcdir}/${pkgname}-${pkgver}-Source"
  patch -p1 -i ../0001-Do-not-crash-on-missing-invalid-DISPLAY-envvar.patch

  mkdir build
  cd build
  
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}-Source/build"
  make DESTDIR="${pkgdir}" install
}
md5sums=('769e8872ca8a59327b2073ce2f142589'
         'c30f52b1a9018132e5ecd0c14616cb65')

# $Id$
# Maintainer: juergen <juergen@archlinux.org>
# Contributor : St√©phane Gaudreault <stephane@archlinux.org>

pkgname=xemacs
pkgver=21.5.31
pkgrel=1
pkgdesc="An highly customizable open source text editor and application development system forked from GNU Emacs"
arch=('i686' 'x86_64')
url="http://www.xemacs.org/"
license=('GPL')
depends=('db' 'libpng' 'libtiff' 'gpm' 'bash' 'ncurses' 'desktop-file-utils' 'libxaw' 'libjpeg')
optdepends=('xorg-fonts-75dpi: X bitmap fonts needed for the interface'
            'xorg-fonts-100dpi: X bitmap fonts needed for the interface')
makedepends=('giflib' 'xbitmaps')
install=${pkgname}.install
source=("http://ftp.xemacs.org/pub/xemacs/beta/$pkgname-$pkgver.tar.gz"
        'xemacs.desktop')
md5sums=('0185fe905d0b8d8d094d9b60cf262d4a'
         'a4d3d5c0aa2c7ce7bec491f809ca3694')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --build="${CHOST}" --prefix=/usr --with-dynamic \
      --without-postgresql --with-athena=xaw \
      --enable-database=berkdb --without-ldap \
      --enable-menubars=lucid --enable-scrollbars=lucid \
      --enable-widgets=athena --enable-dialogs=athena \
      --enable-external-widget \
      --with-jpeg --with-png --with-tiff \
      --with-ncurses --with-pop --with-xfs --disable-sound \
      --infodir=/usr/share/info \
      --with-mule \
      --mandir=/usr/share/man/man1

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make -j1 prefix="${pkgdir}/usr" mandir="${pkgdir}/usr/share/man/man1" infodir="${pkgdir}/usr/share/info" install gzip-el

  rm "${pkgdir}"/usr/bin/{b2m,ctags,etags}
  rm "${pkgdir}"/usr/share/man/man1/{ctags.1,etags.1}

  # fix  FS#7927
  install -d -m755 "${pkgdir}/usr/share/pixmaps"
  install -d -m755 "${pkgdir}/usr/share/applications"
  install -m644 "${srcdir}/${pkgname}.desktop" ${pkgdir}/usr/share/applications/
  ln -sf /usr/lib/xemacs-21.5-b28/etc/xemacs-icon.xpm "${pkgdir}/usr/share/pixmaps/"

  # correct permissions
  chown -R root:root "${pkgdir}"

  rm "${pkgdir}"/usr/share/info/dir
  gzip "${pkgdir}"/usr/share/info/*
  rm "${pkgdir}"/usr/share/info/{info.info.gz,texinfo.info-2.gz,cl.info.gz,texinfo.info-1.gz,texinfo.info.gz,standards.info.gz,widget.info.gz}
}
# vim: ts=2 sw=2 et ft=sh

# $Id$
# Maintainer: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: AkiraYB <brunoyb!yahoo,com,br>

pkgname=minbif
pkgver=1.0.4
pkgrel=4
pkgdesc='An IRC gateway to IM networks that uses libpurple.'
arch=('i686' 'x86_64')
url='http://minbif.im/'
license=('GPL2')
depends=('gnutls' 'libcaca' 'libpurple')
makedepends=('cmake')
optdepends=('xinetd: to run minbif through xinetd')
backup=('etc/minbif/minbif.conf'
        'etc/minbif/minbif.motd')
install='minbif.install'
source=("http://minbif.im/attachments/download/90/${pkgname}-${pkgver}.tar.gz"
        'minbif')
md5sums=('25069fb6015dfb9ddc24ef2e09e74da3'
         '307a6fab42bcaeed11a55b5fb4589778')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make PREFIX=/usr CONF_PREFIX=/etc/minbif ENABLE_MINBIF=ON ENABLE_IMLIB=ON ENABLE_CACA=ON \
       ENABLE_VIDEO=OFF ENABLE_PLUGIN=OFF ENABLE_PAM=ON ENABLE_TLS=ON DEBUG=OFF
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  install -dm0755 "${pkgdir}/var/lib"
  install -dm0770 -o67 -g67 "${pkgdir}/var/lib/minbif"
  install -dm0770 -o67 -g67 "${pkgdir}/var/lib/minbif/users"
  install -Dm0755 "${srcdir}/minbif" "${pkgdir}/etc/rc.d/minbif"

  make DESTDIR="${pkgdir}" install
}

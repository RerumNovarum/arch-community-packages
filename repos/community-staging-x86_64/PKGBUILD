# $Id$
# Maintainer: schuay <jakob.gruber@gmail.com>
# Contributor: quantax -- contact via Arch Linux forum or AUR
# Contributor: Christoph Zeiler <archNOSPAM_at_moonblade.dot.org>

pkgname=pcsxr
pkgver=1.9.92
pkgrel=5
pkgdesc='A Sony PlayStation (PSX) emulator based on the PCSX-df project'
arch=('i686' 'x86_64')
url='http://pcsxr.codeplex.com/'
license=('GPL')
depends=('libcdio' 'libgl' 'libglade' 'libxtst' 'libxv' 'sdl')
makedepends=('mesa')
source=("ftp://ftp.archlinux.org/other/community/pcsxr/pcsxr-${pkgver}.tar.bz2"
        "link_order.patch")

build() {
    cd "$srcdir/$pkgname-$pkgver"
    patch -Np0 < "$srcdir/link_order.patch"
    ./configure --prefix=/usr --enable-libcdio --enable-opengl
    make
}

package() {
    cd "$srcdir/$pkgname-$pkgver"
    make DESTDIR="$pkgdir" install
}

md5sums=('28411aed0b4424f97227d94bdefaec83'
         'd38c38fa0fbb11fe881b4a9a868cf603')

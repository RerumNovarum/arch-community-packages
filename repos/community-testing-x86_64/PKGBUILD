# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-gir-generator
pkgver=0.9.0.20151217
_commit=55cc72169a21e1e9a284926b9d4c0423c5a0afb1
pkgrel=1
pkgdesc='Generate static golang bindings for GObject'
arch=('i686' 'x86_64')
url="https://gitcafe.com/Deepin/go-gir-generator"
license=('GPL3')
depends=('gobject-introspection' 'gcc-libs>=5.3.0-3')
makedepends=('git' 'gcc-go')
source=("git+https://github.com/linuxdeepin/go-gir-generator.git#commit=$_commit")
md5sums=('SKIP')

prepare() {
  export GOPATH="$srcdir/go-gir-generator/vender:$srcdir/go-gir-generator"
}

build() {
  cd go-gir-generator/src/gir-generator
  go build -gccgoflags "$(pkg-config --libs gobject-introspection-1.0) $CFLAGS $LDFLAGS" -o out/gir-generator
}

package() {
  install -Dm 755 go-gir-generator/src/gir-generator/out/gir-generator "${pkgdir}"/usr/bin/gir-generator
}

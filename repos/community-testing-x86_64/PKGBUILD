# $Id$
# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Jaroslaw Swierczynski <swiergot@aur.archlinux.org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>

pkgname=cinelerra-cv
pkgver=2.2
_gitrel=2.2.0
pkgrel=14
epoch=1
pkgdesc="Professional video editing and compositing environment"
arch=('x86_64' 'i686')
url="http://cinelerra.org/"
license=('GPL')
depends=('e2fsprogs' 'libavc1394' 'libiec61883' 'libxv'
         'libtiff' 'mjpegtools' 'fftw' 'a52dec' 'glu'
         'ffmpeg' 'faad2' 'faac' 'openexr>=2.0.0')
makedepends=('git' 'nasm' 'mesa')
options=('!libtool')
source=("$pkgname::git+git://git.cinelerra.org/CinelerraCV.git#branch=rel$_gitrel"
        'v4l1_removal.patch'
        'ffmpeg_api.patch')
md5sums=('SKIP'
         'bfa85e20809429d88eba4ab83e569612'
         'b05ec2fb54e7d02f6167525417802111')

_confit() {
  ./configure --prefix=/usr \
              --with-buildinfo=git/recompile \
              --with-external-ffmpeg \
              --enable-opengl \
              --disable-esd $@
}

prepare() {
  cd "$srcdir/$pkgname"

  # v4l1 removal patch
  patch -Np1 -i "$srcdir/v4l1_removal.patch"

  # new ffmpeg api patch
  patch -Np1 -i "$srcdir/ffmpeg_api.patch"

  # TODO: check if this is still needed (not sure what it does) --schiv
  sed -i -e '/Debian/d' admin/nasm

  # if you don't need OpenGL comment out the next line (and no glu/mesa dep)
  # TODO: kind of forgot about the specifics of this one, recheck --schiv
  sed -i '/\/X11R6/s///' configure.in
}

build() {
  cd "$srcdir/$pkgname"

  # gcc 4.6 workaround
  export CFLAGS+=" -Wwrite-strings -D__STDC_CONSTANT_MACROS"
  export CPPFLAGS="$CFLAGS"

  # remove executable stack
  export LDFLAGS+=" -Wl,-z,noexecstack"

  ./autogen.sh

  # TODO: check if this is still needed (forgot why at all) --schiv
  if [ "$CARCH" = 'x86_64' ]; then
    _confit --disable-mmx
  else
    _confit --enable-mmx
  fi

  make
}

package() {
  cd "$srcdir/$pkgname"

  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
